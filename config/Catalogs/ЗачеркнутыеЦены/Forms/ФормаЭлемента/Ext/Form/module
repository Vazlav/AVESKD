
Процедура КоманднаяПанель1кнЗагрузитьПоказатели(Кнопка)
	
	Если Товар.Количество() > 0 Тогда		
		Режим = РежимДиалогаВопрос.ДаНет;
		Ответ = Вопрос("Очистить существующие строки?", Режим, 0);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Товар.Очистить();
		КонецЕсли;		
	КонецЕсли;
	
	Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗагрузкиПоказателей");
	ФайлСпискаТоваров = Форма.ОткрытьМодально();
	
	Если Не ЗначениеЗаполнено(ФайлСпискаТоваров) Тогда
		Сообщить("Не указан путь к файлу!");
		Возврат;
	КонецЕсли;
	
	ВыбФайл = Новый Файл(ФайлСпискаТоваров); 	
	Если Не ВыбФайл.Существует() Тогда
		Сообщить("Не найден файл " + ФайлСпискаТоваров);
		Возврат;
	КонецЕсли;
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");	
	Исключение
		Сообщить ("Не удалось запустить Excel");
		Возврат;
	КонецПопытки;
  
	Книга = Excel.Workbooks.Open(ФайлСпискаТоваров);	
	Лист = Книга.WorkSheets(1);	
	ВсегоСтрок = Лист.Cells(1,1).SpecialCells(11).Row;
	
	Если ВсегоСтрок > 100 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	АССОРТИМЕНТНЫЙ_ПЛАН.Код как КодТовара,
		               |	АССОРТИМЕНТНЫЙ_ПЛАН.Ссылка как ТоварСсылка
		               |ИЗ
		               |	Справочник.АССОРТИМЕНТНЫЙ_ПЛАН КАК АССОРТИМЕНТНЫЙ_ПЛАН";
		ТЗТоваров = Запрос.Выполнить().Выгрузить();
		ТЗТоваров.Индексы.Добавить("КодТовара");
	Иначе
		ТЗтоваров = Неопределено;
	КонецЕсли;
	
	КолвоПодрядПустыхСтрок = 0;
	Для Строка = 2 По ВсегоСтрок Цикл
		
		ОбработкаПрерыванияПользователя();
		
		Если Строка%50 = 0 Тогда
			Состояние("Обработано: " + Строка + " строк из " + ВсегоСтрок);
		КонецЕсли;
		
		Если КолвоПодрядПустыхСтрок = 10 Тогда
			Сообщить("Встречено минимум 10 подряд строк с пустым значениями. Обработка прервана. ");
			Сообщить("Загружено строк :" + Товар.Количество());
			Прервать;
		КонецЕсли;
		
		
		Поле1 = Лист.Cells(Строка,1).Value;
		Поле2 = Лист.Cells(Строка,2).Value;
		
		КодТовара = ОМ_ТСО.ПолучитьЧислоИзСтроки(Поле1);
		
		Если КодТовара > 0 Тогда
			КолвоПодрядПустыхСтрок = 0;
		КонецЕсли;
		
		Если КодТовара = 0 Тогда
			КолвоПодрядПустыхСтрок = КолвоПодрядПустыхСтрок +1;	
			Продолжить;
		КонецЕсли;		
		
		Цена = ОМ_ТСО.ПолучитьЧислоИзСтроки(Поле2);
		Если ТЗТоваров = Неопределено Тогда
			ТоварСсылка = Справочники.АССОРТИМЕНТНЫЙ_ПЛАН.НайтиПоКоду(КодТовара);		
			Если Не ЗначениеЗаполнено(ТоварСсылка) Тогда			
				Сообщить("Не найден товар по коду: " + КодТовара);
				Продолжить; 			
			КонецЕсли;
			
			НовСтр = Товар.Добавить();
			НовСтр.Товар = ТоварСсылка;
			НовСтр.Цена = Цена;
			
		Иначе
			НайденнаяСтрока = ТЗТоваров.Найти(КодТовара,"КодТовара");
			Если НайденнаяСтрока = Неопределено Тогда
				Сообщить("Не найден товар по коду: " + КодТовара);
				Продолжить; 			
			КонецЕсли;
			
			НовСтр = Товар.Добавить();
			НовСтр.Товар = НайденнаяСтрока.ТоварСсылка;
			НовСтр.Цена = Цена;			
		КонецЕсли;
		
	КонецЦикла;
	
	Excel.Application.Quit();
	
	
КонецПроцедуры
