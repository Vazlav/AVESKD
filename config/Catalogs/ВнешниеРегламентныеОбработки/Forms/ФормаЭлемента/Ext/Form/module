
Процедура КоманднаяПанель1ОткрытьФайл(Кнопка)
	
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФайла.Фильтр = "Внешняя обработка(*.epf)|*.epf";
	
	Если ДиалогФайла.Выбрать() Тогда
		
		ПолноеИмяФайлаОбработки = ДиалогФайла.ПолноеИмяФайла;
		
		Попытка
				
			ВнешняяОбработка = ВнешниеОбработки.Создать(ПолноеИмяФайлаОбработки);
			
			Если НЕ ЗначениеЗаполнено(Наименование) Тогда
				Наименование = ВнешняяОбработка.Метаданные().Синоним;
			КонецЕсли;
			
		Исключение
			
			СообщениеОшибки = "Выбранный файл не является внешней обработкой.
							  |Либо, данная обработка не предназначена для
							  |запуска в этой конфигурации.";
			
			Сообщить(ОписаниеОшибки());
			Предупреждение(СообщениеОшибки);
			
			Возврат;
			
		КонецПопытки;
		
	Иначе
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ДиалогФайла.ПолноеИмяФайла);
	ИмяФайла = Файл.Имя;
	
	ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ПолноеИмяФайлаОбработки));
	
КонецПроцедуры

Процедура КоманднаяПанель1СохранитьФайл(Кнопка)
	
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогФайла.Фильтр = "Внешняя обработка(*.epf)|*.epf";
	
	ДвоичныеДанные = ХранилищеВнешнейОбработки.Получить();
	
	Если ДвоичныеДанные = Неопределено Тогда
		Предупреждение("Внешний файл отсутствует в хранилище"); 
		Возврат;
	КонецЕсли; 
	
	ДиалогФайла.ПолноеИмяФайла = ИмяФайла;
	Если ДиалогФайла.Выбрать() Тогда
		ПолноеИмяФайла = ДиалогФайла.ПолноеИмяФайла;
		Попытка
			ДвоичныеДанные.Записать(ПолноеИмяФайла);
		Исключение
			Предупреждение("Внешний файл не сохранен
			|"+ОписаниеОшибки()); 
		КонецПопытки; 
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель1УдалитьФайл(Кнопка)
	
	ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(Неопределено);
	ИмяФайла = "";
	
КонецПроцедуры

