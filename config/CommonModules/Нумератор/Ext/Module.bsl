&НаСервереБезКонтекста
Функция Соединение()
	Если ВнешниеИсточникиДанных.Нумератор.ПолучитьСостояние() = СостояниеВнешнегоИсточникаДанных.Отключен Тогда
		ПараметрыСоединенияСеанса = Новый ПараметрыСоединенияВнешнегоИсточникаДанных;
		ПараметрыСоединенияСеанса.АутентификацияОС = Истина;
		ПараметрыСоединенияСеанса.АутентификацияСтандартная = Ложь;
		ПараметрыСоединенияСеанса.СУБД = "PostgresSQL";
		ПараметрыСоединенияСеанса.СтрокаСоединения = "dsn=ave_skd;trusted_connection=1";
		ВнешниеИсточникиДанных.Нумератор.УстановитьПараметрыСоединенияСеанса(ПараметрыСоединенияСеанса);
		ВнешниеИсточникиДанных.Нумератор.УстановитьСоединение();
	КонецЕсли;
	Возврат ?( ВнешниеИсточникиДанных.Нумератор.ПолучитьСостояние() = СостояниеВнешнегоИсточникаДанных.Подключен
	,	Истина
	,	Неопределено);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОписаниеНумератора() Экспорт
	Возврат ?( Соединение() = Неопределено
	,	Неопределено
	,	ВнешниеИсточникиДанных.Нумератор.Таблицы.ave_skd_public_seq_default_stat.НайтиПоПолю("sequence_name","default"));
КонецФункции

&НаСервереБезКонтекста
Функция Следующий() Экспорт
	Возврат ?( Соединение() = Неопределено
	,	Неопределено
	,	ВнешниеИсточникиДанных.Нумератор.public_default_nextval());
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПоследний() Экспорт
	ОписаниеНумератора = ПолучитьОписаниеНумератора();
	Возврат ?( ОписаниеНумератора = Неопределено
	,	Неопределено
	,	ОписаниеНумератора.last_value);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПоследнийВСессии() Экспорт
	Возврат ?( Соединение() = Неопределено
	,	Неопределено
	,	ВнешниеИсточникиДанных.Нумератор.public_default_currval());
КонецФункции