
Процедура ОбменСИнтернетМагазинами() Экспорт
	
	Драгстор_ВыгрузкаСпискаАптек();
	//Драгстор_ВыгрузкаСпискаТоваров();
	
КонецПроцедуры

Процедура ОбменСДрагСторВнутридневной() Экспорт
	
	//Драгстор_ВыгрузкаПрайсЛистаПоАптекам();
	//Драгстор_ВыгрузкаПрайсЛистаПоСлужбеДоставки();
	
КонецПроцедуры


/////////////////////////////////////////////////////////////
// ДРАГСТОР

Процедура Драгстор_ВыгрузкаСпискаАптек()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыАптек.Аптека КАК АптекаСсылка,
	|	КодыАптек.Код КАК КодАптеки,
	|	ЕСТЬNULL(КодыАптек.Аптека.РежимРаботы.Наименование, """") КАК РежимРаботы,
	|	ЕСТЬNULL(КодыАптек.Аптека.Бренд.Наименование, """") КАК Бренд,
	|	КодыАптек.Аптека.ТелефонДляСправки КАК Телефон,
	|	ЕСТЬNULL(КодыАптек.Аптека.СубъектРФ.Наименование, """") КАК СубъектРФ,
	|	ЕСТЬNULL(КодыАптек.Аптека.Город.Наименование, """") КАК Город,
	|	КодыАптек.Аптека.Улица КАК Улица,
	|	КодыАптек.Аптека.Дом КАК Дом
	|ИЗ
	|	Справочник.КодыАптек КАК КодыАптек
	|ГДЕ
	|	КодыАптек.Владелец = &Контрагент
	|	И КодыАптек.Аптека <> ЗНАЧЕНИЕ(Справочник.МестаХранения.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Контрагент", Справочники.Поставщики.НайтиПоНаименованию("DS-tech"));

	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ИмяФайла = "aptekiave.csv";
	ПутьКФайлу = КаталогВременныхФайлов() + ИмяФайла;
	
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку("code;region;city;street;house;brandname;working;weekend;work24;phone"); //заголовок
	
	Пока Выборка.Следующий() Цикл
		
		//ИДАптеки = Строка(Выборка.АптекаСсылка.УникальныйИдентификатор());
		//НаименованиеАптеки = "Аптека №" + Формат(Выборка.НомерАптеки, "ЧДЦ=0; ЧН=; ЧГ=0");
		
		Если Выборка.РежимРаботы = "круглосуточно" Тогда
			working = "00:00-24:00";
			weekend = "00:00-24:00";
			work24 = "1";
		ИначеЕсли Выборка.РежимРаботы = "с 09-00 до 18-00, сб., вс -вых" Тогда
			working = "09:00-18:00";
			weekend = "00:00-00:00";
			work24 = "0";
		ИначеЕсли Выборка.РежимРаботы = "Закрыта на ремонт" Тогда
			working = "00:00-00:00";
			weekend = "00:00-00:00";
			work24 = "0";
		ИначеЕсли Выборка.РежимРаботы = "будни 8-22, вых 9-21" Тогда
			working = "08:00-22:00";
			weekend = "09:00-21:00";
			work24 = "0";
		Иначе
			working = СтрЗаменить(СтрЗаменить(СтрЗаменить(Выборка.РежимРаботы, "-", ":"), " до ", "-"), "с ", "");
			weekend = "00:00-00:00";
			work24 = "0";
		КонецЕсли;
		
		code		= Формат(Выборка.КодАптеки, "ЧДЦ=0; ЧН=; ЧГ=0");		
		region		= СокрЛП(Выборка.СубъектРФ);
		city		= СокрЛП(Выборка.Город);
		street		= СокрЛП(Выборка.Улица);
		house		= СокрЛП(Выборка.Дом);
		brandname	= СокрЛП(Выборка.Бренд);
		phone		= СтрЗаменить(СокрЛП(Выборка.Телефон), ";", ",");

		Текст.ДобавитьСтроку(code + ";" + region + ";" + city + ";" + street + ";" + house + ";" 
							+ brandname + ";" + working + ";" + weekend + ";" + work24+ ";" + phone);

	КонецЦикла;
	
	Текст.Записать(ПутьКФайлу);
	
	Сервер = Новый FTPСоединение("tabsnab.ru", 21, "ftpuser366", "GtZNugAszu4O", , Истина);
	Сервер.Записать(ПутьКФайлу, ИмяФайла);	
	
	УдалитьФайлы(ПутьКФайлу);
	
КонецПроцедуры

Процедура Драгстор_ВыгрузкаСпискаТоваров()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СвязкиТовараСПоставщиком.ТоварФирмы.Код КАК КодТовара,
	|	СвязкиТовараСПоставщиком.ТоварФирмы.Наименование КАК НаименованиеТовара,
	|	СвязкиТовараСПоставщиком.КодТовараПоставщика,
	|	ЕСТЬNULL(СвязкиТовараСПоставщиком.ТоварФирмы.Производитель.Наименование, """") КАК НаименованиеПроизводителя
	|ИЗ
	|	Справочник.СвязкиТовараСПоставщиком КАК СвязкиТовараСПоставщиком
	|ГДЕ
	|	НЕ СвязкиТовараСПоставщиком.ПометкаУдаления
	|	И НЕ СвязкиТовараСПоставщиком.Блокировка
	|	И СвязкиТовараСПоставщиком.Поставщик = &Поставщик";
	
	Запрос.УстановитьПараметр("Поставщик", Справочники.Поставщики.НайтиПоНаименованию("DS-tech"));

	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ИмяФайла = "сodtovar.csv";
	ПутьКФайлу = КаталогВременныхФайлов() + ИмяФайла; 	
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку("code;code_1s;name_1s;producer");
	
	ИмяФайла2 = "tovar.csv";
	ПутьКФайлу2 = КаталогВременныхФайлов() + ИмяФайла2; 	
	Текст2 = Новый ТекстовыйДокумент;
	Текст2.ДобавитьСтроку("code;name_1s;producer");
	
	Пока Выборка.Следующий() Цикл
	         				
		code		= Формат(Выборка.КодТовараПоставщика, "ЧДЦ=0; ЧН=; ЧГ=0");
		code_1s		= Формат(Выборка.КодТовара, "ЧДЦ=0; ЧН=; ЧГ=0");
		name_1s		= СокрЛП(Выборка.НаименованиеТовара);
		producer	= СокрЛП(Выборка.НаименованиеПроизводителя);

		Текст.ДобавитьСтроку(code + ";" + code_1s + ";" + name_1s + ";" + producer);
		Текст2.ДобавитьСтроку(code + ";" + name_1s + ";" + producer);

	КонецЦикла;
	
	Текст.Записать(ПутьКФайлу);	
	Текст2.Записать(ПутьКФайлу2);		

	Сервер = Новый FTPСоединение("tabsnab.ru", 21, "ftpuser366", "GtZNugAszu4O", , Истина);
	Сервер.Записать(ПутьКФайлу, ИмяФайла);
	Сервер.Записать(ПутьКФайлу2, ИмяФайла2);
	
	УдалитьФайлы(ПутьКФайлу);
	УдалитьФайлы(ПутьКФайлу2);
	
КонецПроцедуры

Процедура Драгстор_ВыгрузкаПрайсЛистаПоАптекам()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыАптек.Аптека
	|ПОМЕСТИТЬ втВсеАптеки
	|ИЗ
	|	Справочник.КодыАптек КАК КодыАптек
	|ГДЕ
	|	КодыАптек.Владелец = &Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СменаККМТовар.Ссылка.Склад КАК Аптека
	|ПОМЕСТИТЬ втАктивныеАптеки
	|ИЗ
	|	Документ.СменаККМ.Товар КАК СменаККМТовар
	|ГДЕ
	|	СменаККМТовар.Ссылка.Склад В
	|			(ВЫБРАТЬ
	|				вт.Аптека
	|			ИЗ
	|				втВсеАптеки КАК вт)
	|	И СменаККМТовар.ДатаОткрытияЧека > &МинимальнаяДатаПоследнегоЧека
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Аптека
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязкиТовараСПоставщиком.ТоварФирмы КАК ТоварФирмы,
	|	МАКСИМУМ(СвязкиТовараСПоставщиком.КодТовараПоставщика) КАК КодТовараПоставщика
	|ПОМЕСТИТЬ втКодыПоставщиков
	|ИЗ
	|	Справочник.СвязкиТовараСПоставщиком КАК СвязкиТовараСПоставщиком
	|ГДЕ
	|	НЕ СвязкиТовараСПоставщиком.ПометкаУдаления
	|	И НЕ СвязкиТовараСПоставщиком.Блокировка
	|	И СвязкиТовараСПоставщиком.Поставщик = &Контрагент
	|	И НЕ СвязкиТовараСПоставщиком.ТоварФирмы.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	СвязкиТовараСПоставщиком.ТоварФирмы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТоварФирмы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииЖНВЛСОстатки.Склад.Код КАК КодСклада,
	|	ПартииЖНВЛСОстатки.Товар.Код КАК КодТовара,
	|	СУММА(ПартииЖНВЛСОстатки.КолвоОстаток / ВЫБОР
	|			КОГДА ПартииЖНВЛСОстатки.Партия.К = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПартииЖНВЛСОстатки.Партия.К
	|		КОНЕЦ) КАК Количество,
	|	ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток / ПартииЖНВЛСОстатки.КолвоОстаток * ВЫБОР
	|		КОГДА ПартииЖНВЛСОстатки.Партия.К = 0
	|			ТОГДА 1
	|		ИНАЧЕ ПартииЖНВЛСОстатки.Партия.К
	|	КОНЕЦ КАК Цена,
	|	втКодыПоставщиков.КодТовараПоставщика
	|ИЗ
	|	РегистрНакопления.ПартииЖНВЛС.Остатки(
	|			,
	|			Склад В
	|					(ВЫБРАТЬ
	|						вт.Аптека
	|					ИЗ
	|						втАктивныеАптеки КАК вт)
	|				И Товар В
	|					(ВЫБРАТЬ
	|						вт.ТоварФирмы
	|					ИЗ
	|						втКодыПоставщиков КАК вт)) КАК ПартииЖНВЛСОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКодыПоставщиков КАК втКодыПоставщиков
	|		ПО ПартииЖНВЛСОстатки.Товар = втКодыПоставщиков.ТоварФирмы
	|ГДЕ
	|	ПартииЖНВЛСОстатки.Партия.СрокГодности > &МинимальныйСрокГодности
	|	И ПартииЖНВЛСОстатки.КолвоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииЖНВЛСОстатки.Склад.Код,
	|	ПартииЖНВЛСОстатки.Товар.Код,
	|	ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток / ПартииЖНВЛСОстатки.КолвоОстаток * ВЫБОР
	|		КОГДА ПартииЖНВЛСОстатки.Партия.К = 0
	|			ТОГДА 1
	|		ИНАЧЕ ПартииЖНВЛСОстатки.Партия.К
	|	КОНЕЦ,
	|	втКодыПоставщиков.КодТовараПоставщика
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодСклада,
	|	КодТовара,
	|	Цена УБЫВ
	|ИТОГИ ПО
	|	КодСклада,
	|	КодТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втВсеАптеки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втАктивныеАптеки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втКодыПоставщиков";
	
	Запрос.УстановитьПараметр("Контрагент", Справочники.Поставщики.НайтиПоНаименованию("DS-tech"));
	Запрос.УстановитьПараметр("МинимальнаяДатаПоследнегоЧека", ТекущаяДата() - 28800); //-8 часов
	Запрос.УстановитьПараметр("МинимальныйСрокГодности", ДобавитьМесяц(ТекущаяДата(), 3));

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПоАптекам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПоАптекам.Следующий() Цикл
		
		apt_code = Формат(ВыборкаПоАптекам.КодСклада, "ЧДЦ=0; ЧН=; ЧГ=0");
		
		ИмяФайла = "apt" + apt_code + ".csv";
		ПутьКФайлу = КаталогВременныхФайлов() + ИмяФайла;
		
		Текст = Новый ТекстовыйДокумент;		
		Текст.ДобавитьСтроку("apt_code;goods_code;qnt;price"); //заголовок
		
		ВыборкаПоТоварам = ВыборкаПоАптекам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			ВыборкаДетальныеЗаписи = ВыборкаПоТоварам.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				goods_code	= Формат(ВыборкаДетальныеЗаписи.КодТовараПоставщика, "ЧДЦ=0; ЧН=; ЧГ=0");
				qnt			= Формат(Цел(ВыборкаДетальныеЗаписи.Количество), "ЧДЦ=0; ЧН=; ЧГ=0");
				price		= Формат(ВыборкаДетальныеЗаписи.Цена, "ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				
				Текст.ДобавитьСтроку(apt_code + ";" + goods_code + ";" + qnt + ";" + price); 
				
				// Выгружаем только партию с наибольшей ценой
				Прервать;
				
			КонецЦикла; 
			
		КонецЦикла;
		
		Текст.Записать(ПутьКФайлу);
		
	КонецЦикла;

	Сервер = Новый FTPСоединение("tabsnab.ru", 21, "ftpuser366", "GtZNugAszu4O", , Истина);
	Сервер.Записать(ПутьКФайлу, ИмяФайла);	
	
	УдалитьФайлы(ПутьКФайлу);
	
КонецПроцедуры

Процедура Драгстор_ВыгрузкаПрайсЛистаПоСлужбеДоставки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МестаХранения.Ссылка КАК Аптека
	|ПОМЕСТИТЬ втВсеАптеки
	|ИЗ
	|	Справочник.МестаХранения КАК МестаХранения
	|ГДЕ
	|	МестаХранения.Код В (182, 183, 184, 1000)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СменаККМТовар.Ссылка.Склад КАК Аптека
	|ПОМЕСТИТЬ втАктивныеАптеки
	|ИЗ
	|	Документ.СменаККМ.Товар КАК СменаККМТовар
	|ГДЕ
	|	СменаККМТовар.Ссылка.Склад В
	|			(ВЫБРАТЬ
	|				вт.Аптека
	|			ИЗ
	|				втВсеАптеки КАК вт)
	|	И СменаККМТовар.ДатаОткрытияЧека > &МинимальнаяДатаПоследнегоЧека
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Аптека
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязкиТовараСПоставщиком.ТоварФирмы КАК ТоварФирмы,
	|	МАКСИМУМ(СвязкиТовараСПоставщиком.КодТовараПоставщика) КАК КодТовараПоставщика
	|ПОМЕСТИТЬ втКодыПоставщиков
	|ИЗ
	|	Справочник.СвязкиТовараСПоставщиком КАК СвязкиТовараСПоставщиком
	|ГДЕ
	|	НЕ СвязкиТовараСПоставщиком.ПометкаУдаления
	|	И НЕ СвязкиТовараСПоставщиком.Блокировка
	|	И СвязкиТовараСПоставщиком.Поставщик = &Контрагент
	|	И НЕ СвязкиТовараСПоставщиком.ТоварФирмы.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	СвязкиТовараСПоставщиком.ТоварФирмы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТоварФирмы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииЖНВЛСОстатки.Склад.Код КАК КодСклада,
	|	ПартииЖНВЛСОстатки.Товар.Код КАК КодТовара,
	|	СУММА(ПартииЖНВЛСОстатки.КолвоОстаток / ВЫБОР
	|			КОГДА ПартииЖНВЛСОстатки.Партия.К = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПартииЖНВЛСОстатки.Партия.К
	|		КОНЕЦ) КАК Количество,
	|	втКодыПоставщиков.КодТовараПоставщика,
	|	ПартииЖНВЛСОстатки.Товар.МинЦенаMedlux КАК Цена
	|ИЗ
	|	РегистрНакопления.ПартииЖНВЛС.Остатки(
	|			,
	|			Склад В
	|					(ВЫБРАТЬ
	|						вт.Аптека
	|					ИЗ
	|						втАктивныеАптеки КАК вт)
	|				И Товар В
	|					(ВЫБРАТЬ
	|						вт.ТоварФирмы
	|					ИЗ
	|						втКодыПоставщиков КАК вт)) КАК ПартииЖНВЛСОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКодыПоставщиков КАК втКодыПоставщиков
	|		ПО ПартииЖНВЛСОстатки.Товар = втКодыПоставщиков.ТоварФирмы
	|ГДЕ
	|	ПартииЖНВЛСОстатки.Партия.СрокГодности > &МинимальныйСрокГодности
	|	И ПартииЖНВЛСОстатки.КолвоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииЖНВЛСОстатки.Склад.Код,
	|	ПартииЖНВЛСОстатки.Товар.Код,
	|	втКодыПоставщиков.КодТовараПоставщика,
	|	ПартииЖНВЛСОстатки.Товар.МинЦенаMedlux
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодСклада,
	|	КодТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втВсеАптеки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втАктивныеАптеки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втКодыПоставщиков";
	
	Запрос.УстановитьПараметр("Контрагент", Справочники.Поставщики.НайтиПоНаименованию("DS-tech"));
	Запрос.УстановитьПараметр("МинимальнаяДатаПоследнегоЧека", ТекущаяДата() - 28800); //-8 часов
	Запрос.УстановитьПараметр("МинимальныйСрокГодности", ДобавитьМесяц(ТекущаяДата(), 3));

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	
	ИмяФайла = "price" + Формат(ТекущаяДата(), "ДФ=ddMMyyyy") + ".csv";
	ПутьКФайлу = КаталогВременныхФайлов() + "price" + Формат(ТекущаяДата(), "ДФ=ddMMyyyy") + ".csv";
	
	Текст = Новый ТекстовыйДокумент;  	
	Текст.ДобавитьСтроку("apt_code;goods_code;qnt;price"); //заголовок

	Пока Выборка.Следующий() Цикл
		
		apt_code	= Формат(Выборка.КодСклада, "ЧДЦ=0; ЧН=; ЧГ=0");
		goods_code	= Формат(Выборка.КодТовараПоставщика, "ЧДЦ=0; ЧН=; ЧГ=0");
		qnt			= Формат(Цел(Выборка.Количество), "ЧДЦ=0; ЧН=; ЧГ=0");
		price		= Формат(Выборка.Цена, "ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		
		Текст.ДобавитьСтроку(apt_code + ";" + goods_code + ";" + qnt + ";" + price);			
		
	КонецЦикла;
	
	Текст.Записать(ПутьКФайлу);		

	Сервер = Новый FTPСоединение("tabsnab.ru", 21, "ftpuser366", "GtZNugAszu4O", , Истина);
	Сервер.Записать(ПутьКФайлу, ИмяФайла);	
	
	УдалитьФайлы(ПутьКФайлу);
	
КонецПроцедуры
