Функция ВернутьДатуЗапретаРедактирования(Склад)   Экспорт
	
	ОбщаяДатаЗапрета = КонецДня(Константы.ДатаЗапретаРедактирования.Получить());
	
	ТХТ = "ВЫБРАТЬ
	      |	ТоварныеОтчеты.ДатаЗакрытия,
	      |	ТоварныеОтчеты.ОтменаЗапрета
	      |ИЗ
	      |	РегистрСведений.ТоварныеОтчеты КАК ТоварныеОтчеты
	      |ГДЕ
	      |	ТоварныеОтчеты.Аптека = &Аптека";
		  
	Запрос = Новый Запрос;
	Запрос.Текст = ТХТ;
	Запрос.УстановитьПараметр("Аптека",Склад);
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		Возврат ОбщаяДатаЗапрета;
	Иначе
		Выборка = Рез.Выбрать();
		Выборка.Следующий();
		Если Выборка.ОтменаЗапрета = Истина Тогда
			Возврат Дата(2001,01,01);
		Иначе
			ДатаЗакрытияПоАптеке = КонецДня(Выборка.ДатаЗакрытия);
			Возврат  Макс(ДатаЗакрытияПоАптеке,ОбщаяДатаЗапрета);
		КонецЕсли;
	КонецЕсли;
	
	
КонецФункции

Процедура ПроверкаПериодаДокумента(ДокументОбъект,Отказ,РежимЗаписи)
	
	ДатаЗапретаРедактирования = ВернутьДатуЗапретаРедактирования(ДокументОбъект.Склад);
	
	Если Не ДокументОбъект.ЭтоНовый() Тогда
		СтараяВерсияДокумента = ПолучитьВерсиюДокументаПередИзменением(ДокументОбъект);
    	ПроверитьВерсиюДокумента(СтараяВерсияДокумента, ДатаЗапретаРедактирования, Отказ);
	КонецЕсли;
			
	Если Не Отказ Тогда
		ПроверитьВерсиюДокумента(ДокументОбъект, ДатаЗапретаРедактирования, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает из БД версию документа до его изменения
//
Функция ПолучитьВерсиюДокументаПередИзменением(ДокументОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ 
	|Дата, 
	|Проведен КАК Проведен	
	|ИЗ Документ." + ДокументОбъект.Метаданные().Имя + "
	|ГДЕ Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка;
	
КонецФункции // ПолучитьВерсиюДокументаПередИзменением()

// Процедура проверки версии документа на нарушение даты запрета
//
Процедура ПроверитьВерсиюДокумента(ДокументОбъект, ДатаЗапретаРедактирования, Отказ, РежимЗаписи = Неопределено) Экспорт
	
	
	Если ДокументОбъект.Дата <= ДатаЗапретаРедактирования	Тогда
		Отказ = Истина;			
	КонецЕсли;		
	    
	
КонецПроцедуры // ПроверитьВерсиюДокумента()


// Проверка возможности записи данных документа с учетом даты запрета изменения данных (даты запрета редактирования)
//
Процедура ПередЗаписьюДокументовПроверкаДоступностиПериода(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ПараметрыСеанса.Беспредел Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаПериодаДокумента(Источник, Отказ, РежимЗаписи);
	#Если Клиент Тогда
		Если Отказ Тогда
			Сообщить("Редактирование данных этого периода запрещено. Изменения не могут быть записаны...", СтатусСообщения.Важное);
		КонецЕсли;
	#КонецЕсли
КонецПроцедуры	// ПередЗаписьюДокументовПроверкаДоступностиПериода

Процедура ПередУдалениемРеализацииККМПередУдалением(Источник, Отказ) Экспорт
	
	Если ПараметрыСеанса.Беспредел Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаПериодаДокумента(Источник, Отказ, "");
	#Если Клиент Тогда
		Если Отказ Тогда
			Сообщить("Редактирование данных этого периода запрещено. Изменения не могут быть записаны...", СтатусСообщения.Важное);
		КонецЕсли;
	#КонецЕсли	
	
КонецПроцедуры

Процедура ДоступностьДокументаДляЗаписи(ДатаИзменения,Организация,Отказ)
	
	
	Запрос=Новый Запрос();
	
	Запрос.Текст=  "ВЫБРАТЬ
	               |	АС_ДатаЖесткогоЗапретаИзмененияДанных.Организация,
	               |	КОНЕЦПЕРИОДА(АС_ДатаЖесткогоЗапретаИзмененияДанных.ДатаЗапретаИЗмененияДанных, ДЕНЬ) КАК ДатаЗапретаИЗмененияДанных,
	               |	АС_РазрешениеНаИзмДанВЗП.Пользователь,
	               |	АС_РазрешениеНаИзмДанВЗП.РазрешитьИсправлятьДо,
	               |	ВЫБОР
	               |		КОГДА КОНЕЦПЕРИОДА(&ДатаДокумента, ДЕНЬ) <= КОНЕЦПЕРИОДА(АС_ДатаЖесткогоЗапретаИзмененияДанных.ДатаЗапретаИЗмененияДанных, ДЕНЬ)
	               |			ТОГДА ВЫБОР
	               |					КОГДА АС_РазрешениеНаИзмДанВЗП.Пользователь ЕСТЬ NULL
	               |						ТОГДА ЛОЖЬ
	               |					ИНАЧЕ ВЫБОР
	               |							КОГДА КОНЕЦПЕРИОДА(&ДатаДокумента, ДЕНЬ) МЕЖДУ НАЧАЛОПЕРИОДА(АС_РазрешениеНаИзмДанВЗП.ПериодИсправляемыхДокументовС, ДЕНЬ) И КОНЕЦПЕРИОДА(АС_РазрешениеНаИзмДанВЗП.ПериодИсправляемыхДокументовПо, ДЕНЬ)
	               |								ТОГДА ИСТИНА
	               |							ИНАЧЕ ЛОЖЬ
	               |						КОНЕЦ
	               |				КОНЕЦ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК РазрешеноИзменятьДанные,
	               |	&ДатаДокумента,
	               |	АС_РазрешениеНаИзмДанВЗП.ПериодИсправляемыхДокументовС,
	               |	АС_РазрешениеНаИзмДанВЗП.ПериодИсправляемыхДокументовПо
	               |ИЗ
	               |	РегистрСведений.ДатаЖесткогоЗапретаИзмененияДанных КАК АС_ДатаЖесткогоЗапретаИзмененияДанных
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазрешениеНаИзменениеДанных КАК АС_РазрешениеНаИзмДанВЗП
	               |		ПО АС_ДатаЖесткогоЗапретаИзмененияДанных.Организация = АС_РазрешениеНаИзмДанВЗП.Организация
	               |			И (АС_РазрешениеНаИзмДанВЗП.Пользователь = &Пользователь)
	               |			И (КОНЕЦПЕРИОДА(АС_РазрешениеНаИзмДанВЗП.РазрешитьИсправлятьДо, ДЕНЬ) >= КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ))
	               |ГДЕ
	               |	АС_ДатаЖесткогоЗапретаИзмененияДанных.Организация = &Организация";
	
	
	
	
	
	Запрос.УстановитьПараметр("Пользователь",ПараметрыСеанса.ТекущийСотр);
	Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ДатаДокумента",ДатаИзменения);    
	
	Рез=Запрос.Выполнить().Выгрузить();
	
	Если Рез.Количество()=0 Тогда
		Возврат; // Разрешено ибо нет запрета вообще
	ИначеЕсли Рез.Количество()=1 Тогда
		// есть запрет, но возможно 2 варианта да и нет
		Если Рез.Получить(0).РазрешеноИзменятьДанные=Истина ТОгда
			Возврат;
		Иначе
			#Если Клиент тогда
				Предупреждение("Установлена дата запрета изменения данных ( по  "+Рез.Получить(0).ДатаЗапретаИЗмененияДанных+" ) !!!!
				|по вопросу редактирования документа обращайтесь по адресу SKLAD_SUPPORT@366.ru с темой письма ""Склад"" (без кавычек)",6);
			#КонецЕсли
			
			Отказ=Истина;
			Возврат;
		КонецЕсли;
	ИначеЕсли Рез.Количество()>1 Тогда //есть несколько действующих разрешений
		Возврат; // разрешено
	КонецЕсли;	
	
КонецПроцедуры

Процедура УЗ_ПередЗаписьюДокументаЗапретРедактированияПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ПараметрыСеанса.РазрешитьДействияВЗакрытомПериоде = Истина Тогда 
		Возврат;
	КонецЕсли;		

	
	Ссылка = Источник.Ссылка;
	ТипЗнчСсылка = ТипЗнч(Ссылка);
	ДатаПрошлая = ТекущаяДата();
	ДатаТекущая = ТекущаяДата();
	
	
	
	
	Если ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_ПоступлениеТовара") Тогда
		Если Источник.ЭтоНовый() и НЕ Источник.ДатаФактПоступления = Дата(1,1,1) Тогда //Если сразу новый документ проводят с факт. датой , то пишем в регистр
			ДоступностьДокументаДляЗаписи(Источник.ДатаФактПоступления,Источник.Фирма,Отказ);
		Иначе //Если документ не новый тогда 
			Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
				
				ОрганизацияПрошлая = Ссылка.Фирма;
				ОрганизацияТекущая = Источник.Фирма;
				
				Если Источник.ДатаФактПоступления <> Дата(1,1,1)  Тогда
					ДатаТекущая = Источник.ДатаФактПоступления;
				КонецЕсли;
				
				Если Ссылка.ДатаФактПоступления <> Дата(1,1,1)  Тогда
					ДатаПрошлая = Ссылка.ДатаФактПоступления;
				КонецЕсли;		
				
				ДоступностьДокументаДляЗаписи(Мин(ДатаТекущая,ДатаПрошлая),ОрганизацияПрошлая,Отказ);
				Если ОрганизацияПрошлая <> ОрганизацияТекущая Тогда
					ДоступностьДокументаДляЗаписи(Мин(ДатаТекущая,ДатаПрошлая),ОрганизацияТекущая,Отказ);
				КонецЕсли;
	
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_РеализацияККМ") Тогда
		Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
			ДатаТекущая = Источник.Дата;
			Если НЕ Источник.ЭтоНовый() Тогда
				ДатаПрошлая = Ссылка.Дата;
			КонецЕсли;	
			ДоступностьДокументаДляЗаписи(Мин(ДатаТекущая,ДатаПрошлая),Справочники.Фирмы.НайтиПоКоду(Источник.ФирмаКод),Отказ);
		КонецЕсли;
	ИначеЕсли	ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_Списание") 
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_ВводОстатков")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_ВозвратТовара")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_АрбитражПомещение")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_АрбитражИзъятие")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_ВозвратОтПокупателя")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_Торг2")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_КорректировкаНедовоза")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_Разбраковка")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_МелкооптоваяРеализация")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_Инвентаризация")
			или ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_Трансформация") Тогда
			
		Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда

			Если НЕ Источник.ЭтоНовый() Тогда
				Если Ссылка.Проведен Тогда //оставляем возможность переноса документа непроведенного из закрытого периода в открытый
					ДоступностьДокументаДляЗаписи(Ссылка.Дата,Ссылка.Фирма,Отказ);
				КонецЕсли;
			КонецЕсли;	
			ДоступностьДокументаДляЗаписи(Источник.Дата,Источник.Фирма,Отказ);

			
		КонецЕсли;
	ИначеЕсли ТипЗнчСсылка = Тип("ДокументСсылка.УЗ_Перемещение") Тогда
		
		Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
			//Пишем для отправителя
			Если НЕ Источник.ДатаФактическойОтгрузки = Дата(1,1,1) и Источник.ДатаФактическойОтгрузки <> Ссылка.ДатаФактическойОтгрузки Тогда
				ДоступностьДокументаДляЗаписи(Источник.ДатаФактическойОтгрузки,Источник.ФирмаОтправитель,Отказ); 
			КонецЕсли;
			
			Если (Источник.ДатаФактическойОтгрузки <> Ссылка.ДатаФактическойОтгрузки или Источник.СкладОтправитель <> Ссылка.СкладОтправитель) и НЕ Ссылка.ДатаФактическойОтгрузки = Дата(1,1,1) и НЕ Источник.ЭтоНовый() Тогда
				ДоступностьДокументаДляЗаписи(Ссылка.ДатаФактическойОтгрузки,Ссылка.ФирмаОтправитель,Отказ); 	
			КонецЕсли;
			
			//Пишем для Получателя
			Если НЕ Источник.ДатаЗавоза = Дата(1,1,1) и Источник.ДатаЗавоза <> Ссылка.ДатаЗавоза Тогда
				ДоступностьДокументаДляЗаписи(Источник.ДатаЗавоза,Источник.ФирмаПолучатель,Отказ);
			КонецЕсли;
			
			Если (Источник.ДатаЗавоза <> Ссылка.ДатаЗавоза или Источник.СкладПолучатель <> Ссылка.СкладПолучатель) и НЕ Ссылка.ДатаЗавоза = Дата(1,1,1) и НЕ Источник.ЭтоНовый() Тогда
				ДоступностьДокументаДляЗаписи(Ссылка.ДатаЗавоза,Ссылка.ФирмаПолучатель,Отказ);	
			КонецЕсли;			
			
		КонецЕсли;
		
	КонецЕсли;
	
	
	
КонецПроцедуры

