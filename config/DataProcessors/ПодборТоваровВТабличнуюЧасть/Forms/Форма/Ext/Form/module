
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Построитель.Выполнить();
	МассивТоваров = Построитель.Результат.Выгрузить().ВыгрузитьКолонку("Товар");
	
	Закрыть(МассивТоваров);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьПостроитель();
	
КонецПроцедуры

Процедура ИнициализироватьПостроитель()

	Построитель.Текст = 
	"ВЫБРАТЬ
	|	АССОРТИМЕНТНЫЙ_ПЛАН.Ссылка КАК Товар
	|ИЗ
	|	Справочник.АССОРТИМЕНТНЫЙ_ПЛАН КАК АССОРТИМЕНТНЫЙ_ПЛАН
	|{ГДЕ
	|	АССОРТИМЕНТНЫЙ_ПЛАН.Ссылка.* КАК Товар}";

КонецПроцедуры

Процедура ПередСохранениемЗначений(Отказ)
	
	НастройкиПостроителя = Построитель.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Ложь);
	
КонецПроцедуры

Процедура ПослеВосстановленияЗначений()
	
	Если ТипЗнч(НастройкиПостроителя) = Тип("НастройкиПостроителяОтчета") Тогда
		Построитель.УстановитьНастройки(НастройкиПостроителя, Истина, Ложь, Ложь, Ложь, Ложь);
	КонецЕсли;
		
КонецПроцедуры

Процедура КоманднаяПанельОтбораВвестиСписокКодов(Кнопка)
	
	СписокКодов = Новый СписокЗначений();
	СписокКодовСтрока = "";
	
	Если Не ВвестиСтроку(СписокКодовСтрока, "Введите список кодов", , Истина) Тогда
		Возврат;
	КонецЕсли;	
	
	Для ч = 1 По СтрЧислоСтрок(СписокКодовСтрока) Цикл	
		 
		ТекКод = ОМ_ТСО.УбратьНечисловыеСимволы(СтрПолучитьСтроку(СписокКодовСтрока, ч));
		
		Если ЗначениеЗаполнено(ТекКод) Тогда
		 	СписокКодов.Добавить(Число(ТекКод));
		КонецЕсли;
		 
	КонецЦикла;
	
	ОтборПоКодуТовара = Построитель.Отбор.Найти("ТоварКод");
	
	Если ОтборПоКодуТовара = Неопределено Тогда
		ОтборПоКодуТовара = Построитель.Отбор.Добавить("Товар.Код");
	КонецЕсли;	
	
	ОтборПоКодуТовара.ВидСравнения	= ВидСравнения.ВСписке;
	ОтборПоКодуТовара.Использование	= Истина;
	ОтборПоКодуТовара.Значение		= СписокКодов;
	
КонецПроцедуры
