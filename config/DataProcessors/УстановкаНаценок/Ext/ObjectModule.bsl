
Процедура ОбновитьРегистрНаценок() Экспорт
	
	НаборЗаписей = РегистрыСведений.Наценки.СоздатьНаборЗаписей();
	
	Построитель = Новый ПостроительОтчета;
	Построитель.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АССОРТИМЕНТНЫЙ_ПЛАН.ЖНВЛС КАК ЖНВЛС,
	|	АССОРТИМЕНТНЫЙ_ПЛАН.ТипДляЦО КАК ТипДляЦО,
	|	АССОРТИМЕНТНЫЙ_ПЛАН.Бренд КАК Бренд,
	|	МестаХранения.СубъектРФ КАК СубъектРФ
	|ИЗ
	|	Справочник.АССОРТИМЕНТНЫЙ_ПЛАН КАК АССОРТИМЕНТНЫЙ_ПЛАН,
	|	Справочник.МестаХранения КАК МестаХранения
	|ГДЕ
	|	НЕ АССОРТИМЕНТНЫЙ_ПЛАН.ТипДляЦО ЕСТЬ NULL 
	|	И НЕ АССОРТИМЕНТНЫЙ_ПЛАН.ЖНВЛС ЕСТЬ NULL 
	|	И НЕ АССОРТИМЕНТНЫЙ_ПЛАН.Бренд ЕСТЬ NULL 
	|	И НЕ МестаХранения.СубъектРФ ЕСТЬ NULL";
	
	Построитель.ЗаполнитьНастройки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВариантыНаценок.Ссылка КАК ВариантНаценки,
	|	ВариантыНаценок.НастройкиПостроителя,
	|	ВариантыНаценок.Код КАК Приоритет
	|ИЗ
	|	Справочник.ВариантыНаценок КАК ВариантыНаценок
	|ГДЕ
	|	ВариантыНаценок.НачалоСрокаДействия <= &ТекущаяДата
	|	И (ВариантыНаценок.ОкончаниеСрокаДействия >= &ТекущаяДата
	|			ИЛИ ВариантыНаценок.ОкончаниеСрокаДействия = ДАТАВРЕМЯ(1, 1, 1))
	|	И НЕ ВариантыНаценок.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДата()));
	
	Результат = Запрос.Выполнить();
	
	ВыборкаВариантыНаценки = Результат.Выбрать();
	
	Пока ВыборкаВариантыНаценки.Следующий() Цикл
		
		НастройкиПостроителя = ВыборкаВариантыНаценки.НастройкиПостроителя;
		
		Если СокрЛП(НастройкиПостроителя) <> "" Тогда
			Построитель.УстановитьНастройки(ЗначениеИзСтрокиВнутр(НастройкиПостроителя), Истина, Ложь, Ложь, Ложь, Ложь);		
		КонецЕсли;  		
		
		Построитель.Выполнить();
		Выборка = Построитель.Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаВариантыНаценки);
			
		КонецЦикла;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры