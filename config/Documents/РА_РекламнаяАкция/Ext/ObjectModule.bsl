Перем ВременнаяТЗДляВыводаХЗ;
Перем СправочникиВыгружаемыеЗначением;

Процедура ВывестиЗначениеХранилищаВоВременнуюТЗ(ЗначениеРеквизита,ИмяКол,ГУИД,НомерБлока,НомерСтрокиБлока,ВхRVAL)
	Значение=ЗначениеРеквизита.Получить();
	
	Если ТипЗнч(Значение)=Тип("Массив") И Значение.Количество()=1 Тогда
		Значение=Значение.Получить(0);
		
	КонецЕсли;	
	
	
	Если ТипЗнч(Значение)<>Тип("Массив") Тогда
		Стр=ВременнаяТЗДляВыводаХЗ.Добавить();
		
		Стр.BLOCK=НомерБлока;
		Стр.NSBLOCK=НомерСтрокиБлока;
		Стр.RNAME=ИмяКол;
		
		RVAL=КонвертерRVAL(Значение,ИмяКол,НомерБлока,НомерСтрокиБлока);		
		
		Стр.RVAL=RVAL.Значение;
		Стр.VALT=RVAL.Тип;
		Стр.VLSID =1;
	 //   Стр.VLGUID=ГУИД;
	 
	 	ВхRVAL.Тип=RVAL.Тип; // чтобы в файле в строках где в хранилище значения единственное значение был его тип, а не список значений

	Иначе
		Х=0;
		Для Каждого ЗначениеСписка Из Значение Цикл
			Х=Х+1;
			Стр=ВременнаяТЗДляВыводаХЗ.Добавить();
			
			Стр.BLOCK=НомерБлока;
			Стр.NSBLOCK=НомерСтрокиБлока;
			Стр.RNAME=ИмяКол;
			
			RVAL=КонвертерRVAL(ЗначениеСписка,ИмяКол,НомерБлока,НомерСтрокиБлока);		
			
			Стр.RVAL=RVAL.Значение;
			
			Стр.VALT=RVAL.Тип;
			Стр.VLSID =Х;
			//Стр.VLGUID=ГУИД;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры 

Процедура ДобавитьВременнуюТЗХЗ(ДБФ)
	
	//---------------<Вариант вывода "одно значение списка -> одна строка">---------------------------// GtG // 13.05.2013 10:52:25
	//СписокКолонок= Новый Массив();
	//Для Каждого Кол Из ВременнаяТЗДляВыводаХЗ.Колонки Цикл
	//	СписокКолонок.Добавить(Кол.Имя);
	//КонецЦикла;	
	//
	//Для Каждого Стр Из ВременнаяТЗДляВыводаХЗ Цикл
	//	
	//	ДБФ.Добавить();
	//	
	//	Для Каждого Кол Из СписокКолонок Цикл
	//		Дбф.УстановитьЗначениеПоля(Кол,Стр[Кол]);
	//	КонецЦикла;
	//	
	//	ДБФ.Записать();
	//КонецЦикла;	
	
	//---------------<Вариант вывода " все значения списка в одну длинную строку, с разбивкой её по 254 символа и выводом этих частей в строки">---------------------------// GtG // 13.05.2013 10:53:38
	ДлиннаяСтрока="";
	
	Для каждого стр из ВременнаяТЗДляВыводаХЗ цикл
		BLOCK=Стр.BLOCK;
		NSBLOCK=Стр.NSBLOCK;
		RNAME=Стр.RNAME;
		VALT=стр.VALT;
		
		ДлиннаяСтрока=ДлиннаяСтрока+СокрЛП(Стр.RVAL)+",";
	КонецЦикла;	
	
	ДлиннаяСтрока=Лев(ДлиннаяСтрока,СтрДлина(ДлиннаяСтрока)-1);// отрежем последнюю запятую
	
	Если СокрЛП(ДлиннаяСтрока)="" Тогда
		ДлиннаяСтрока="";
	Иначе	
		ДлиннаяСтрока="("+СокрЛП(ДлиннаяСтрока)+")";
	КонецЕсли;
	
	
	МассивСтрок254=Новый Массив;
	
	Пока СтрДлина(ДлиннаяСтрока)>0 Цикл
	    МассивСтрок254.Добавить( СокрЛП(Лев(ДлиннаяСтрока,254)) ); 
		ДлиннаяСтрока=СокрЛП(Сред(ДлиннаяСтрока,255, СтрДлина(ДлиннаяСтрока)));
	КонецЦикла;	
	
	//---------------<Выводим в ДБФ>---------------------------// GtG // 13.05.2013 11:05:21
	Х=0;
	Для Каждого Элем254 из МассивСтрок254 Цикл
		ДБФ.Добавить();
		ДБФ.BLOCK=BLOCK;
		ДБФ.NSBLOCK=NSBLOCK;
		ДБФ.RNAME=RNAME;
		
		ДБФ.RVAL=Элем254;
		ЕСЛИ ВременнаяТЗДляВыводаХЗ.КОЛИЧЕСТВО()=1 ТОГДА
			ДБФ.VALT=VALT;
		Иначе	 
			ДБФ.VALT="VL"; 
		КонецЕсли;
		
		Х=Х+1;
		ДБФ.VLSID=Х;
		ДБФ.Записать();
		
	КонецЦикла;
	
		
	
	
	
	
	
	
	
КонецПроцедуры	

Функция КонвертерRVAL(ЗначениеРеквизита,ИмяКол,НомерБлока,НомерСтрокиБлока)
	
		
	
	
	
	Если ТипЗнч(ЗначениеРеквизита)=Тип("Число") Тогда
		RVAL=Новый Структура("Значение,Тип",Формат(ЗначениеРеквизита,"ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="),"N");
	ИначеЕсли ТипЗнч(ЗначениеРеквизита)=Тип("Строка") Тогда 
		RVAL=Новый Структура("Значение,Тип",СокрЛП(ЗначениеРеквизита),"S");
	ИначеЕсли ТипЗнч(ЗначениеРеквизита)=Тип("Дата") Тогда 
		RVAL=Новый Структура("Значение,Тип",Формат(ЗначениеРеквизита,"ДФ=yyyyMMddHHmmss"),"D");
	ИначеЕсли ТипЗнч(ЗначениеРеквизита)=Тип("Булево") Тогда 
		RVAL=Новый Структура("Значение,Тип",Формат(ЗначениеРеквизита,"БЛ=0; БИ=1"),"N");
	ИначеЕсли ЗначениеРеквизита=Неопределено Тогда
		RVAL=Новый Структура("Значение,Тип",ЗначениеРеквизита,"S"); // непонятно как конвертировать
	ИначеЕсли ТипЗнч(ЗначениеРеквизита)=Тип("ХранилищеЗначения") Тогда // в хранилище значения может быть любая шняга
		                                                               // задумано для хранения там либо единичного значения либо списка однотипных значений
		ГУИД=Новый УникальныйИдентификатор();
		
		RVAL=Новый Структура("Значение,Тип",ГУИД,"VL");
		ВременнаяТЗДляВыводаХЗ.Очистить();
		ВывестиЗначениеХранилищаВоВременнуюТЗ(ЗначениеРеквизита,ИмяКол,ГУИД,НомерБлока,НомерСтрокиБлока,RVAL);
		
	Иначе
		РеквТипЗнч=ТипЗнч(ЗначениеРеквизита);
		ОбъектМетаданных = Метаданные.НайтиПоТипу(РеквТипЗнч);
		Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
			Если  ЗначениеРеквизита.ПУстая()=ЛОжь Тогда
				
				ПолеПредставления=СправочникиВыгружаемыеЗначением.Получить(ТипЗнч(ЗначениеРеквизита));
				Если Полепредставления=Неопределено Тогда
					RVAL=Новый Структура("Значение,Тип","'{"+Формат(ЗначениеРеквизита.Код,"ЧГ=")+",0}'","S");
				Иначе
					RVAL=Новый Структура("Значение,Тип",Формат(ЗначениеРеквизита[Полепредставления],"ЧГ="),"S");
				КонецЕсли;
			Иначе
				RVAL=Новый Структура("Значение,Тип","","S");//RVAL=Новый Структура("Значение,Тип","'{-1,0}'","S");
			КонецЕсли;
			
		ИначеЕсли Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
			RVAL=Новый Структура("Значение,Тип",ЗначениеРеквизита,"S"); // непонятно как конвертировать
		ИначеЕсли Метаданные.Перечисления.Содержит(ОбъектМетаданных) Тогда
			ЗначРеквДляФайла="";
			если ТипЗнч(ЗначениеРеквизита)=Тип("ПеречислениеСсылка.ОператорыОбъединенияРА") или 
				ТипЗнч(ЗначениеРеквизита)=Тип("ПеречислениеСсылка.АгрегатныеФункцииДляРА")
				тогда
				//---------------<Перечисления выводимые текстом>---------------------------// GtG // 16.07.2013 12:21:25
				Если ЗначениеРеквизита.Пустая()=Ложь Тогда
					
					ИмяМДПер=ЗначениеРеквизита.Метаданные().Имя;
					МДПер=Перечисления[ИмяМДПер];
					ИндПер=МДПер.Индекс(ЗначениеРеквизита);
					ЗначРеквДляФайла=Метаданные.Перечисления[ИмяМДПер].ЗначенияПеречисления.Получить(ИндПер).Имя;
					
					ЗначРеквДляФайла=СтрЗаменить(ЗначРеквДляФайла,"DISTINCT"," DISTINCT");
					
				Иначе
					ЗначРеквДляФайла="";
				КонецЕсли;
					 
					 
				 RVAL=Новый Структура("Значение,Тип",ВРЕГ(ЗначРеквДляФайла),"S");
				 
			//---------------<Перечисления выводимые синонимом>---------------------------// GtG // 16.07.2013 14:34:40 
			ИначеЕсли ТипЗнч(ЗначениеРеквизита)=Тип("ПеречислениеСсылка.ОператорыУсловийПримененияРА") тогда
			    RVAL=Новый Структура("Значение,Тип",ВРЕГ(ЗначениеРеквизита),"S");
				
			иначе
				//---------------<Перечисления выводимые порядком>---------------------------// GtG // 16.07.2013 12:21:50
				Если ЗначениеРеквизита.Пустая()=Ложь Тогда
					ПорядокЗначенияПеречисления=Перечисления[ЗначениеРеквизита.Метаданные().имя].Индекс(ЗначениеРеквизита);
					
					RVAL=Новый Структура("Значение,Тип",ПорядокЗначенияПеречисления,"N");	
				Иначе
					RVAL=Новый Структура("Значение,Тип",-1,"N");
				КонецЕсли;	
			конецесли;
			
		Иначе
			RVAL=Новый Структура("Значение,Тип",ЗначениеРеквизита,"S"); // непонятно как конвертировать
		КонецЕсли;
	КонецЕсли;	
	
	
	Если RVAL.Тип="S" И ИмяКол="ПраваяЧастьХЗ"  ТОгда
		RVAL.Значение="'"+СокрЛП(RVAL.Значение)+"'";
		
		RVAL.Значение=СтрЗаменить(RVAL.Значение,"''{","'{");
		RVAL.Значение=СтрЗаменить(RVAL.Значение,"}''","}'");
	КонецЕсли;
	
	
	Возврат RVAL; // Структура
	
КонецФункции	


Процедура ВыгрузитьТЧВДБФ(НомерБлока,ИмяТЧ,ДБФ)
//---------------<Блок ТЧ Дни Недели>---------------------------// GtG // 07.05.2013 21:16:17
	
	СписокИменКолонок=Новый Массив;
	Для Каждого Кол Из ЭтотОбъект.Метаданные().ТабличныеЧасти[ИмяТЧ].Реквизиты Цикл
	    СписокИменКолонок.Добавить(Кол.Имя);
	КонецЦикла;	
	
	НомерСтрокиБлока=0;
	Для Каждого Стр Из ЭтотОбъект[ИмяТЧ] Цикл
		НомерСтрокиБлока=НомерСтрокиБлока+1;
		
		Для Каждого ИмяКол Из СписокИменКолонок Цикл 
			
			Если ИмяКол="ПраваяЧасть" Тогда 
				Продолжить;
			КонецЕсли;
			
			
			Если ИмяКол<>"ПраваяЧастьХЗ" Тогда 
				
				
				ДБФ.Добавить();
				ДБФ.BLOCK=НомерБлока;
				ДБФ.NSBLOCK=НомерСтрокиБлока;
				ДБФ.RNAME=ИмяКол;
				
				ЗначениеРеквизита=Стр[ИмяКол];
				
				RVAL=КонвертерRVAL(ЗначениеРеквизита,ИмяКол,НомерБлока,НомерСтрокиБлока);		
				
				ДБФ.RVAL=RVAL.Значение;
				ДБФ.VALT=RVAL.Тип;
				
				ДБФ.Записать();
			Иначе
				// это поле-хранилище значения, и оно было выгружено в ВременнаяТЗДляВыводаХЗ
				// добавим содержимое ВременнаяТЗДляВыводаХЗ в ДБФ  . там либо одна строка либо пачка строк из списка значений
				
				ЗначениеРеквизита=Стр[ИмяКол];
				
				RVAL=КонвертерRVAL(ЗначениеРеквизита,ИмяКол,НомерБлока,НомерСтрокиБлока);		
				
				ДобавитьВременнуюТЗХЗ(ДБФ);
				
			КонецЕсли;
			
			
			
		КонецЦикла;
		
	КонецЦикла;	
	
	
	
	
	
КонецПроцедуры	



Процедура Выгрузить(ТестоваяВыгрузка=Ложь) Экспорт
	
	Если ЭтотОбъект.Проведен=Ложь Тогда
		#Если Клиент Тогда
			Предупреждение("Документ не проведен! Выгрузить можно только проведенный документ!");
		#КонецЕсли
		
		Возврат;  // выгружаются только проведенные документы
	КонецЕсли;
	
	//---------------<Описание формата выгрузки>---------------------------// GtG // 08.05.2013 10:21:43
	//	Файл типа DBF
	// Структура файла: Файл имеет фиксированную структуру
	// 1) BLOCK   - Номер блока
	// 2) NSBLOCK - Номер строки блока
	// 3) RNAME   - Имя реквизита
	// 4) RVAL    - Значение реквизита
	// 5) VALT    - Тип Значения реквизита  N-число,S-строка,D-Дата,VL- список значений, в строках после этой идут сами значения
	// 6) VLSID  - ID строки списка значений из хранилища значений (ячейки чему равно условий применения. 
	//             Одна строка - одна запись списка значений для оператора IN, и вообще для всего. Если значение единичное будет одна запись)
	//**// 7) VLGUID - GUID списка значений. По этому гуиду из таблицы можно надрать весь список разом. 
	//             И вачпе все списки заначений одним запросом. Вот.
	//
	//  ВАЖНО!!! в поле RVAL простые типы выгружаются как строковое представление значений 
	//           (дата как YYYYMMDDhhmmss, Числа как число с 2 знаками после запятой, строка как строка, булево как 0-ложь или 1-истина, 
	//           Объектные типы данных выгружаются как коды. Справочники - код справочника, число, Перечисления - как индексы значения в перечислении , число
	//  ЕЩЕ БОЛЕЕ ВАЖНО!!!! Если в документе реквизит объектного типа не заполнен - вместо реального кода или индекса выгружается -1 (минус один).
	//
	//  ОБЕР МЕГА ВАЖНО!!! В таблице условий используется поле типа хранилище значений, 
	//                     где сидят либо единичные значения либо списки однотипных значений из-за ограничения на длину строки в дбф вывод данных из
	//                     хранилища значения идет в следующих строках после строки где попалось это хранилище значения 
	//
	// ДОКУМЕНТ ПРИ ВЫГРУЗКЕ РАЗВОРАЧИВАЕТСЯ В ЛЕНТУ ДАННЫХ, каждая строка табличной части представлена массивом строк файла , 
	// по одной записи на каждую ячейку таблицы табличной части
	//
	// Шапка документа в блоке №1
	// Табличные части: 
	//                СписокОрганизаций // не выгружается, документ подкладывается в аптеки этой организации
	//                СписокАптек       // не выгружается, документ подкладывается в аптеки этой организации 
	//				  ДниНедели блок №2
	//                СписокПодарков №3
	//                УсловияПримененияПострочно №4  // вывод идет так: сначала пачка строк ДБФ по колонкам одной строки условия, 
	//                                                                  затем пачка строк по списку значений из ячеки чему равно
	//                                                                  потом следующая строка документа, затем еще пачка строк условия
	//                УсловияПримененияИтоги     №5
	//--------------------------------------------------------------------
	// При выгрузке формируется файл выгрузки и копируется в папки аптек на FTP сервер в зависимости от условий описанных 
	// на закладке Область действия. 
	
	ДБФ=Новый XBase;
	ДБФ.Кодировка=КодировкаXBase.OEM; // DOS!
	ДБФ.поля.Добавить("BLOCK","N",15,0);
	ДБФ.поля.Добавить("NSBLOCK","N",15,0);
	ДБФ.поля.Добавить("RNAME","S",30,0);
	ДБФ.поля.Добавить("RVAL","S",254,0);
    ДБФ.поля.Добавить("VALT","S",2,0);   //2 буквы макс
	ДБФ.поля.Добавить("VLSID","N",10,0);   
	//ДБФ.поля.Добавить("VLGUID","S",36,0);   
	
	ИмяВременногоФайла=Лев(Новый УникальныйИдентификатор(),8);
	ИмяВременногоФайла=КаталогВременныхФайлов()+""+ИмяВременногоФайла+".DBF";
	ДБФ.СоздатьФайл(ИмяВременногоФайла);
	
	ВременнаяТЗДляВыводаХЗ=Новый ТаблицаЗначений; // таблица для вывода значений хранилищ значения
	Для Каждого Кол Из ДБФ.поля Цикл
		ВременнаяТЗДляВыводаХЗ.Колонки.Добавить(Кол.Имя);
	КонецЦикла;	

	
	СписокРеквизитовНаВыгрузку=Новый Массив;
	СписокРеквизитовНаВыгрузку.Добавить("Дата");
	СписокРеквизитовНаВыгрузку.Добавить("Номер");
	Для Каждого РеквШапки Из ЭтотОбъект.Метаданные().Реквизиты Цикл
		СписокРеквизитовНаВыгрузку.Добавить(РеквШапки.Имя);
	КонецЦикла;	
	
	
	Для Каждого ИмяРекв из СписокРеквизитовНаВыгрузку Цикл
		ДБФ.Добавить();
		ДБФ.BLOCK=1;
		ДБФ.NSBLOCK=1;
		ДБФ.RNAME=ИмяРекв;
		Если Имярекв = "ОбластьДействияПоАптекам" Тогда
			ДБФ.RVAL="0";
			ДБФ.VALT="N";
		Иначе
			ЗначениеРеквизита=ЭтотОбъект[ИмяРекв];
			
			RVAL=КонвертерRVAL(ЗначениеРеквизита,ИмяРекв,1,1);		
			
			ДБФ.RVAL=RVAL.Значение;
			ДБФ.VALT=RVAL.Тип;
		КонецЕсли;
		ДБФ.Записать();
	КонецЦикла;	
	
	//---------------<Табличные части>---------------------------// GtG // 07.05.2013 21:24:33 
	ВыгрузитьТЧВДБФ(2,"ДниНедели",ДБФ);   
	ВыгрузитьТЧВДБФ(3,"Карты",ДБФ);
	ВыгрузитьТЧВДБФ(4,"СписокПодарков",ДБФ);  
	ВыгрузитьТЧВДБФ(5,"УсловияПримененияПострочно",ДБФ);
	ВыгрузитьТЧВДБФ(6,"УсловияПримененияИтоги",ДБФ);

	
	
	
	
	
	 ДБФ.ЗакрытьФайл();
	 
	 
	 //---------------<Распихиваем временный файл по папкам аптек>---------------------------// GtG // 16.07.2013 11:55:31
	 ЗапросСпискаАптек=Новый Запрос;
	 ЗапросСпискаАптек.УстановитьПараметр("Док",ЭтотОбъект.Ссылка);
	   
	 Если ОбластьДействияПоАптекам=Перечисления.ОбластиДействия.ВсеАптеки Тогда
		 ЗапросСпискаАптек.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
		                         |	МестаХранения.Ссылка,
		                         |	МестаХранения.КаталогОбмена как КаталогОбмена
		                         |ИЗ
		                         |	Справочник.МестаХранения КАК МестаХранения
		                         |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РА_РекламнаяАкция.СписокОрганизаций КАК РА_РекламнаяАкцияСписокОрганизаций
		                         |		ПО МестаХранения.Фирма = РА_РекламнаяАкцияСписокОрганизаций.Организация
		                         |ГДЕ
		                         |	РА_РекламнаяАкцияСписокОрганизаций.Ссылка = &Док
		                         |	И (МестаХранения.ДатаЗакрытия > РА_РекламнаяАкцияСписокОрганизаций.Ссылка.Дата
		                         |			ИЛИ МестаХранения.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
		                         |	И МестаХранения.КаталогОбмена <> """"";
		
	ИначеЕсли ОбластьДействияПоАптекам=Перечисления.ОбластиДействия.ПоВсемКромеСпискаАптек Тогда
		ЗапросСпискаАптек.Текст= "ВЫБРАТЬ РАЗЛИЧНЫЕ
		                         |	ВсеАптеки.Ссылка,
		                         |	ВсеАптеки.КаталогОбмена
		                         |ИЗ
		                         |	(ВЫБРАТЬ
		                         |		МестаХранения.Ссылка КАК Ссылка,
		                         |		МестаХранения.КаталогОбмена КАК КаталогОбмена
		                         |	ИЗ
		                         |		Справочник.МестаХранения КАК МестаХранения
		                         |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РА_РекламнаяАкция.СписокОрганизаций КАК РА_РекламнаяАкцияСписокОрганизаций
		                         |			ПО МестаХранения.Фирма = РА_РекламнаяАкцияСписокОрганизаций.Организация
		                         |	ГДЕ
		                         |		РА_РекламнаяАкцияСписокОрганизаций.Ссылка = &Док
		                         |		И (МестаХранения.ДатаЗакрытия > РА_РекламнаяАкцияСписокОрганизаций.Ссылка.Дата
		                         |				ИЛИ МестаХранения.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
		                         |		И МестаХранения.КаталогОбмена <> """") КАК ВсеАптеки
		                         |ГДЕ
		                         |	НЕ ВсеАптеки.Ссылка В
		                         |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		                         |					РА_РекламнаяАкцияСписокАптек.Склад
		                         |				ИЗ
		                         |					Документ.РА_РекламнаяАкция.СписокАптек КАК РА_РекламнаяАкцияСписокАптек
		                         |				ГДЕ
		                         |					РА_РекламнаяАкцияСписокАптек.Ссылка = &Док)"; // Сгенерировано в GtG's Консоль запросов. 16.07.2013 16:56:31
	ИначеЕсли ОбластьДействияПоАптекам=Перечисления.ОбластиДействия.ПоСпискуАптек Тогда
		ЗапросСпискаАптек.Текст= "ВЫБРАТЬ РАЗЛИЧНЫЕ
		                         |	РА_РекламнаяАкцияСписокАптек.Склад,
		                         |	РА_РекламнаяАкцияСписокАптек.Склад.КаталогОбмена КАК КаталогОбмена
		                         |ИЗ
		                         |	Документ.РА_РекламнаяАкция.СписокАптек КАК РА_РекламнаяАкцияСписокАптек
		                         |ГДЕ
		                         |	РА_РекламнаяАкцияСписокАптек.Ссылка = &Док"; // Сгенерировано в GtG's Консоль запросов. 16.07.2013 16:56:31
	
    КонецЕсли;
								 
	СписокКаталоговОбмена=ЗапросСпискаАптек.Выполнить(); 
	
	Если СписокКаталоговОбмена.Пустой() ТОгда
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;
	
	
	ЗипьФайлИмя="ids_mact_"+Формат(ЭтотОбъект.Номер,"ЧГ=0")+"_"+формат(ЭтотОбъект.Дата,"ДФ=yyyyMMdd")+".zip";
	
	ЗипьФайл=КаталогВременныхФайлов()+ЗипьФайлИмя;
	
	КрасивоеИмяВременногоФайла=СтрЗаменить(ЗипьФайл,".zip",".dbf");
	
	ПереместитьФайл(ИмяВременногоФайла,КрасивоеИмяВременногоФайла);
	ИмяВременногоФайла=КрасивоеИмяВременногоФайла;
	
	
	
	ОМ17_ЗапаковатьФайлИСкопироватьЕгоВПапку(ИмяВременногоФайла,ЗипьФайл);
	
	
	ВыбКат=СписокКаталоговОбмена.Выбрать();
	
	
	УдалитьФайлы(ИмяВременногоФайла);

	Если ТестоваяВыгрузка=Ложь Тогда
		
		//---------------<распихиваем файлик по папкам аптек>---------------------------// GtG // 16.07.2013 17:12:40
		Пока ВыбКат.Следующий() Цикл
			
			Попытка
				КопироватьФайл(ЗипьФайл,ВыбКат.КаталогОбмена+ЗипьФайлИмя);
				#Если Клиент Тогда
					Сообщить("Выгружено в файл: "+ВыбКат.КаталогОбмена+ЗипьФайлИмя);
				#КонецЕсли
			Исключение
			КонецПопытки;
			
			
			
		КонецЦикла;
		
		УдалитьФайлы(ЗипьФайл);
		
	Иначе
		
		Сообщить("Выгружено в :    "+ЗипьФайл);
		
	КонецЕсли;
	
	
	 
Конецпроцедуры	

 Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	 ИДРА="РА-"+Формат(Номер,"ЧГ=0")+"-"+Формат(Дата,"ДФ=dd.MM.yyyy");
	 ДатаИзменения = ТекущаяДата();
	 СписокАптек.Свернуть("Склад","");
	 
	 
 КонецПроцедуры
 
 
 СправочникиВыгружаемыеЗначением=Новый Соответствие;
 
 СправочникиВыгружаемыеЗначением.Вставить(Тип("СправочникСсылка.РА_ПараметрыДляРекламныхАкций"),"ИмяРеквизитаВБазеАптечногоМодуля");
 