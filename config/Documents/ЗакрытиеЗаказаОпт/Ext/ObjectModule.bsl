Процедура ЗакрытьЗаказПроизводителя(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Расход.КодТовара КАК КодТовара,
	               |	Расход.Товар КАК Товар,
	               |	СУММА(Расход.Количество) КАК Количество,
	               |	СУММА(Расход.СуммаЗаказа) КАК СуммаЗаказа
	               |ПОМЕСТИТЬ ДокТЧ
	               |ИЗ
	               |	Документ.ЗакрытиеЗаказаОпт.Товар КАК Расход
	               |ГДЕ
	               |	Расход.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Расход.КодТовара,
	               |	Расход.Товар
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДокТЧ.КодТовара КАК КодТовара,
	               |	ДокТЧ.Товар КАК Товар,
	               |	ДокТЧ.Количество КАК Количество,
	               |	ДокТЧ.СуммаЗаказа
	               |ИЗ
	               |	ДокТЧ КАК ДокТЧ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ЗаказПроизводителюОпт.Записывать = Истина;
	Выборка = РезультатЗапроса.Выбрать();
	КодПроизводителя = Производитель.Код;
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ЗаказПроизводителюОпт.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.КодПроизводителя = КодПроизводителя;
		Движение.КодТовара = Выборка.КодТовара;
		Движение.Заказ = ДокументОснование;
		Движение.Количество = Выборка.Количество;
		Движение.СуммаЗаказа = Выборка.СуммаЗаказа;
	КонецЦикла; 
	Движения.Записать();
	
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПроизводителюОстатки.КодТовара КАК КодТовара,
	|	ЗаказПроизводителюОстатки.КоличествоОстаток КАК Остаток
	|ИЗ
	|	РегистрНакопления.ЗаказПроизводителюОпт.Остатки(
	|			,
	|			КодТовара В
	|					(ВЫБРАТЬ
	|						ДокТЧ.КодТовара
	|					ИЗ
	|						ДокТЧ КАК ДокТЧ)
	|				И Заказ = &Заказ) КАК ЗаказПроизводителюОстатки
	|ГДЕ
	|	ЗаказПроизводителюОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Заказ", ДокументОснование);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОст = РезультатЗапроса.Выбрать();
	Пока ВыборкаОст.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Превышение количества по коду  " + Формат(ВыборкаОст.КодТовара,"ЧГ=0") + " , после проведения документа остаток составит " + ВыборкаОст.Остаток;
		Сообщение.Сообщить();
		Отказ = Истина;        
	КонецЦикла; 
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	
	Если РегистрЗакрытияЗаказа = Перечисления.РегистрЗакрытияЗаказа.ЗаказПроизводителяОпт Тогда
		ЗакрытьЗаказПроизводителя(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатаПоследнегоИзменения = ТекущаяДата();
	
	ОбщегоНазначения.ЗаписатьСменуСостоянияДокумента(Ссылка,РежимЗаписи,ПометкаУдаления);	
	
КонецПроцедуры

