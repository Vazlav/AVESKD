 
Процедура ПроверитьНаЗаполнение(Отказ)
	


	Если НЕ Товар.Найти(0,"Коэфф") = Неопределено Тогда
			#Если Клиент Тогда
				Сообщить("В документе есть строки с коэффициентами =0!
					|Это недопустимо!
					|Очевидно проблемы с единицами товаров.");
				ПроведениеЗакончено=Истина;
			#КонецЕсли
			Отказ = Истина;
			Возврат ;
	КонецЕсли;

	Если НЕ Товар.Найти(0,"ЦенаЗакупБезНДС") = Неопределено Тогда
		// Есть строки с 0-ми
		#Если Клиент Тогда
			Сообщить("В документе есть строки без закуп. цены!
			|Это недопустимо!
			|Укажите цену закупочную!");
		#КонецЕсли
		ПроведениеЗакончено=Истина;

		Отказ = ИСТИНА;
		Возврат ;
	КонецЕсли;

	
КонецПроцедуры


Процедура ПодготовитьТаблицыДвижений(ТаблицыДвижений)
	
	КачествоТовараХТ = Перечисления.УЗ_КачествоТовара.Индекс(Перечисления.УЗ_КачествоТовара.ХорошийТовар);
	//КачествоТовараНедовоз = Перечисления.УЗ_КачествоТовара.Индекс(Перечисления.УЗ_КачествоТовара.Недовоз);
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("СкладКод",Склад.Код);
	Запрос.УстановитьПараметр("ФирмаКод",Фирма.Код);
	Запрос.УстановитьПараметр("Склад",Склад);
	Запрос.УстановитьПараметр("КачествоТовараХТ",КачествоТовараХТ);
	//Запрос.УстановитьПараметр("КачествоТовараНедовоз",КачествоТовараНедовоз);
	
	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	&СкладКод КАК СкладКод,
	               |	&ФирмаКод КАК ФирмаКод,
	               |	Партии.ВидПоступления КАК ВидПоступленияПорядок,
	               |	ТЧТовар.КодПартии КАК ПартияКод,
	               |	ТЧТовар.КодТовара КАК ТоварКод,
	               |	ТЧТовар.Количество * ТЧТовар.Коэфф КАК Количество,
	               |	ТЧТовар.Коэфф,
	               |	ТЧТовар.СтавкаНДСЗакуп,
	               |	ТЧТовар.СуммаЗакупБезНДС
	               |ПОМЕСТИТЬ ВТТовары
	               |ИЗ
	               |	Документ.УЗ_КорректировкаНедовоза.Товар КАК ТЧТовар
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК Партии
	               |		ПО (Партии.Код = ТЧТовар.КодПартии)
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
				   |// 1. Приход партий :  УЗ_Партии
	               |ВЫБРАТЬ
	               |	ВТТовары.СкладКод КАК СкладКод,
	               |	ВТТовары.ФирмаКод КАК ФирмаКод,
	               |	ВТТовары.ВидПоступленияПорядок КАК ВидПоступленияПорядок,
	               |	ВТТовары.ПартияКод,
	               |	ВТТовары.ТоварКод,
	               |	ВТТовары.СтавкаНДСЗакуп,
	               |	ВТТовары.СуммаЗакупБезНДС,
	               |	ВТТовары.Количество,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	&Дата КАК Период
	               |ИЗ
	               |	ВТТовары КАК ВТТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
				   |// 2. Расход недовоза из партии недовоза :  УЗ_ПартииНедовоз
	               |ВЫБРАТЬ
	               |	ВТТовары.СкладКод КАК СкладКод,
	               |	ВТТовары.ФирмаКод КАК ФирмаКод,
	               |	ВТТовары.ВидПоступленияПорядок КАК ВидПоступленияПорядок,
	               |	ВТТовары.ПартияКод,
	               |	ВТТовары.ТоварКод,
	               |	ВТТовары.СтавкаНДСЗакуп,
	               |	ВТТовары.СуммаЗакупБезНДС,
	               |	ВТТовары.Количество,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	&Дата КАК Период
	               |ИЗ
	               |	ВТТовары КАК ВТТовары
	               |
	               |;
	               |
				   |// 3. Приход хорошего в ТО  :  УЗ_ТоварныйОтчет
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТТовары.СкладКод,
	               |	ВТТовары.ФирмаКод,
	               |	&КачествоТовараХТ КАК КачествоТовараПорядок,
	               |	ВТТовары.ВидПоступленияПорядок,
	               |	ВТТовары.СтавкаНДСЗакуп КАК СтавкаНДС,
	               |	СУММА(ВТТовары.СуммаЗакупБезНДС) КАК СуммаЗакупБезНДС,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	&Дата КАК Период,
	               |	0 КАК СуммаОкругления
	               |ИЗ
	               |	ВТТовары КАК ВТТовары
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТТовары.СкладКод,
	               |	ВТТовары.ФирмаКод,
	               |	ВТТовары.ВидПоступленияПорядок,
	               |	ВТТовары.СтавкаНДСЗакуп
	               |
	               |;
	               |
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТТовары";
				   
			Результат = Запрос.ВыполнитьПакет();	   
			ТаблицыДвижений.Вставить("УЗ_Партии",				                        Результат[1].Выгрузить());
			ТаблицыДвижений.Вставить("УЗ_ПартииНедовоз",		                        Результат[2].Выгрузить());
			ТаблицыДвижений.Вставить("УЗ_ТоварныйОтчет",			                    Результат[3].Выгрузить());

				   
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, Режим)
	 
	 
	ТаблицыДвижений = Новый Структура();
	
	ПодготовитьТаблицыДвижений(ТаблицыДвижений);
	
	Таблица= ТаблицыДвижений.УЗ_Партии;
	Движения.УЗ_Партии.Записывать = Истина;
	Движения.УЗ_Партии.Загрузить(Таблица);	
	
	Таблица= ТаблицыДвижений.УЗ_ПартииНедовоз;
	Движения.УЗ_ПартииНедовоз.Записывать = Истина;
	Движения.УЗ_ПартииНедовоз.Загрузить(Таблица);		
	
	Таблица= ТаблицыДвижений.УЗ_ТоварныйОтчет;
	Движения.УЗ_ТоварныйОтчет.Записывать = Истина;
	Движения.УЗ_ТоварныйОтчет.Загрузить(Таблица);
	
	
	ПоместитьВОбменСкладБух();
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатаПоследнегоИзменения = ТекущаяДата();
	СуммаЗакупБезНДС = Товар.Итог("СуммаЗакупБезНДС");
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверитьНаЗаполнение(Отказ);	
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ОМ41_ПередУдалениемДокумента  (ЭтотОбъект,Отказ);
		Если Отказ = Истина Тогда
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	
	ОбщегоНазначения.ЗаписатьСменуСостоянияДокумента(Ссылка,РежимЗаписи,ПометкаУдаления);
	
КонецПроцедуры

Процедура ПоместитьВОбменСкладБух() Экспорт

	МЗ = РегистрыСведений.ОбменСкладБух.СоздатьМенеджерЗаписи();
	МЗ.ВидДокумента = 217;
	МЗ.ДатаОчередиСклад = Дата;
	МЗ.КодФирмы = Фирма.Код;
	МЗ.СсылкаТХТ = XMLСтрока(Ссылка);
	МЗ.КодСклада = Склад.Код;
	МЗ.КодКонтрагента = ?(ЗначениеЗаполнено(Поставщик) , Поставщик.Код, 0);
	МЗ.Объект = Ссылка;
	МЗ.Проведен = Истина;
	МЗ.ПомеченНаУдаление = Ложь;
	МЗ.Записать();
	
	Если ЗначениеЗаполнено(ДокументПоступления) Тогда
		Если НЕ ДокументПоступления.ФирмаКомитент.Пустая() Тогда
			МЗ = РегистрыСведений.ОбменСкладБух.СоздатьМенеджерЗаписи();
			МЗ.ВидДокумента = 217;
			МЗ.ДатаОчередиСклад = Дата;
			МЗ.КодФирмы = ДокументПоступления.ФирмаКомитент.Код;
			МЗ.СсылкаТХТ = XMLСтрока(Ссылка);
			МЗ.КодСклада = Склад.Код;
			МЗ.КодКонтрагента = ?(ЗначениеЗаполнено(Поставщик) , Поставщик.Код, 0);
			МЗ.Объект = Ссылка;
			МЗ.Проведен = Истина;
			МЗ.ПомеченНаУдаление = Ложь;
			МЗ.Записать();			
		КонецЕсли;
	КонецЕсли;

	
КонецПроцедуры
