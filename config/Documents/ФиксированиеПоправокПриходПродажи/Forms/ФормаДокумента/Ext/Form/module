
Процедура КоманднаяПанель1ЗаполнитьПоказатели(Кнопка)
	
	//ТСО {10.03.2015 #0014 изм}
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КоэффициентыПриходПродажи.Поставщик,
	|	КоэффициентыПриходПродажи.Товар,
	|	КоэффициентыПриходПродажи.НачДата,
	|	КоэффициентыПриходПродажи.КонДата,
	|	КоэффициентыПриходПродажи.ПриходРасход,
	|	КоэффициентыПриходПродажи.Коэффициент
	|ПОМЕСТИТЬ втКоэффициенты
	|ИЗ
	|	РегистрСведений.КоэффициентыПриходПродажи КАК КоэффициентыПриходПродажи
	|ГДЕ
	|	(КоэффициентыПриходПродажи.НачДата МЕЖДУ &НачалоПериода И &КонецПериода
	|			ИЛИ КоэффициентыПриходПродажи.КонДата МЕЖДУ &НачалоПериода И &КонецПериода)
	|	И КоэффициентыПриходПродажи.Коэффициент <> 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииЖНВЛСОбороты.Склад,
	|	ПартииЖНВЛСОбороты.Товар,
	|	СУММА(ПартииЖНВЛСОбороты.КолвоПриход * (ЕСТЬNULL(втКоэффициенты.Коэффициент, втКоэффициентыПоВсемПоставщикам.Коэффициент) - 1)) КАК ПриходКоличество,
	|	СУММА(ПартииЖНВЛСОбороты.КолвоРасход * ЕСТЬNULL(втКоэффициенты.Коэффициент, втКоэффициентыПоВсемПоставщикам.Коэффициент)) КАК РасходКоличество,
	|	СУММА(ПартииЖНВЛСОбороты.СуммаЗакупСНДСПриход * (ЕСТЬNULL(втКоэффициенты.Коэффициент, втКоэффициентыПоВсемПоставщикам.Коэффициент) - 1)) КАК ПриходЗакупочнаяСумма,
	|	СУММА(ПартииЖНВЛСОбороты.СуммаРознСНДСПриход * (ЕСТЬNULL(втКоэффициенты.Коэффициент, втКоэффициентыПоВсемПоставщикам.Коэффициент) - 1)) КАК ПриходРозничнаяСумма,
	|	СУММА(ПартииЖНВЛСОбороты.СуммаЗакупСНДСРасход * (ЕСТЬNULL(втКоэффициенты.Коэффициент, втКоэффициентыПоВсемПоставщикам.Коэффициент) - 1)) КАК РасходЗакупочнаяСумма,
	|	СУММА(ПартииЖНВЛСОбороты.СуммаРознСНДСРасход * (ЕСТЬNULL(втКоэффициенты.Коэффициент, втКоэффициентыПоВсемПоставщикам.Коэффициент) - 1)) КАК РасходРозничнаяСумма
	|ИЗ
	|	РегистрНакопления.ПартииЖНВЛС.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Товар В
	|				(ВЫБРАТЬ
	|					вт.Товар
	|				ИЗ
	|					втКоэффициенты КАК вт)) КАК ПартииЖНВЛСОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКоэффициенты КАК втКоэффициенты
	|		ПО (ПартииЖНВЛСОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТовара)
	|			И (ВЫРАЗИТЬ(ПартииЖНВЛСОбороты.Регистратор КАК Документ.ПоступлениеТовара).Поставщик = втКоэффициенты.Поставщик)
	|			И (втКоэффициенты.ПриходРасход = ""+"")
	|			И ПартииЖНВЛСОбороты.Товар = втКоэффициенты.Товар
	|			И (ПартииЖНВЛСОбороты.Период МЕЖДУ втКоэффициенты.НачДата И втКоэффициенты.КонДата)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКоэффициенты КАК втКоэффициентыПоВсемПоставщикам
	|		ПО (ПартииЖНВЛСОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТовара
	|					И втКоэффициентыПоВсемПоставщикам.ПриходРасход = ""+""
	|				ИЛИ ПартииЖНВЛСОбороты.Регистратор ССЫЛКА Документ.РеализацияККМ
	|					И втКоэффициентыПоВсемПоставщикам.ПриходРасход = ""-"")
	|			И ПартииЖНВЛСОбороты.Товар = втКоэффициентыПоВсемПоставщикам.Товар
	|			И (ПартииЖНВЛСОбороты.Период МЕЖДУ втКоэффициентыПоВсемПоставщикам.НачДата И втКоэффициентыПоВсемПоставщикам.КонДата)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииЖНВЛСОбороты.Склад,
	|	ПартииЖНВЛСОбороты.Товар";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Движения.ПопровкиПриходПродажи.Записывать = Истина;
	Движения.ПопровкиПриходПродажи.Очистить();
	
	Пока Выборка.Следующий() Цикл 
		Движение = Движения.ПопровкиПриходПродажи.Добавить();
		Движение.Период = Дата;
		
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДатаПриИзменении(Элемент)
	
	Если Дата <> НачалоМесяца(Дата) Тогда
		Дата = НачалоМесяца(Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЭтоНовый() Тогда 		
		Дата = НачалоМесяца(ДобавитьМесяц(ТекущаяДата(), -1));		
	КонецЕсли;
	
КонецПроцедуры


