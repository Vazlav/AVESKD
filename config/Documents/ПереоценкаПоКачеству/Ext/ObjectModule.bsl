Функция КорректировкаСпецСимволов(Значение)
	
	//Возврат Значение;
	
   Результат = СтрЗаменить(Значение, "&", "&amp;");
   Результат = СтрЗаменить(Результат, "<", "&lt;");
   Результат = СтрЗаменить(Результат, ">", "&gt;");
   Результат = СтрЗаменить(Результат, """", "&quot;");
   Результат = СтрЗаменить(Результат, "'", "&apos;");
   Результат = СтрЗаменить(Результат, "/", "&#x2F;");	
   Возврат Результат;
   
КонецФункции

Процедура ЗаписатьЭлементXML(ЗаписьXML, Имя, Значение) 
	
	Если Значение = "" Тогда
		ЗаписьXML.ЗаписатьСтроку("<" + Имя + "/>");
	Иначе
		ЗаписьXML.ЗаписатьСтроку("<" + Имя + ">" + Значение + "</" + Имя + ">");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьНачалоЭлемента(ЗаписьXML,Имя)
	
	ЗаписьXML.ЗаписатьСтроку("<" + Имя + ">");
	
КонецПроцедуры

Процедура ЗаписатьКонецЭлемента(ЗаписьXML,Имя)
	
	ЗаписьXML.ЗаписатьСтроку("</" + Имя + ">");
	
КонецПроцедуры

Процедура УстановитьИндивидуальныеЦены()
	
	Если ПричинаПереоценки = Перечисления.ПричиныПереоценки.НекорректнаяЦена Тогда
		Уценка = Ложь;
	Иначе
		Уценка = Истина;
	КонецЕсли;
	
	СкладКод = Склад.Код;
	ТекДата = ТекущаяДата();
	Для каждого стр из Товар Цикл
		Если стр.Количество > 0 Тогда
			МЗ = РегистрыСведений.РозничныеЦеныПоПартиям.СоздатьМенеджерЗаписи();
			МЗ.АптекаКод = СкладКод;
			МЗ.ТоварКод = стр.КодТовара;
			МЗ.ПартияКод = стр.КодПартииНовый;
			МЗ.ДатаУстановки =  ТекДата;
			МЗ.Цена = стр.ЦенаИндивидуальнаяНовая;
			МЗ.Коэффициент = стр.КоэффициентРазбивки;
			МЗ.СтавкаНДС = стр.СтавкаНДС;
			МЗ.Документ = Ссылка;
			МЗ.НомерДокумента = Номер;
			МЗ.Уценка = Уценка;
			МЗ.Записать();
			
			РЦПО = РегистрыСведений.РозничныеЦеныПоПартиямОтложенные.СоздатьМенеджерЗаписи();
			РЦПО.АптекаКод = СкладКод;
			РЦПО.ТоварКод = стр.КодТовара;
			РЦПО.ПартияКод = стр.КодПартииНовый;
			РЦПО.Прочитать();
			Если РЦПО.Выбран() Тогда
				ЗаполнитьЗначенияСвойств(РЦПО,МЗ);
				РЦПО.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СгенерироватьНовыеПартии()
	
	//Пара = Новый Соответствие;
	спрПартии = Справочники.УЗ_Партии;

	Для каждого стр из Товар Цикл
		Если стр.Количество > 0 Тогда
			//СуществующийКодПартии = Пара.Получить(стр.КодПартии);
			//Если НЕ СуществующийКодПартии = Неопределено Тогда
			//	стр.КодПартииНовый = СуществующийКодПартии; 
			//	Продолжить;
			//КонецЕсли;
			Если стр.КодПартииНовый = 0 Тогда
				ИсходнаяПартия = спрПартии.НайтиПоКоду(стр.КодПартии);
				НоваяПартия = ИсходнаяПартия.Скопировать();
				НоваяПартия.УстановитьНовыйКод();
				Если ПричинаПереоценки = Перечисления.ПричиныПереоценки.РасформированиеИнтернетЗаказа Тогда
					НоваяПартия.ТипПартии = "";
				КонецЕсли;				
				//Барданов А.Ю. 21.03.2019 ENT-1418 +++ 
				Если ИсходнаяПартия.ПартияРодительКод = 0 Тогда
					НоваяПартия.ПартияРодительКод = стр.КодПартии; 
				Иначе
					НоваяПартия.ПартияРодительКод = ИсходнаяПартия.ПартияРодительКод;
				КонецЕсли;
				//---
				НоваяПартия.Записать();
				стр.КодПартииНовый = НоваяПартия.Код;
				//Пара.Вставить(стр.КодПартии,стр.КодПартииНовый);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Функция ЕстьНулеваяЦена()
	ЕстьНуль = Ложь;
	Для каждого стр из Товар Цикл
		Если стр.ЦенаИндивидуальнаяНовая = 0 и стр.Количество > 0 Тогда
			#Если Клиент Тогда
				Сообщить("по товару "+стр.Товар + " не установлена розничная цена.");	
			#КонецЕсли
			ЕстьНуль = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьНуль;
	
КонецФункции

Функция ВыгрузитьДокумент()
	
	ИмяФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьXML = Новый ЗаписьТекста(ИмяФайла,"windows-1251");
	
	
	ЗаписьXML.ЗаписатьСтроку("<?xml version=""1.0"" encoding=""WINDOWS-1251""?>");

	ЗаписьXML.ЗаписатьСтроку("<document>"); 

	
	ЗаписатьЭлементXML(ЗаписьXML, "pack_type", "IO_REVAL"); 
	ЗаписатьЭлементXML(ЗаписьXML, "fmt_ver", "1"); 

		
		ЗаписьXML.ЗаписатьСтроку("<hdr>");
			     ЗаписатьЭлементXML(ЗаписьXML, "id_doc_type", 	"207"); 
				 ЗаписатьЭлементXML(ЗаписьXML, "guid", ИДДокументаАптеки); 
				 Если СтатусПереоценки = Перечисления.СтатусыПереоценки.Аннулирован Тогда
				 	ЗаписатьЭлементXML(ЗаписьXML, "status",	Перечисления.СтатусДокАптеки.Индекс(Перечисления.СтатусДокАптеки.Аннулирован)); 
				 Иначе
					ЗаписатьЭлементXML(ЗаписьXML, "status",	Перечисления.СтатусДокАптеки.Индекс(Перечисления.СтатусДокАптеки.Проведен)); 
				КонецЕсли;
				Если НЕ СозданВАптеке Тогда
					ЗаписатьЭлементXML(ЗаписьXML, "ndoc",		Формат(Номер,"ЧГ=0")); 
					ЗаписатьЭлементXML(ЗаписьXML, "ddoc",		Формат(Дата,"ДФ=dd.MM.yyyy"));	
				КонецЕсли;
				ЗаписатьЭлементXML(ЗаписьXML, "pos_qty",		Формат(Товар.Количество(),"ЧН=; ЧГ=0")); 
				ЗаписатьЭлементXML(ЗаписьXML, "id_doc_subtype",	Формат(Перечисления.ПричиныПереоценки.Индекс(ПричинаПереоценки)," ЧН=0")); 
				ЗаписатьЭлементXML(ЗаписьXML, "dsc_office", 		КорректировкаСпецСимволов(СокрЛП(Комментарий)));	
				ЗаписатьЭлементXML(ЗаписьXML, "dsc_dep", 			КорректировкаСпецСимволов(СокрЛП(КомментарийАптеки)));
				ЗаписатьЭлементXML(ЗаписьXML, "is_bo_created", 		Число(НЕ СозданВАптеке));	
	  	ЗаписьXML.ЗаписатьСтроку("</hdr>"); //конец записи секции  "hdr"
		
		Если НЕ СтатусПереоценки = Перечисления.СтатусыПереоценки.Аннулирован Тогда
			ЗаписьXML.ЗаписатьСтроку("<str>");
			Для каждого ВыборкаСтроки из Товар Цикл
				ЗаписьXML.ЗаписатьСтроку("<row>");
				ЗаписатьЭлементXML(ЗаписьXML, "guid_gpart"	,Формат(ВыборкаСтроки.КодПартии,"ЧГ=0; ЧН=0")); 
				ЗаписатьЭлементXML(ЗаписьXML, "guid_gpart_new"	,Формат(ВыборкаСтроки.КодПартииНовый,"ЧГ=0; ЧН=0")); 
				ЗаписатьЭлементXML(ЗаписьXML, "id_gpart"		,Формат(ВыборкаСтроки.КодПартииАптеки,"ЧГ=0")); 
				ЗаписатьЭлементXML(ЗаписьXML, "qnt"				,Формат(ВыборкаСтроки.Количество*ВыборкаСтроки.Коэфф,"ЧГ=0; ЧН=0")); 
				ЗаписатьЭлементXML(ЗаписьXML, "coeff"			,Формат(ВыборкаСтроки.КоэффициентРазбивки,"ЧГ=0; ЧН=0")); 
				ЗаписатьЭлементXML(ЗаписьXML, "cost_rtl_w_vat_new"	,Формат(ВыборкаСтроки.ЦенаИндивидуальнаяНовая/ВыборкаСтроки.КоэффициентРазбивки,"ЧРД=.; ЧН=0; ЧГ=0")); 
				ЗаписьXML.ЗаписатьСтроку("</row>");
			КонецЦикла;
			ЗаписьXML.ЗаписатьСтроку("</str>"); //конец записи секции  "str"
		КонецЕсли;
	
	ЗаписьXML.ЗаписатьСтроку("</document>"); //конец записи секции  "document"
	ЗаписьXML.Закрыть();
	ЗаписьXML = Новый ЧтениеТекста(ИмяФайла,"windows-1251");
	ВесьТекст = ЗаписьXML.Прочитать();
	ЗаписьXML.Закрыть();	
	Попытка
		УдалитьФайлы(ИмяФайла);
	Исключение
		#Если Клиент Тогда
			Сообщить(ОписаниеОшибки());	
		#КонецЕсли
	КонецПопытки;

	
	КодСклада = Склад.Код;
	//КодСчетчика = ОМ_ТСО.ПолучитьКодСчетчика("ОбменАптекаОфисЦелевые");
	//Если КодСчетчика = -1 Тогда
	//	КодСчетчика = ОМ_ТСО.ПолучитьКодСчетчика("ОбменАптекаОфисЦелевые");
	//	Если КодСчетчика = -1 Тогда
	//		Возврат Ложь;	
	//	КонецЕсли;
	//КонецЕсли;
	
	МЗ = РегистрыСведений.ОфисАптекаЦелевые.СоздатьМенеджерЗаписи();
	МЗ.Код = 1;
	МЗ.КодАптеки = Склад.Код;
	МЗ.ТипУпаковки = "IO_REVAL";
	МЗ.Приоритет = 1;
	МЗ.ВерсияФормата = 1;
	МЗ.ИмяФайла = "io_reval_" + СокрЛП(Формат(КодСклада,"ЧГ=0")) + "_" + СокрЛП(Формат(Номер,"ЧГ=0")) + "_" + Формат(Дата,"ДФ=dd.MM.yyyy") +".xml";
	МЗ.ИдентификаторКодировки = 1;
	МЗ.ХМЛСтрока = ВесьТекст;
	МЗ.ИдентификаторДокумента = ИДДокументаАптеки;
	МЗ.Записать();	
	
	Если НЕ СтатусПереоценки = Перечисления.СтатусыПереоценки.Аннулирован Тогда
		СтатусПереоценки = Перечисления.СтатусыПереоценки.Выгружен;
	КонецЕсли;
	ОбщегоНазначения.ЗаписатьИсториюИзмененияДокумента(Ссылка,"Выгружен в аптеку",ПараметрыСеанса.ТекущийСотр,"Выгружен в аптеку");		
	
	Выгружен = Истина;
	
	Записать(РежимЗаписиДокумента.Запись);
	
	Возврат Истина;
	
КонецФункции

Функция ВыгрузитьВАптеку() Экспорт 
	
	Если НЕ СтатусПереоценки = Перечисления.СтатусыПереоценки.Аннулирован Тогда
		Если СгенерироватьНовыеПартии() = Ложь Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если ЕстьНулеваяЦена() Тогда
			#Если Клиент Тогда
				Предупреждение("В документе есть нулевые цены. Документ не может быть выгружен в аптеку!");	
			#КонецЕсли
			Возврат Ложь;	
		КонецЕсли;
		
	КонецЕсли;	
	
	
	Рез = ВыгрузитьДокумент();
	Возврат Рез;
	
КонецФункции


Процедура ПодготовитьТаблицыДвижений(ТаблицыДвижений)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("СкладКод",Склад.Код);
	Запрос.УстановитьПараметр("ФирмаКод",Склад.Фирма.Код);
	Запрос.УстановитьПараметр("Склад",Склад);
	Запрос.УстановитьПараметр("КачествоТовараПорядок",Перечисления.УЗ_КачествоТовара.Индекс(Перечисления.УЗ_КачествоТовара.ХорошийТовар));
	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	&Дата КАК Период,
	               |	&СкладКод КАК СкладКод,
	               |	&ФирмаКод КАК ФирмаКод,
	               |	Партии.ВидПоступления КАК ВидПоступленияПорядок,
	               |	ТЧТовар.КодПартии КАК ПартияКод,
	               |	ТЧТовар.КодТовара КАК ТоварКод,
	               |	СУММА(ТЧТовар.Количество * ТЧТовар.Коэфф) КАК Количество,
	               |	ТЧТовар.Коэфф,
	               |	Партии.СтавкаНДСЗакуп,
	               |	СУММА(ТЧТовар.ЦенаЗакупБезНДС * ТЧТовар.Количество * ТЧТовар.Коэфф / ТЧТовар.КоэффициентРазбивки) КАК СуммаЗакупБезНДС,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	               |ИЗ
	               |	Документ.ПереоценкаПоКачеству.Товар КАК ТЧТовар
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК Партии
	               |		ПО (Партии.Код = ТЧТовар.КодПартии)
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |	И ТЧТовар.Количество > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Партии.ВидПоступления,
	               |	ТЧТовар.КодПартии,
	               |	ТЧТовар.КодТовара,
	               |	ТЧТовар.Коэфф,
	               |	Партии.СтавкаНДСЗакуп
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Дата,
	               |	&СкладКод,
	               |	&ФирмаКод,
	               |	Партии.ВидПоступления,
	               |	ТЧТовар.КодПартииНовый,
	               |	ТЧТовар.КодТовара,
	               |	СУММА(ТЧТовар.Количество * ТЧТовар.Коэфф),
	               |	ТЧТовар.Коэфф,
	               |	Партии.СтавкаНДСЗакуп,
	               |	СУММА(ТЧТовар.ЦенаЗакупБезНДС * ТЧТовар.Количество * ТЧТовар.Коэфф / ТЧТовар.КоэффициентРазбивки),
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	               |ИЗ
	               |	Документ.ПереоценкаПоКачеству.Товар КАК ТЧТовар
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК Партии
	               |		ПО (Партии.Код = ТЧТовар.КодПартииНовый)
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |	И ТЧТовар.Количество > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Партии.ВидПоступления,
	               |	ТЧТовар.КодПартииНовый,
	               |	ТЧТовар.КодТовара,
	               |	ТЧТовар.Коэфф,
	               |	Партии.СтавкаНДСЗакуп
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Дата КАК Период,
	               |	&СкладКод КАК СкладКод,
	               |	&ФирмаКод КАК ФирмаКод,
	               |	&КачествоТовараПорядок КАК КачествоТовараПорядок,
	               |	Партии.ВидПоступления КАК ВидПоступленияПорядок,
	               |	Партии.СтавкаНДСРозн КАК СтавкаНДС,
	               |	СУММА(ТЧТовар.ЦенаЗакупБезНДС * ТЧТовар.Количество * ТЧТовар.Коэфф / ТЧТовар.КоэффициентРазбивки) КАК СуммаЗакупБезНДС,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	               |ИЗ
	               |	Документ.ПереоценкаПоКачеству.Товар КАК ТЧТовар
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК Партии
	               |		ПО (Партии.Код = ТЧТовар.КодПартии)
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |	И ТЧТовар.Количество > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Партии.ВидПоступления,
	               |	Партии.СтавкаНДСРозн
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Дата,
	               |	&СкладКод,
	               |	&ФирмаКод,
	               |	&КачествоТовараПорядок,
	               |	Партии.ВидПоступления,
	               |	Партии.СтавкаНДСРозн,
	               |	СУММА(ТЧТовар.ЦенаЗакупБезНДС * ТЧТовар.Количество * ТЧТовар.Коэфф / ТЧТовар.КоэффициентРазбивки),
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	               |ИЗ
	               |	Документ.ПереоценкаПоКачеству.Товар КАК ТЧТовар
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК Партии
	               |		ПО (Партии.Код = ТЧТовар.КодПартииНовый)
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |	И ТЧТовар.Количество > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Партии.ВидПоступления,
	               |	Партии.СтавкаНДСРозн";
				   
			Результат = Запрос.ВыполнитьПакет();	   
			ТаблицыДвижений.Вставить("УЗ_Партии",  			Результат[0].Выгрузить());
			ТаблицыДвижений.Вставить("УЗ_ТоварныйОтчет",  	Результат[1].Выгрузить());

				   
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	
	Если Склад.АптекаНаНовомПО Тогда
		
		ТаблицыДвижений = Новый Структура();
		ПодготовитьТаблицыДвижений(ТаблицыДвижений);
		

		Движения.УЗ_Партии.Записывать = Истина;
		Движения.УЗ_Партии.Загрузить(ТаблицыДвижений.УЗ_Партии);	
		
		Движения.УЗ_ТоварныйОтчет.Записывать = Истина;
		Движения.УЗ_ТоварныйОтчет.Загрузить(ТаблицыДвижений.УЗ_ТоварныйОтчет);		
	    
		ОбщегоНазначения.ЗаписатьИсториюИзмененияДокумента(Ссылка,"Проведение",ПараметрыСеанса.ТекущийСотр,"Проведение");

		УстановитьИндивидуальныеЦены();
		
		ОбщегоНазначения.ЗаписатьИсториюИзмененияДокумента(Ссылка,"Установка цен",ПараметрыСеанса.ТекущийСотр,"Установка новых цен");
	КонецЕсли;
	
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ ЗначениеЗаполнено(ПричинаПереоценки) Тогда
		#Если Клиент Тогда
			Предупреждение("Не указана причина переоценки. Выполнение не может быть продолжено");
		#КонецЕсли
		Отказ = истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		#Если Клиент Тогда
			Предупреждение("Не выбрана аптека. Выполнение не может быть продолжено");
		#КонецЕсли
		Отказ = истина;
	КонецЕсли;
	
	Если НЕ СтатусДокАптеки = Перечисления.СтатусДокАптеки.Проведен и РежимЗаписи = РежимЗаписиДокумента.Проведение  Тогда
		#Если Клиент Тогда
			Предупреждение("Проведение документа в офисе возможно только после проведения его в аптеке.");
		#КонецЕсли
		Отказ = истина;
	КонецЕсли;
	
	
	Если ЭтоНовый()  Тогда
		Если ПустаяСтрока(ИДДокументаАптеки) Тогда
			ДокСсылка = ПолучитьСсылкуНового();	
			Если НЕ ЗначениеЗаполнено(ДокСсылка) Тогда
				ДокСсылка = Документы.ПереоценкаПоКачеству.ПолучитьСсылку();	
				УстановитьСсылкуНового(ДокСсылка);
			КонецЕсли;
			ИДДокументаАптеки = XMLСтрока(ДокСсылка);
			
			Автор = ПараметрыСеанса.ТекущийСотр;
		КонецЕсли;
		
		СтатусПереоценки = Перечисления.СтатусыПереоценки.ОжидаетОбработки;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И СтатусДокАптеки = Перечисления.СтатусДокАптеки.Проведен Тогда
		СтатусПереоценки = Перечисления.СтатусыПереоценки.Завершен;
	КонецЕсли;

	ДатаПоследнегоИзменения = ТекущаяДата();
	
	ОбщегоНазначения.ЗаписатьСменуСостоянияДокумента(Ссылка,РежимЗаписи,ПометкаУдаления);
	
КонецПроцедуры



