
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Для Каждого ЗаписьРегистра Из Движения.КорректировкиРасхода Цикл
		ЗаписьРегистра.Период = Дата;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДатаПриИзменении(Элемент)
	
	Если Дата <> НачалоМесяца(Дата) Тогда
		Дата = НачалоМесяца(Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель1ЗаполнитьКорректировки(Кнопка)

	Движения.КорректировкиРасхода.Записывать = Истина;
	Движения.КорректировкиРасхода.Очистить();
	                   
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъемыКорректировокПоТовару.Товар КАК Товар,
	|	ОбъемыКорректировокПоТовару.Товар.Код КАК КодТовара,
	|	СУММА(ОбъемыКорректировокПоТовару.Количество) КАК КолвоКорректировка
	|ПОМЕСТИТЬ втОбъемы
	|ИЗ
	|	РегистрСведений.ОбъемыКорректировокПоТовару КАК ОбъемыКорректировокПоТовару
	|ГДЕ
	|	ОбъемыКорректировокПоТовару.ПериодКорректировки МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ОбъемыКорректировокПоТовару.ПриходРасход = ""-""
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъемыКорректировокПоТовару.Товар,
	|	ОбъемыКорректировокПоТовару.Товар.Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбъемы.Товар КАК Товар,
	|	МАКСИМУМ(втОбъемы.КолвоКорректировка) КАК КолвоКорректировка,
	|	Склады.Ссылка КАК Склад,
	|	1 КАК Колво,
	|	СУММА(ПартииОбороты.КоличествоРасход) КАК КоличествоРасход
	|ИЗ
	|	РегистрНакопления.УЗ_Партии.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			ТоварКод В
	|				(ВЫБРАТЬ
	|					вт.КодТовара
	|				ИЗ
	|					втОбъемы КАК вт)) КАК ПартииОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбъемы КАК втОбъемы
	|		ПО ПартииОбороты.ТоварКод = втОбъемы.КодТовара
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МестаХранения КАК Склады
	|		ПО ПартииОбороты.СкладКод = Склады.Код
	|ГДЕ
	|	ПартииОбороты.Регистратор ССЫЛКА Документ.УЗ_РеализацияККМ
	|
	|СГРУППИРОВАТЬ ПО
	|	втОбъемы.Товар,
	|	Склады.Ссылка
	|
	|ИТОГИ
	|	МАКСИМУМ(КолвоКорректировка),
	|	СУММА(Колво)
	|ПО
	|	Товар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОбъемы";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	
	Пока ВыборкаТовар.Следующий() Цикл
		
		КолвоКорректировкаВсего = ВыборкаТовар.КолвоКорректировка;
		КолвоКорректировкаОстаток = КолвоКорректировкаВсего;			
		КолвоАптекВсего = ВыборкаТовар.Колво;
			
		ТекНомерАптеки = 1;
		
		ВыборкаДетальныеЗаписи = ВыборкаТовар.Выбрать();
		    				
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если ТекНомерАптеки = КолвоАптекВсего Тогда
				КолвоКорректировка = КолвоКорректировкаОстаток;					
			Иначе
				КолвоКорректировкаСреднее = Окр(КолвоКорректировкаОстаток / (КолвоАптекВсего - ТекНомерАптеки + 1));
				ДиапазонС = Макс(Окр(КолвоКорректировкаСреднее * 0.2), 1);
				ДиапазонПо = Макс(ДиапазонС, Окр(КолвоКорректировкаСреднее * 1.8));
				КолвоКорректировкаРасчет = ГСЧ.СлучайноеЧисло(ДиапазонС, ДиапазонПо);
				КолвоКорректировка = Мин(КолвоКорректировкаОстаток, КолвоКорректировкаРасчет);
			КонецЕсли;
			
			Движение = Движения.КорректировкиРасхода.Добавить();
			Движение.Период		= Дата;
			Движение.Товар		= ВыборкаДетальныеЗаписи.Товар;
			Движение.Склад		= ВыборкаДетальныеЗаписи.Склад;
			Движение.Количество	= КолвоКорректировка;
			
			КолвоКорректировкаОстаток = КолвоКорректировкаОстаток - КолвоКорректировка;
			Если КолвоКорректировкаОстаток = 0 Тогда
				Прервать;
			КонецЕсли;
			
			ТекНомерАптеки = ТекНомерАптеки + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанель1ЗаполнитьКорректировкиНовая(Кнопка)
	
	Сообщить("Для корректного распределения корректировок необходимо воспользоваться внешней обработкой 1156 ""Корректировка прихода и расхода""");
	
КонецПроцедуры
