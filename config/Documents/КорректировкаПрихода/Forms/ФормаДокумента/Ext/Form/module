
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Для Каждого ЗаписьРегистра Из Движения.КорректировкиПрихода Цикл
		ЗаписьРегистра.Период = Дата;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДатаПриИзменении(Элемент)
	
	Если Дата <> НачалоМесяца(Дата) Тогда
		Дата = НачалоМесяца(Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель1ЗаполнитьКорректировки(Кнопка)
	
	Движения.КорректировкиПрихода.Записывать = Истина;
	Движения.КорректировкиПрихода.Очистить();
	                   
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъемыКорректировокПоТовару.Товар КАК Товар,
	|	ОбъемыКорректировокПоТовару.Поставщик КАК Поставщик,
	|	СУММА(ОбъемыКорректировокПоТовару.Количество) КАК КолвоКорректировка
	|ПОМЕСТИТЬ втОбъемы
	|ИЗ
	|	РегистрСведений.ОбъемыКорректировокПоТовару КАК ОбъемыКорректировокПоТовару
	|ГДЕ
	|	ОбъемыКорректировокПоТовару.ПериодКорректировки МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ОбъемыКорректировокПоТовару.ПриходРасход = ""+""
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъемыКорректировокПоТовару.Товар,
	|	ОбъемыКорректировокПоТовару.Поставщик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втОбъемы.Товар КАК Ссылка,
	|	втОбъемы.Товар.Код КАК Код
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	втОбъемы КАК втОбъемы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииОбороты.ТоварКод КАК ТоварКод,
	|	ПартииОбороты.СкладКод КАК СкладКод,
	|	Товары.Ссылка КАК Товар,
	|	Склады.Ссылка КАК Склад,
	|	ВЫРАЗИТЬ(ПартииОбороты.Регистратор КАК Документ.УЗ_ПоступлениеТовара).Поставщик КАК Поставщик,
	|	СУММА(ПартииОбороты.КоличествоПриход) КАК КолвоПриход
	|ПОМЕСТИТЬ втОбороты
	|ИЗ
	|	РегистрНакопления.УЗ_Партии.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			ТоварКод В
	|				(ВЫБРАТЬ
	|					вт.Код
	|				ИЗ
	|					втТовары КАК вт)) КАК ПартииОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТовары КАК Товары
	|		ПО ПартииОбороты.ТоварКод = Товары.Код
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МестаХранения КАК Склады
	|		ПО ПартииОбороты.СкладКод = Склады.Код
	|ГДЕ
	|	ПартииОбороты.Регистратор ССЫЛКА Документ.УЗ_ПоступлениеТовара
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Склады.Ссылка,
	|	ВЫРАЗИТЬ(ПартииОбороты.Регистратор КАК Документ.УЗ_ПоступлениеТовара).Поставщик,
	|	ПартииОбороты.ТоварКод,
	|	ПартииОбороты.СкладКод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбороты.Товар КАК Товар,
	|	втОбороты.Склад КАК Склад,
	|	втОбъемы.Поставщик КАК Поставщик,
	|	втОбороты.КолвоПриход КАК КолвоПриход,
	|	втОбъемы.КолвоКорректировка КАК КолвоКорректировка,
	|	1 КАК Колво
	|ИЗ
	|	втОбороты КАК втОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбъемы КАК втОбъемы
	|		ПО втОбороты.Товар = втОбъемы.Товар
	|			И (втОбъемы.Поставщик = ЗНАЧЕНИЕ(Справочник.Поставщики.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОбороты.Товар,
	|	втОбороты.Склад,
	|	втОбъемы.Поставщик,
	|	втОбороты.КолвоПриход,
	|	втОбъемы.КолвоКорректировка,
	|	1
	|ИЗ
	|	втОбъемы КАК втОбъемы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбороты КАК втОбороты
	|		ПО втОбъемы.Товар = втОбороты.Товар
	|			И втОбъемы.Поставщик = втОбороты.Поставщик
	|ИТОГИ
	|	МАКСИМУМ(КолвоКорректировка),
	|	СУММА(Колво)
	|ПО
	|	Товар,
	|	Поставщик";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();

	Пока ВыборкаТовар.Следующий() Цикл
		
		ВыборкаПоставщик = ВыборкаТовар.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоставщик.Следующий() Цикл
			
			КолвоКорректировкаВсего = ВыборкаПоставщик.КолвоКорректировка;
			КолвоКорректировкаОстаток = КолвоКорректировкаВсего;			
			КолвоАптекВсего = ВыборкаПоставщик.Колво;
			
			ТекНомерАптеки = 1;
			
			ВыборкаДетальныеЗаписи = ВыборкаПоставщик.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если ТекНомерАптеки = КолвоАптекВсего Тогда
					КолвоКорректировка = КолвоКорректировкаОстаток;					
				Иначе
					КолвоКорректировкаСреднее = Окр(КолвоКорректировкаОстаток / (КолвоАптекВсего - ТекНомерАптеки + 1));
					ДиапазонС = Макс(Окр(КолвоКорректировкаСреднее * 0.2), 1);
					ДиапазонПо = Макс(ДиапазонС, Окр(КолвоКорректировкаСреднее * 1.8));
					КолвоКорректировкаРасчет = ГСЧ.СлучайноеЧисло(ДиапазонС, ДиапазонПо);
					КолвоКорректировка = Мин(КолвоКорректировкаОстаток, КолвоКорректировкаРасчет);
				КонецЕсли;
				
				Движение = Движения.КорректировкиПрихода.Добавить();
				Движение.Период		= Дата;
				Движение.Товар		= ВыборкаДетальныеЗаписи.Товар;
				Движение.Поставщик	= ВыборкаДетальныеЗаписи.Поставщик;
				Движение.Склад		= ВыборкаДетальныеЗаписи.Склад;
				Движение.Количество	= КолвоКорректировка;
				
				КолвоКорректировкаОстаток = КолвоКорректировкаОстаток - КолвоКорректировка;
				Если КолвоКорректировкаОстаток = 0 Тогда
					Прервать;
				КонецЕсли;
				
				ТекНомерАптеки = ТекНомерАптеки + 1;
				
			КонецЦикла; 			
						
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанель1ЗаполнитьКорректировкиНовая(Кнопка)
	
	Сообщить("Для корректного распределения корректировок необходимо воспользоваться внешней обработкой 1156 ""Корректировка прихода и расхода""");

КонецПроцедуры
