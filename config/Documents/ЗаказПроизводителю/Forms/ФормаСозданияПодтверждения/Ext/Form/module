Процедура ОсновныеДействияФормыСоздатьПодтверждение(Кнопка)

	Форма = ПолучитьФорму("Документ.ПодтверждениеЗаказа.ФормаОбъекта");
	Форма.ДатаПоследнегоИзменения = ДокументОбъект.ДатаПоследнегоИзменения;
	Форма.ДатаПоставки = ДокументОбъект.ДатаПоставки;
	Форма.ИдентификаторEDI = ДокументОбъект.ИдентификаторEDI;
	Форма.Комментарий = ДокументОбъект.Комментарий;
	Форма.Производитель = ДокументОбъект.Производитель;
	Форма.ДокументОснование = ДокументОбъект.Ссылка;
	Для Каждого ТекСтрокаТовар Из ТаблицаСТоварами Цикл
		//Начало НЭТИ Барданов А.Ю. 06.02.2019 ENT-1265
		Если ТекСтрокаТовар.КоличествоКИзменению = 0 Тогда
			Продолжить;
		КонецЕсли;	
		//Конец НЭТИ Барданов А.Ю. 06.02.2019 ENT-1265
		НоваяСтрока = Форма.Товар.Добавить();
		НоваяСтрока.КодТовара = ТекСтрокаТовар.КодТовара;
		НоваяСтрока.Товар = ТекСтрокаТовар.Товар;
		Если ТекСтрокаТовар.Товар.СтавкаНДС.Код = 3 И ДокументОбъект.Дата < Дата(2019, 1, 1) Тогда
			НоваяСтрока.СтавкаНДСПроизводителя = 18;
		Иначе
			НоваяСтрока.СтавкаНДСПроизводителя = ТекСтрокаТовар.Товар.СтавкаНДС.Ставка; 
		КонецЕсли;
		НоваяСтрока.Количество = ТекСтрокаТовар.КоличествоКИзменению;  		
		НоваяСтрока.ЦенаСоСкидкойСНДС = ТекСтрокаТовар.ЦенаКИзменению;
		НоваяСтрока.СуммаСНДС = НоваяСтрока.Количество*НоваяСтрока.ЦенаСоСкидкойСНДС;
		НоваяСтрока.ЦенаСоСкидкойБезНДС = НоваяСтрока.ЦенаСоСкидкойСНДС / (1+НоваяСтрока.СтавкаНДСПроизводителя/100);
		НоваяСтрока.СрокГодности = Дата(1,1,1);
		НоваяСтрока.ЦенаЗаказа = ТекСтрокаТовар.Цена;

	КонецЦикла;
	
	Форма.Открыть(); 
	Закрыть();
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	ЗаполнитьТоварыОстатками(ДокументОбъект.Товар.Выгрузить());
КонецПроцедуры

Процедура ЗаполнитьТоварыОстатками(ТаблицаИсходная)
	
	//Начало НЭТИ Барданов А.Ю. 19.02.2019 ENT-1346 	
	ЗапросСуществующиеЗаписи = Новый Запрос;
	ЗапросСуществующиеЗаписи.Текст = "ВЫБРАТЬ
	                                 |	ЗаказПроизводителюОстатки.КодТовара КАК КодТовара,
	                                 |	СУММА(ЗаказПроизводителюОстатки.КоличествоОстаток) КАК КоличествоОстаток
	                                 |ПОМЕСТИТЬ ТоварыВПути
	                                 |ИЗ
	                                 |	РегистрНакопления.ЗаказПроизводителюОпт.Остатки(&Дата, Заказ = &Ссылка) КАК ЗаказПроизводителюОстатки
	                                 |
	                                 |СГРУППИРОВАТЬ ПО
	                                 |	ЗаказПроизводителюОстатки.КодТовара
	                                 |;
	                                 |
	                                 |////////////////////////////////////////////////////////////////////////////////
	                                 |ВЫБРАТЬ
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.Ссылка) КАК Ссылка,
	                                 |	ЗаказПроизводителюТовар.Товар КАК Товар,
	                                 |	СУММА(ЗаказПроизводителюТовар.Количество) КАК Количество,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.Цена) КАК Цена,
	                                 |	СУММА(ЗаказПроизводителюТовар.Сумма) КАК Сумма,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.КодТовара) КАК КодТовара,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.КоличествоВКоробе) КАК КоличествоВКоробе,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.ЛучшаяЦенаПоставщика) КАК ЛучшаяЦенаПоставщика,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.УсловияВозврата) КАК УсловияВозврата,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.КритическаяДатаВозврата) КАК КритическаяДатаВозврата,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.КодТовараПоставщика) КАК КодТовараПоставщика,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.НаименованиеТовараПоставщика) КАК НаименованиеТовараПоставщика,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.КаталогПрайса) КАК КаталогПрайса,
	                                 |	МАКСИМУМ(ЗаказПроизводителюТовар.НомерЗаписи) КАК НомерЗаписи
	                                 |ПОМЕСТИТЬ ТоварыЗаказа
	                                 |ИЗ
	                                 |	Документ.ЗаказПроизводителю.Товар КАК ЗаказПроизводителюТовар
	                                 |ГДЕ
	                                 |	ЗаказПроизводителюТовар.Ссылка = &Ссылка
	                                 |
	                                 |СГРУППИРОВАТЬ ПО
	                                 |	ЗаказПроизводителюТовар.Товар
	                                 |;
	                                 |
	                                 |////////////////////////////////////////////////////////////////////////////////
	                                 |ВЫБРАТЬ
	                                 |	ТоварыЗаказа.Ссылка КАК Ссылка,
	                                 |	ТоварыЗаказа.Товар КАК Товар,
	                                 |	ЕСТЬNULL(ТоварыВПути.КоличествоОстаток, 0) КАК Количество,
	                                 |	ТоварыЗаказа.Цена КАК Цена,
	                                 |	ЕСТЬNULL(ТоварыВПути.КоличествоОстаток, 0) * ТоварыЗаказа.Цена КАК Сумма,
	                                 |	ТоварыЗаказа.КодТовара КАК КодТовара,
	                                 |	ТоварыЗаказа.КоличествоВКоробе КАК КоличествоВКоробе,
	                                 |	ТоварыЗаказа.ЛучшаяЦенаПоставщика КАК ЛучшаяЦенаПоставщика,
	                                 |	ТоварыЗаказа.УсловияВозврата КАК УсловияВозврата,
	                                 |	ТоварыЗаказа.КритическаяДатаВозврата КАК КритическаяДатаВозврата,
	                                 |	ТоварыЗаказа.КодТовараПоставщика КАК КодТовараПоставщика,
	                                 |	ТоварыЗаказа.НаименованиеТовараПоставщика КАК НаименованиеТовараПоставщика,
	                                 |	ТоварыЗаказа.КаталогПрайса КАК КаталогПрайса,
	                                 |	ТоварыЗаказа.НомерЗаписи КАК НомерЗаписи
	                                 |ИЗ
	                                 |	ТоварыЗаказа КАК ТоварыЗаказа
	                                 |		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыВПути КАК ТоварыВПути
	                                 |		ПО ТоварыЗаказа.КодТовара = ТоварыВПути.КодТовара";
	ЗапросСуществующиеЗаписи.УстановитьПараметр("Дата", ТекущаяДата());
	ЗапросСуществующиеЗаписи.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);

	Выгрузка = ЗапросСуществующиеЗаписи.Выполнить().Выгрузить();
	Для Каждого Элемент Из Выгрузка Цикл
		НоваяСтрокаТаблицыСТоварами = ТаблицаСТоварами.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицыСТоварами, Элемент);
		НоваяСтрокаТаблицыСТоварами.КоличествоКИзменению = Элемент.Количество;
		НоваяСтрокаТаблицыСТоварами.ЦенаКИзменению = Элемент.Цена; 
	КонецЦикла;	
	//Конец НЭТИ Барданов А.Ю. 19.02.2019 ENT-1346	

КонецПроцедуры	

Процедура Товар1ПередУдалением(Элемент, Отказ)
	//Отказ = Истина;
КонецПроцедуры

Процедура Товар1ПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = Истина;
КонецПроцедуры

Процедура ОсновныеДействияФормыОтмена(Кнопка)
	Закрыть();
КонецПроцедуры
