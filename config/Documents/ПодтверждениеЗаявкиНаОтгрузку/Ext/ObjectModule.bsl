
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Расход.КодТовара КАК КодТовара,
	               |	СУММА(Расход.ПодтвержденнаяПотребность) КАК Количество,
	               |	СУММА(Расход.ПодтвержденнаяПотребность*Расход.ЦенаЗакупБезНДСИзЗаявки) КАК СуммаЗакупБезНДС
	               |ПОМЕСТИТЬ ДокТЧ
	               |ИЗ
	               |	Документ.ПодтверждениеЗаявкиНаОтгрузку.Товар КАК Расход
	               |ГДЕ
	               |	Расход.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Расход.КодТовара
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДокТЧ.КодТовара КАК КодТовара,
	               |	ДокТЧ.Количество КАК Количество,
	               |	ДокТЧ.СуммаЗакупБезНДС КАК СуммаЗакупБезНДС
	               |ИЗ
	               |	ДокТЧ КАК ДокТЧ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ЗаявкаНаОптовуюОтгрузку.Записывать = Истина;
	Выборка = РезультатЗапроса.Выбрать();
	КодКлиента = Клиент.Код;
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ЗаявкаНаОптовуюОтгрузку.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.КодКлиента = КодКлиента;
		Движение.КодТовара = Выборка.КодТовара;
		Движение.Заявка = ДокументОснование;
		Движение.Количество = Выборка.Количество;
		Движение.СуммаЗакупБезНДС = Выборка.СуммаЗакупБезНДС;
	КонецЦикла; 
	Движения.Записать();
	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегЗаявка.КодТовара КАК КодТовара,
	               |	РегЗаявка.КоличествоОстаток КАК Остаток
	               |ИЗ
	               |	РегистрНакопления.ЗаявкаНаОптовуюОтгрузку.Остатки(
	               |			,
	               |			КодТовара В
	               |					(ВЫБРАТЬ
	               |						ДокТЧ.КодТовара
	               |					ИЗ
	               |						ДокТЧ КАК ДокТЧ)
	               |				И Заявка = &Заявка) КАК РегЗаявка
	               |ГДЕ
	               |	РегЗаявка.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Заявка", ДокументОснование);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОст = РезультатЗапроса.Выбрать();
	Пока ВыборкаОст.Следующий() Цикл
		#Если Клиент Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Превышение количества по коду  " + Формат(ВыборкаОст.КодТовара,"ЧГ=0") + " , после проведения документа остаток составит " + ВыборкаОст.Остаток;
			Сообщение.Сообщить();
		#КонецЕсли
		Отказ = Истина;        
	КонецЦикла; 
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОМ_ТСО.ЗарегистрироватьОбъектДляВыгрузки(Ссылка, Перечисления.ВидыВыгрузок.ПодтверждениеЗаявкиНаОтгрузку_ВыгрузкаВОриолу);
	    	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатаПоследнегоИзменения = ТекущаяДата();
	СуммаЗакупБезНДС = Товар.Итог("СуммаЗакупБезНДС");
	СуммаОтпускнаяОптБезНДС = Товар.Итог("СуммаОтпускнаяОптБезНДС");
	СуммаОтпускнаяОптСНДС = Товар.Итог("СуммаОтпускнаяОптСНДС");
	
	
	ОбщегоНазначения.ЗаписатьСменуСостоянияДокумента(Ссылка,РежимЗаписи,ПометкаУдаления);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ОМ_ТСО.ЗарегистрироватьОбъектДляВыгрузки(Ссылка, Перечисления.ВидыВыгрузок.ПодтверждениеЗаявкиНаОтгрузку_ВыгрузкаВОриолу, Истина);
	
КонецПроцедуры
