
Процедура ЗаполнитьТаблицу()
	
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МК.СуммаУсловногоБонусаЗаУпаковку + ВЫБОР
	               |		КОГДА МК.ОтменаБУБ = ИСТИНА
	               |			ТОГДА 0
	               |		ИНАЧЕ МК.СуммаБезусловногоБонусаЗаУпаковку
	               |	КОНЕЦ КАК МаркетинговыйБонус,
	               |	МК.КодТовара КАК КодТовара
	               |ПОМЕСТИТЬ ВТМК
	               |ИЗ
	               |	Документ.МаркетинговыеКонтракты.Товар КАК МК
	               |ГДЕ
	               |	МК.Ссылка.НачалоПериода <= &ТекДата
	               |	И МК.Ссылка.КонецПериода >= &ТекДата
	               |	И МК.Ссылка.Проведен = ИСТИНА
	               |	И МК.Ссылка.Производитель = &Производитель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	К.КодТовара КАК КодТовара,
	               |	К.Товар КАК Товар,
	               |	К.СтавкаНДС КАК СтавкаЗакупНДС,
	               |	К.Кратность КАК КоличествоВКоробе,
	               |	К.Товар.Производитель КАК Производитель,
	               |	К.Товар.СтавкаНДС.Ставка КАК СтавкаОптНДС,
	               |	К.БонусОптовый КАК БонусОптовый,
	               |	ВЫРАЗИТЬ(К.ЦенаЗакупБезНДС * (1 - К.БонусДополнительный / 100) КАК ЧИСЛО(15, 2)) КАК ЦенаЗакупБезНДС,
	               |	К.БонусДополнительный КАК БонусДополнительный,
	               |	ВЫРАЗИТЬ((ВЫРАЗИТЬ(К.ЦенаЗакупБезНДС * (1 - К.БонусДополнительный / 100) КАК ЧИСЛО(15, 2))) * К.БонусОптовый / 100 КАК ЧИСЛО(15, 2)) КАК ОптовыйБонус,
	               |	ЕСТЬNULL(ВТМК.МаркетинговыйБонус, 0) КАК МаркетинговыйБонус,
	               |	0 КАК ПотребностьВКоробах,
	               |	0 КАК Потребность,
	               |	0 КАК ЦенаОтпускнаяОптСНДС
	               |ИЗ
	               |	Документ.КонтрактыПроизводителей.Товар КАК К
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТМК КАК ВТМК
	               |		ПО (ВТМК.КодТовара = К.КодТовара)
	               |ГДЕ
	               |	К.Ссылка = &Контракт
	               |	И К.ЦенаЗакупБезНДС > 0";
	
	Запрос.УстановитьПараметр("ТекДата",ТекущаяДата());
	Запрос.УстановитьПараметр("Контракт",Контракт);
	Запрос.УстановитьПараметр("Производитель",Производитель);
	Состав = Запрос.Выполнить().Выгрузить();
	
	
	Для каждого стр из ВладелецФормы.ЭлементыФормы.Товар.Значение Цикл
			НайденнаяСтрока = Состав.Найти(стр.КодТовара,"КодТовара");
			Если НЕ НайденнаяСтрока = Неопределено Тогда	
				НайденнаяСтрока.Потребность = стр.Потребность;
				НайденнаяСтрока.ЦенаОтпускнаяОптСНДС = стр.ЦенаОтпускнаяОптСНДС;
				ПривестиККоробу(НайденнаяСтрока);
			КонецЕсли;
	КонецЦикла;
		
	
	
КонецПроцедуры

Процедура ОсновныеДействияФормыкнПеренестиВДокумент(Кнопка)
	Результат = Истина;
	Для каждого стр из Состав Цикл
		Если стр.ПотребностьВКоробах > 0 И стр.ЦенаОтпускнаяОптСНДС = 0 Тогда
			Сообщить("По товару " + стр.Товар + " не задана отпускная оптовая цена с НДС");	
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если Результат = Истина Тогда
		ОповеститьОВыборе(Состав);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЗаполнитьТаблицу();
	
КонецПроцедуры

Процедура СоставПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.ПотребностьВКоробах > 0 Тогда
		ОформлениеСтроки.ЦветФона=Новый Цвет(152,251,152);
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПривестиККоробу(стр)
	
	Если стр.Потребность > 0 Тогда
		Если стр.КоличествоВКоробе > 0 Тогда
			стр.ПотребностьВКоробах = Окр(стр.Потребность/стр.КоличествоВКоробе ,0)* стр.КоличествоВКоробе;
			стр.ПотребностьВКоробах = ?(стр.ПотребностьВКоробах = 0 , стр.КоличествоВКоробе, стр.ПотребностьВКоробах);
		Иначе
			стр.ПотребностьВКоробах = стр.Потребность;
		КонецЕсли;
	Иначе
		стр.ПотребностьВКоробах = 0;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура СоставПотребностьПриИзменении(Элемент)
	
	стр = ЭлементыФормы.Состав.ТекущаяСтрока;
	ПривестиККоробу(стр);
	
КонецПроцедуры
