
Процедура ПоместитьВОбменСкладБух() Экспорт
	
	Запрос = Новый Запрос;
	запрос.Текст =  "ВЫБРАТЬ РАЗЛИЧНЫЕ
	                |	300 КАК ВидДокумента,
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка.ФирмаКод КАК КодФирмы,
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка.Дата КАК ДатаОчередиСклад,
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка.СкладКод КАК КодСклада,
	                |	0 КАК КодКонтрагента,
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка КАК Объект,
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка.Проведен КАК Проведен,
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка.ПометкаУдаления КАК ПомеченНаУдаление,
	                |	"""" КАК ОшибкаПриЗагрузке,
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка.GuidСмены как СсылкаТХТ
	                |ИЗ
	                |	Документ.УЗ_РеализацияККМ.ДляБухгалтерии КАК УЗ_РеализацияККМДляБухгалтерии
	                |ГДЕ
	                |	УЗ_РеализацияККМДляБухгалтерии.Ссылка = &Ссылка
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                |	300,
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.КомитентОрганизация,
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.Ссылка.Дата,
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.Ссылка.СкладКод,
	                |	0,
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.Ссылка,
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.Ссылка.Проведен,
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.Ссылка.ПометкаУдаления,
	                |	"""",
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.Ссылка.GuidСмены как СсылкаТХТ
	                |ИЗ
	                |	Документ.УЗ_РеализацияККМ.ОтчетКомиссионераОПродажах КАК УЗ_РеализацияККМОтчетКомиссионераОПродажах
	                |ГДЕ
	                |	УЗ_РеализацияККМОтчетКомиссионераОПродажах.КомитентОрганизация <> 0
	                |	И УЗ_РеализацияККМОтчетКомиссионераОПродажах.Ссылка = &Ссылка"; // Сгенерировано в GtG's Консоль запросов. 06.02.2016 03:39:07

	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МЗ = РегистрыСведений.ОбменСкладБух.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МЗ,Выборка);
		//МЗ.СсылкаТХТ = XMLСтрока(Ссылка);
		МЗ.Записать();
	КонецЦикла;
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	 Запрос=Новый Запрос();

	 
	Если Дата >= Дата(2017,08,18) Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	&Регистратор КАК Регистратор,
		               |	&Период КАК Период,
		               |	ИСТИНА КАК активность,
		               |	&ВидДвижения КАК ВидДвижения,
		               |	&Склад КАК Склад,
		               |	СУММА(ТЧ.СуммаРозн) КАК Сумма
		               |ИЗ
		               |	Документ.УЗ_РеализацияККМ.ДляБухгалтерии КАК ТЧ
		               |ГДЕ
		               |	ТЧ.Ссылка = &Регистратор
		               |	И ТЧ.ТипОплаты = 0";
		
		Запрос.УстановитьПараметр("Регистратор",ЭтотОбъект.Ссылка);
		Запрос.УстановитьПараметр("Склад",Справочники.МестаХранения.НайтиПоКоду(СкладКод));
		Запрос.УстановитьПараметр("Период",Дата);
		Запрос.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Приход);	
		
		
		
		НЗ=РегистрыНакопления.ОперационнаяКасса.СоздатьНаборЗаписей();
		НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
		НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
		НЗ.Записать();
	КонецЕсли; 
	 
	//---------------<Отметим нахождение ККМ в Аптеке>---------------------------// GtG // 28.12.2015 23:34:21
	// Для приблизительного понимания где стоит этот фискальный регистратор.
	НЗФР=РегистрыСведений.УЗ_ФискальныеРегистраторыВАптеках.СоздатьНаборЗаписей();
	НЗФР.Отбор.НомерККМ.Установить(НомерККМ);
	СтрНЗФР=НЗФР.Добавить();
	СтрНЗФР.НомерККМ=НомерККМ;
	СтрНЗФР.ДатаПоследнейСмены=Дата;
	СтрНЗФР.СкладКод=СкладКод;
	СтрНЗФР.ДатаПоследнейСмены=Дата;
	СтрНЗФР.ПереполнениеЭКЛЗ=ПереполнениеЭКЛЗ;
	НЗФР.Записать();
	
	
	Запрос.Текст="ВЫБРАТЬ
	             |	УЗ_ККМ_ЦТО.Код,
	             |	УЗ_ККМ_ЦТО.Фирма.Код,
	             |	ВЫБОР
	             |		КОГДА УЗ_ККМ_ЦТО.Фирма.Код <> &ТекущаяФирмаКод
	             |			ТОГДА ИСТИНА
	             |		ИНАЧЕ ЛОЖЬ
	             |	КОНЕЦ КАК ФирмаИзменилась,
	             |	УЗ_ККМ_ЦТО.Ссылка
	             |ИЗ
	             |	Справочник.УЗ_ККМ_ЦТО КАК УЗ_ККМ_ЦТО
	             |ГДЕ
	             |	УЗ_ККМ_ЦТО.Код = &НомерККМ";
	Запрос.УстановитьПараметр("НомерККМ",НомерККМ);
	Запрос.УстановитьПараметр("ТекущаяФирмаКод",ФирмаКод);
	Рез=Запрос.Выполнить();
	
	Если рез.Пустой()=Истина Тогда  // записи в справочнике об этом фискальнике еще нет. Создадим.
		Спр=Справочники.УЗ_ККМ_ЦТО.СоздатьЭлемент();
		Спр.Код=НомерККМ;
		Спр.Фирма=Справочники.Фирмы.НайтиПоКоду(ФирмаКод);
		Спр.Записать();
	Иначе  // отследим изменение фирмы на которую работает ФР
		Выб=Рез.Выбрать();
		Выб.Следующий();
		Если Выб.ФирмаИзменилась Тогда
			Спр=Выб.Ссылка.получитьОбъект();
			Спр.Фирма=Справочники.Фирмы.НайтиПоКоду(ФирмаКод);
			Спр.Записать();
		КонецЕсли; 
	КонецЕсли;	
	
	                   
	
	
	//---------------<УЗ_Партии>---------------------------// GtG // 31.12.2015 15:40:50
	
		
	Запрос.Текст="ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ИСТИНА КАК Активность,
	|	&ВДНРасход как ВидДвижения,
	|	УЗ_РеализацияККМПродажиТовара.КодПартии КАК ПартияКод,
	|	УЗ_РеализацияККМПродажиТовара.КодТовара КАК ТоварКод,
	|	&СкладКод,
	|	&ФирмаКод,
	|	УЗ_РеализацияККМПродажиТовара.ВидПоступленияПорядок,
	|	УЗ_РеализацияККМПродажиТовара.СтавкаНДСЗакуп,
	|	СУММА(УЗ_РеализацияККМПродажиТовара.Количество) КАК Количество,
	|	СУММА(УЗ_РеализацияККМПродажиТовара.СуммаЗакупБезНДС) КАК СуммаЗакупБезНДС
	|ИЗ
	|	Документ.УЗ_РеализацияККМ.ПродажиТовара КАК УЗ_РеализацияККМПродажиТовара
	|ГДЕ
	|	УЗ_РеализацияККМПродажиТовара.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	УЗ_РеализацияККМПродажиТовара.СтавкаНДСЗакуп,
	|	УЗ_РеализацияККМПродажиТовара.ВидПоступленияПорядок,
	|	УЗ_РеализацияККМПродажиТовара.КодТовара,
	|	УЗ_РеализацияККМПродажиТовара.КодПартии";
	
	Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("ВДНРасход",ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("СкладКод",СкладКод);
	Запрос.УстановитьПараметр("ФирмаКод",ФирмаКод);
	
	НЗ=РегистрыНакопления.УЗ_Партии.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	
	НЗ.Записать();
	
	//---------------<УЗ_ПродажиДляМотивацииХ>---------------------------// GtG // 31.12.2015 15:41:08
	Запрос.Текст="ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ИСТИНА КАК Активность,
	|	УЗ_РеализацияККММотивацияТовар.СотрудникКод,
	|	УЗ_РеализацияККММотивацияТовар.КодТовара КАК ТоварКод,
	|	&СкладКод,
	|	&ФирмаКод,
	|	СУММА(УЗ_РеализацияККММотивацияТовар.Количество) КАК Количество,
	|	СУММА(УЗ_РеализацияККММотивацияТовар.СуммаРозн) + СУММА(УЗ_РеализацияККММотивацияТовар.СуммаЗачетаАвансаИСерт) КАК СуммаРознФакт,
	|	СУММА(УЗ_РеализацияККММотивацияТовар.СуммаСкидки) КАК СуммаСкидки,
	|	СУММА(УЗ_РеализацияККММотивацияТовар.СуммаЗакупБезНДС + УЗ_РеализацияККММотивацияТовар.СуммаЗакупБезНДС / 100 * УЗ_РеализацияККММотивацияТовар.СтавкаНДСРозн) КАК СуммаЗакупСНДС,
	|	СУММА(УЗ_РеализацияККММотивацияТовар.ОбщийБонус) КАК ОбщийБонус
	|ИЗ
	|	Документ.УЗ_РеализацияККМ.МотивацияТовар КАК УЗ_РеализацияККММотивацияТовар
	|ГДЕ
	|	УЗ_РеализацияККММотивацияТовар.Ссылка = &Ссылка
	|	И УЗ_РеализацияККММотивацияТовар.Количество + УЗ_РеализацияККММотивацияТовар.СуммаРозн + УЗ_РеализацияККММотивацияТовар.СуммаЗачетаАвансаИСерт + УЗ_РеализацияККММотивацияТовар.СуммаСкидки + УЗ_РеализацияККММотивацияТовар.ОбщийБонус <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	УЗ_РеализацияККММотивацияТовар.СотрудникКод,
	|	УЗ_РеализацияККММотивацияТовар.КодТовара";
	
	НомерМесяца=Месяц(Дата);
	
	НЗ=РегистрыНакопления["УЗ_ПродажиДляМотивации"+НомерМесяца].СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать();
	
	//---------------< УЗ_КонтрольСотрудников>---------------------------// GtG // 31.12.2015 18:00:43
	Запрос.Текст="ВЫБРАТЬ
	             |	&Дата КАК Период,
	             |	&Ссылка КАК Регистратор,
	             |	ИСТИНА КАК Активность,
	             |	Итоги.СотрудникКод КАК СотрудникКод,
	             |	&СкладКод КАК СкладКод,
	             |	&ФирмаКод КАК ФирмаКод,
	             |	СУММА(Итоги.СуммаРозн) КАК СуммаРознФакт,
	             |	СУММА(Итоги.СуммаСкидки) КАК СуммаСкидки,
	             |	СУММА(Итоги.ОбщийБонус) КАК ОбщийБонус,
	             |	СУММА(Итоги.КоличествоЧеков) КАК КоличествоЧеков,
	             |	СУММА(Итоги.КоличествоНаименованийВЧеке) КАК КоличествоПозицийВЧеках,
	             |	СУММА(Итоги.СуммаЗакупСНДС) КАК СуммаЗакуп
	             |ИЗ
	             |	Документ.УЗ_РеализацияККМ.Мотивация_Итоги КАК Итоги
	             |ГДЕ
	             |	Итоги.Ссылка = &Ссылка
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	Итоги.СотрудникКод";
	
	НЗ=РегистрыСведений.УЗ_КонтрольСотрудников.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать();
	
	//---------------<УЗ_ТоварныйОтчет>---------------------------// GtG // 31.12.2015 18:14:41
	Запрос.Текст="ВЫБРАТЬ
	|	ДокТовар.Период,
	|	ДокТовар.Регистратор,
	|	ДокТовар.Активность,
	|	ДокТовар.ВидДвижения,
	|	ДокТовар.КачествоТовараПорядок,
	|	ДокТовар.ВидПоступленияПорядок,
	|	ДокТовар.СкладКод,
	|	ДокТовар.ФирмаКод,
	|	СУММА(ДокТовар.СуммаЗакупБезНДС) КАК СуммаЗакупБезНДС,
	|	ДокТовар.СтавкаНДС,
	|	СУММА(ДокТовар.СуммаОкругления) КАК СуммаОкругления
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Дата КАК Период,
	|		&Ссылка КАК Регистратор,
	|		ИСТИНА КАК Активность,
	|		&ВДНРасход КАК ВидДвижения,
	|		0 КАК КачествоТовараПорядок,
	|		УЗ_РеализацияККМПродажиТовара.ВидПоступленияПорядок КАК ВидПоступленияПорядок,
	|		&СкладКод КАК СкладКод,
	|		&ФирмаКод КАК ФирмаКод,
	|		УЗ_РеализацияККМПродажиТовара.СуммаЗакупБезНДС КАК СуммаЗакупБезНДС,
	|		УЗ_РеализацияККМПродажиТовара.СтавкаНДСРозн КАК СтавкаНДС,
	|		0 КАК СуммаОкругления
	|	ИЗ
	|		Документ.УЗ_РеализацияККМ.ПродажиТовара КАК УЗ_РеализацияККМПродажиТовара
	|	ГДЕ
	|		УЗ_РеализацияККМПродажиТовара.Ссылка = &Ссылка) КАК ДокТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокТовар.Период,
	|	ДокТовар.Регистратор,
	|	ДокТовар.Активность,
	|	ДокТовар.ВидДвижения,
	|	ДокТовар.КачествоТовараПорядок,
	|	ДокТовар.ВидПоступленияПорядок,
	|	ДокТовар.СкладКод,
	|	ДокТовар.ФирмаКод,
	|	ДокТовар.СтавкаНДС";
	
	//0 КАК КачествоТовараПрядок,  - хороший товар
	
	НЗ=РегистрыНакопления.УЗ_ТоварныйОтчет.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать();
	
	//---------------<УЗ_Сертификаты>---------------------------// GtG // 31.12.2015 18:15:17
	Запрос.Текст="ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	ИСТИНА КАК Активность,
	|	УЗ_РеализацияККМСертификаты_Зачет.СотрудникКод,
	|	УЗ_РеализацияККМСертификаты_Зачет.ИДСертификата,
	|	&СкладКод,
	|	СУММА(УЗ_РеализацияККМСертификаты_Зачет.Сумма) КАК Сумма
	|ИЗ
	|	Документ.УЗ_РеализацияККМ.Сертификаты_Зачет КАК УЗ_РеализацияККМСертификаты_Зачет
	|ГДЕ
	|	УЗ_РеализацияККМСертификаты_Зачет.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	УЗ_РеализацияККМСертификаты_Зачет.СотрудникКод,
	|	УЗ_РеализацияККМСертификаты_Зачет.ИДСертификата";
	
	
	НЗ=РегистрыНакопления.УЗ_Сертификаты.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать();
	
	//---------------<ОТЧ_ОтчетПоВыручке>---------------------------// GtG // 31.12.2015 18:20:48     
	Запрос.Текст="ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	ИСТИНА КАК Активность,
	|	&ФирмаКод,
	|	&СкладКод,
	|	СУММА(база.КоличествоЧеков) КАК КолвоЧеков,
	|	СУММА(база.КоличествоНаименованийВЧеке) КАК КоличествоНаименованийВЧеке,
	|	СУММА(база.СуммаРозн) КАК СуммаВыручки,
	|	&КомментарийДляОПВ,
	|	&Выходной
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(УЗ_РеализацияККММотивация_Итоги.КоличествоЧеков) КАК КоличествоЧеков,
	|		СУММА(УЗ_РеализацияККММотивация_Итоги.КоличествоНаименованийВЧеке) КАК КоличествоНаименованийВЧеке,
	|		0 КАК СуммаРозн
	|	ИЗ
	|		Документ.УЗ_РеализацияККМ.Мотивация_Итоги КАК УЗ_РеализацияККММотивация_Итоги
	|	ГДЕ
	|		УЗ_РеализацияККММотивация_Итоги.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		0,
	|		СУММА(УЗ_РеализацияККМПродажиТовара.СуммаРозн)
	|	ИЗ
	|		Документ.УЗ_РеализацияККМ.ПродажиТовара КАК УЗ_РеализацияККМПродажиТовара
	|	ГДЕ
	|		УЗ_РеализацияККМПродажиТовара.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		0,
	|		СУММА(Аванс.Сумма)
	|	ИЗ
	|		Документ.УЗ_РеализацияККМ.Авансы_Зачет КАК Аванс
	|	ГДЕ
	|		Аванс.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		0,
	|		СУММА(УЗ_РеализацияККМУслуги.СуммаРозн)
	|	ИЗ
	|		Документ.УЗ_РеализацияККМ.Услуги КАК УЗ_РеализацияККМУслуги
	|	ГДЕ
	|		УЗ_РеализацияККМУслуги.Ссылка = &Ссылка) КАК база";
	
	Если Корректная=Ложь Тогда
		Запрос.УстановитьПараметр("КомментарийДляОПВ","Смена еще не закрыта.");
	Иначе
		Запрос.УстановитьПараметр("КомментарийДляОПВ","ОК.");
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("Выходной",ВыходнойДень);	 
	
	Запрос.УстановитьПараметр("КомментарийДляОПВ",КомментарийДляОПВ);	 
	
	НЗ=РегистрыНакопления.ОТЧ_ОтчетПоВыручке.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать(Истина);
	
	
	//---------------<УЗ_Дисконт>---------------------------// GtG // 02.01.2016 19:50:19
	Запрос.Текст="ВЫБРАТЬ
	             |	&Ссылка КАК Регистратор,
	             |	&Дата КАК Период,
	             |	ИСТИНА КАК Активность,
	             |	УЗ_РеализацияККМДисконт.КодТипаДК,
	             |	УЗ_РеализацияККМДисконт.НомерДК,
	             |	УЗ_РеализацияККМДисконт.ИДРА,
	             |	УЗ_РеализацияККМДисконт.КодПартии КАК ПартияКод,
	             |	УЗ_РеализацияККМДисконт.КодТовара КАК ТоварКод,
	             |	&СкладКод КАК СкладКод,
	             |	&ФирмаКод КАК ФирмаКод,
	             |	УЗ_РеализацияККМДисконт.Количество,
	             |	УЗ_РеализацияККМДисконт.СуммаЗакупБезНДС,
	             |	УЗ_РеализацияККМДисконт.СуммаРозн,
	             |	УЗ_РеализацияККМДисконт.СуммаСкидки,
	             |	ВложенныйЗапрос.СтавкаНДСРозн КАК СтавкаНДС,
	             |	УЗ_РеализацияККМДисконт.ОплатаБаллами,
	             |	УЗ_РеализацияККМДисконт.СотрудникКод,
	             |	УЗ_РеализацияККМДисконт.СуммаСкидкиПоКредиту
	             |ИЗ
	             |	Документ.УЗ_РеализацияККМ.Дисконт КАК УЗ_РеализацияККМДисконт
	             |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	             |			УЗ_РеализацияККМПродажиТовара.КодПартии КАК КодПартии,
	             |			МАКСИМУМ(УЗ_РеализацияККМПродажиТовара.СтавкаНДСРозн) КАК СтавкаНДСРозн
	             |		ИЗ
	             |			Документ.УЗ_РеализацияККМ.ПродажиТовара КАК УЗ_РеализацияККМПродажиТовара
	             |		ГДЕ
	             |			УЗ_РеализацияККМПродажиТовара.Ссылка = &Ссылка
	             |		
	             |		СГРУППИРОВАТЬ ПО
	             |			УЗ_РеализацияККМПродажиТовара.КодПартии) КАК ВложенныйЗапрос
	             |		ПО УЗ_РеализацияККМДисконт.КодПартии = ВложенныйЗапрос.КодПартии
	             |ГДЕ
	             |	УЗ_РеализацияККМДисконт.Ссылка = &Ссылка";
	
	НЗ=РегистрыНакопления.УЗ_Дисконт.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать(Истина);
	
	//---------------<УЗ_РеализацииККМ>---------------------------// GtG // 02.01.2016 20:15:41	
	Запрос.Текст="ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	ИСТИНА КАК Активность,
	|	&СкладКод,
	|	УЗ_РеализацияККМПродажиТовара.КодТовара КАК ТоварКод,
	|	УЗ_РеализацияККМПродажиТовара.КодПартии КАК ПартияКод,
	|	СУММА(УЗ_РеализацияККМПродажиТовара.Количество) КАК Количество,
	|	УЗ_РеализацияККМПродажиТовара.СуммаЗакупБезНДС + УЗ_РеализацияККМПродажиТовара.СуммаЗакупБезНДС / 100 * УЗ_РеализацияККМПродажиТовара.СтавкаНДСЗакуп КАК СуммаЗакупСНДС,
	|	УЗ_РеализацияККМПродажиТовара.СуммаРозн + СуммаЗачетаАвансаИСерт как СуммаРозн,
	|	&ФирмаКод,
	|	УЗ_РеализацияККМПродажиТовара.СтавкаНДСРозн КАК СтавкаНДС,
	|	УЗ_Партии.К,
	|	УЗ_РеализацияККМПродажиТовара.ВидПоступленияПорядок,
	|	УЗ_РеализацияККМПродажиТовара.ТипОплаты
	|ИЗ
	|	Документ.УЗ_РеализацияККМ.ПродажиТовара КАК УЗ_РеализацияККМПродажиТовара
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК УЗ_Партии
	|		ПО УЗ_РеализацияККМПродажиТовара.КодПартии = УЗ_Партии.Код
	|ГДЕ
	|	УЗ_РеализацияККМПродажиТовара.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	УЗ_РеализацияККМПродажиТовара.КодТовара,
	|	УЗ_РеализацияККМПродажиТовара.КодПартии,
	|	УЗ_РеализацияККМПродажиТовара.СуммаРозн + СуммаЗачетаАвансаИСерт,
	|	УЗ_РеализацияККМПродажиТовара.СтавкаНДСРозн,
	|	УЗ_РеализацияККМПродажиТовара.ВидПоступленияПорядок,
	|	УЗ_РеализацияККМПродажиТовара.ТипОплаты,
	|	УЗ_Партии.К,
	|	УЗ_РеализацияККМПродажиТовара.СуммаЗакупБезНДС + УЗ_РеализацияККМПродажиТовара.СуммаЗакупБезНДС / 100 * УЗ_РеализацияККМПродажиТовара.СтавкаНДСЗакуп";
	
	НЗ=РегистрыНакопления.УЗ_РеализацииККМ.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать(Истина);
	
	
	//---------------<УЗ_ПрограммыЛояльности>---------------------------// GtG // 25.01.2016 18:55:08
	Запрос.Текст="ВЫБРАТЬ
	             |	Лояльность.Ссылка.Дата КАК Период,
	             |	Лояльность.Ссылка КАК Регистратор,
	             |	ИСТИНА КАК Активность,
	             |	ТипыДисконтныхКарт.Контрагент КАК Контрагент,
	             |	Лояльность.Ссылка.СкладКод КАК АптекаКод,
	             |	Лояльность.КодТовара КАК ТоварКод,
	             |	Лояльность.НомерДК КАК НомерКарты,
	             |	Лояльность.Ссылка.НомерККМ КАК НомерККМ,
	             |	Лояльность.Ссылка.НомерСмены КАК НомерСмены,
	             |	Лояльность.НомерЧека КАК НомерЧека,
	             |	УЗ_Партии.Баркод КАК БарКодТовара,
	             |	УЗ_Партии.ПоставщикИсходный КАК ПоставщикКод,
	             |	Лояльность.Количество КАК Количество,
	             |	Лояльность.СуммаРознСоСкидкой КАК СуммаСоСкидкой,
	             |	Лояльность.СуммаРознСоСкидкой / (100 + УЗ_Партии.СтавкаНДСРозн) * УЗ_Партии.СтавкаНДСРозн КАК СуммаНДС,
	             |	Лояльность.СуммаСкидки / (100 + УЗ_Партии.СтавкаНДСРозн) * УЗ_Партии.СтавкаНДСРозн КАК СуммаНДССкидки,
	             |	Лояльность.КодПартии КАК ПартияКод,
	             |	Лояльность.GuidЧека,
	             |	УЗ_Партии.К,
	             |	Лояльность.КодТипаДК,
	             |	Лояльность.СотрудникКод,
	             |	Лояльность.СуммаСкидки КАК СуммаСкидки
	             |ИЗ
	             |	Документ.УЗ_РеализацияККМ.ПрограммыЛояльности КАК Лояльность
	             |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыДисконтныхКарт КАК ТипыДисконтныхКарт
	             |		ПО Лояльность.КодТипаДК = ТипыДисконтныхКарт.Код
	             |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК УЗ_Партии
	             |		ПО Лояльность.КодПартии = УЗ_Партии.Код
	             |ГДЕ
	             |	Лояльность.Ссылка = &Ссылка";
	
	НЗ=РегистрыНакопления.УЗ_ПрограммыЛояльности.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать(Истина);
	
	
	//-----------------<Почасовая выручка. Жестко к документу не привязана>--------------------  04-02-2016
	Запрос.Текст="ВЫБРАТЬ
	             |	&СкладКод,
	             |	УЗ_РеализацияККМПочасоваяВыручка.Час,
	             |	УЗ_РеализацияККМПочасоваяВыручка.СотрудникКод,
	             |	&Смена,
	             |	УЗ_РеализацияККМПочасоваяВыручка.СуммаВыручки,
	             |	УЗ_РеализацияККМПочасоваяВыручка.КоличествоЧеков,
	             |	УЗ_РеализацияККМПочасоваяВыручка.КоличествоНаименований
	             |ИЗ
	             |	Документ.УЗ_РеализацияККМ.ПочасоваяВыручка КАК УЗ_РеализацияККМПочасоваяВыручка
	             |ГДЕ
	             |	УЗ_РеализацияККМПочасоваяВыручка.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("СкладКод",СкладКод);			 
	Запрос.УстановитьПараметр("Смена",GuidСмены);
	
	НЗ=РегистрыСведений.УЗ_ПочасоваяВыручка.СоздатьНаборЗаписей();
	НЗ.Отбор.Смена.Установить(GuidСмены);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать();
	
	
	//---------------<УЗ_ПродажиВнешнейКомиссии>---------------------------// Virus // 14.06.2016 20:15:41	
	Запрос.Текст="ВЫБРАТЬ
	             |	&Ссылка КАК Регистратор,
	             |	&Дата КАК Период,
	             |	ИСТИНА КАК Активность,
	             |	&СкладКод КАК СкладКод,
	             |	Продажи.ВнешнийКомитент КАК КомитентКод,
	             |	Продажи.КодТовара КАК ТоварКод,
	             |	Продажи.КодПартии КАК ПартияКод,
	             |	СУММА(Продажи.Количество) КАК Количество,
	             |	СУММА(Продажи.СуммаЗакупБезНДС + Продажи.СуммаЗакупБезНДС / 100 * Продажи.СтавкаНДСЗакуп) КАК СуммаЗакупСНДС,
	             |	СУММА(Продажи.СуммаРозн) КАК СуммаРозн,
	             |	&ФирмаКод КАК ФирмаКод,
	             |	Продажи.СтавкаНДСРозн КАК СтавкаНДС,
	             |	УЗ_Партии.К
	             |ИЗ
	             |	Документ.УЗ_РеализацияККМ.ПродажиТовара КАК Продажи
	             |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УЗ_Партии КАК УЗ_Партии
	             |		ПО Продажи.КодПартии = УЗ_Партии.Код
	             |ГДЕ
	             |	Продажи.Ссылка = &Ссылка
	             |	И Продажи.ВнешнийКомитент > 0
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	Продажи.ВнешнийКомитент,
	             |	Продажи.КодТовара,
	             |	Продажи.КодПартии,
	             |	Продажи.СтавкаНДСРозн,
	             |	УЗ_Партии.К";
	
	НЗ=РегистрыНакопления.УЗ_ПродажиВнешнейКомиссии.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
	НЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	НЗ.Записать(Истина);
	
	
	
	ПоместитьВОбменСкладБух();
	
	
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	
	
	
	
	
КонецПроцедуры
