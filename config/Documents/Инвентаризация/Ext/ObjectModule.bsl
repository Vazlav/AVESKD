Функция СформироватьТЗДанныхАптеки(Текст)
	
	
	
	ТЗОст = Новый ТаблицаЗначений;
	ТЗОст.Колонки.Добавить("Код");
	ТЗОст.Колонки.Добавить("Количество");
	ТЗОст.Колонки.Добавить("Цена");
	ТЗОст.Колонки.Добавить("Партия");
	ТЗОст.Колонки.Добавить("Сумма");
	
	КолСтрок = Текст.КоличествоСтрок();	
	
	Для н=2 по КолСтрок Цикл
		
	
		ТекСтрока		= Текст.ПолучитьСтроку(н);
		ТекСтрока		= СтрЗаменить(ТекСтрока,";",Символы.ПС);
		
		НоваяСтрока = ТЗОст.Добавить();
		//Сообщить(СтрПолучитьСтроку(ТекСтрока,2));
		НоваяСтрока.Партия		= СокрЛП(СтрЗаменить(СтрПолучитьСтроку(ТекСтрока,6),"""",""));
		
		КодЕдиницы				=  СтрЗаменить(СтрПолучитьСтроку(ТекСтрока,2),"""","");
		Если КодЕдиницы = "" Тогда
			
			ТекПартия = Справочники.Партии.НайтиПоНаименованию(НоваяСтрока.Партия,Истина);
			КодЕдиницы = ТекПартия.Владелец.ЕдиницаМин.Код;
			
			Если КодЕдиницы = NULL или КодЕдиницы = 0 Тогда
				ВызватьИсключение "По одной из позиций не обнаружена единица измерения товара." + Символы.ПС + "Необходимо обновить связки в атеке и перевыгрузить документ в офис";	
			КонецЕсли;
		КонецЕсли;
		НоваяСтрока.Код 		= Число(КодЕдиницы); //Число(СтрЗаменить(СтрПолучитьСтроку(ТекСтрока,2),"""",""));

		НоваяСтрока.Количество	= Число(СтрЗаменить(СтрПолучитьСтроку(ТекСтрока,4),"""",""));
		НоваяСтрока.Цена		= Число(СтрЗаменить(СтрПолучитьСтроку(ТекСтрока,5),"""",""));
		НоваяСтрока.Сумма		= НоваяСтрока.Количество*НоваяСтрока.Цена;
		
	КонецЦикла;
	
	ТЗОст.Свернуть("Код,Партия","Количество,Сумма");	
	
	Возврат ТЗОст;
	
КонецФункции


Функция СформируемТЗЕИТ()
	 
	 ТХТ = "ВЫБРАТЬ
	       |	ЕИТ.Код,
	       |	ЕИТ.Ссылка,
	       |	ЕИТ.К,
	       |	ЕИТ.Владелец как Товар
	       |ИЗ
	       |	Справочник.ЕИТ КАК ЕИТ";
	Запрос = Новый Запрос;
	Запрос.Текст = ТХТ;
	ТЗЕИТ = Запрос.Выполнить().Выгрузить();
	ТЗЕИТ.Индексы.Добавить("Код");
	Возврат ТЗЕИТ;
	
КонецФункции


Функция ПолучитьТаблицуСсылокНаПартии(СписокПартий)
	
	ТХТ = "ВЫБРАТЬ
	      |	Партии.Ссылка КАК ПартияСсылка,
	      //|	Партии.Владелец КАК Товар,
	      |	Партии.Наименование КАК ПартияНаименование,
		  | Партии.ЦенаЗакуп,
		  | Партии.СтавкаНДС
	      |ИЗ
	      |	Справочник.Партии КАК Партии
	      |ГДЕ
	      |	Партии.Наименование В(&СписокПартий)";
		  
	Запрос = Новый Запрос;
	Запрос.Текст = ТХТ;
	Запрос.УстановитьПараметр("СписокПартий",СписокПартий);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции


Процедура СовместитьУчетИФакт(ТЗДанныхАптеки) Экспорт
	
	
	//Товар = Товар;
	
	ТЗСсылокНаПартии = ПолучитьТаблицуСсылокНаПартии(ТЗДанныхАптеки.ВыгрузитьКолонку("КодПартии"));
	ТЗЕИТ = СформируемТЗЕИТ();
	
	//ТЗОст.ВыбратьСтроку();
	КоличествоСтрокОстатков = ТЗДанныхАптеки.Количество();

	#Если Клиент Тогда
		ТекстДок = Новый ТекстовыйДокумент;
		Состояние("Накатываем таблицу остатков аптеки на остатки учетные в 1С .......");
	#КонецЕсли
	н=0;
	Для каждого стр из ТЗДанныхАптеки Цикл
		н=н+1;
		#Если Клиент Тогда
			Если н%100 = 0 Тогда
				Состояние("Загружено "+н+ " из " + КоличествоСтрокОстатков);
			КонецЕсли;
		#КонецЕсли
		
		ЕИТСтрока = ТЗЕИТ.Найти(стр.Код,"Код");
		
		Если ЕИТСтрока = Неопределено Тогда
			#Если Клиент Тогда
				Сообщить("Не нашли ЕИТ по коду: " + стр.Код);
				Возврат;
			#КонецЕсли
		КонецЕсли;		
		
		ПартияСтрока = ТЗСсылокНаПартии.Найти(СокрЛП(стр.Партия),"ПартияНаименование");
		Если ПартияСтрока = Неопределено Тогда
			#Если Клиент Тогда
				Сообщить("Не найдена партия "+стр.Партия+" файл не обрабатываем");
				Возврат;				
			#КонецЕсли
		КонецЕсли;
		
		
		ТоварСсылка = ЕИТСтрока.Товар;
		ПартияСсылка = ПартияСтрока.ПартияСсылка;
		
		НайденнаяСтрока = Товар.Найти(ПартияСсылка,"Партия");
		Если НайденнаяСтрока = Неопределено Тогда //Если нет на остатке, то добавляем строку
			НоваяСтрока = Товар.Добавить();
			НоваяСтрока.Товар = ТоварСсылка;
			НоваяСтрока.Коэфф = ЕИТСтрока.К;
			НоваяСтрока.КоличествоУчет = 0;
			НоваяСтрока.КоличествоФакт = стр.Количество;
			НоваяСтрока.КоличествоРазница = стр.Количество;
			НоваяСтрока.Партия = ПартияСсылка;
			НоваяСтрока.СтавкаНДС = ПартияСтрока.СтавкаНДС;
			НоваяСтрока.СуммаЗакупФакт = (стр.Количество*ПартияСтрока.ЦенаЗакуп)/НоваяСтрока.Партия.ЕИТЗакупки.К*ЕИТСтрока.К;
			НоваяСтрока.СуммаЗакупРазница = НоваяСтрока.СуммаЗакупФакт;
			НоваяСтрока.СуммаРознФакт = стр.Сумма;
			НоваяСтрока.СуммаРознРазница = стр.Сумма;
			НоваяСтрока.СуммаНДСЗакупРазница = Окр(ОМ3_НДСИзСуммыПоСтавке(НоваяСтрока.СуммаЗакупФакт,НоваяСтрока.СтавкаНДС),2); 
			НоваяСтрока.СуммаНДСРознРазница = Окр(ОМ3_НДСИзСуммыПоСтавке(НоваяСтрока.СуммаРознФакт,НоваяСтрока.СтавкаНДС),2); 
			
		Иначе //Есть на остатке, редактируем
			НайденнаяСтрока.КоличествоФакт = НайденнаяСтрока.КоличествоФакт + стр.Количество*ЕИТСтрока.К;
			НайденнаяСтрока.КоличествоРазница = НайденнаяСтрока.КоличествоФакт -НайденнаяСтрока.КоличествоУчет;
			Если НайденнаяСтрока.КоличествоУчет = 0 Тогда
				НайденнаяСтрока.СуммаЗакупФакт = НайденнаяСтрока.КоличествоФакт*ПартияСтрока.ЦенаЗакуп/НайденнаяСтрока.Партия.ЕИТЗакупки.К; // (стр.Количество*ПартияСтрока.ЦенаЗакуп)/НайденнаяСтрока.Партия.ЕИТЗакупки.К*ЕИТСтрока.К;
				//Сообщить("партия: "+ НайденнаяСтрока.Партия + " проверить эту строчку");
			Иначе
				НайденнаяСтрока.СуммаЗакупФакт = НайденнаяСтрока.КоличествоФакт*(НайденнаяСтрока.СуммаЗакупУчет/НайденнаяСтрока.КоличествоУчет); //стр.Количество*(НайденнаяСтрока.СуммаЗакупУчет/НайденнаяСтрока.КоличествоУчет);
			КонецЕсли;
			НайденнаяСтрока.СуммаЗакупРазница = НайденнаяСтрока.СуммаЗакупФакт - НайденнаяСтрока.СуммаЗакупУчет;
			НайденнаяСтрока.СуммаРознФакт = НайденнаяСтрока.СуммаРознФакт + стр.Сумма;
			НайденнаяСтрока.СуммаРознРазница = НайденнаяСтрока.СуммаРознФакт - НайденнаяСтрока.СуммаРознУчет; // стр.Сумма - НайденнаяСтрока.СуммаРознУчет;
			НайденнаяСтрока.СуммаНДСЗакупРазница = (Окр(ОМ3_НДСИзСуммыПоСтавке(НайденнаяСтрока.СуммаЗакупФакт,НайденнаяСтрока.СтавкаНДС),2) - НайденнаяСтрока.СуммаНДСЗакупУчет); 
			НайденнаяСтрока.СуммаНДСРознРазница = (Окр(ОМ3_НДСИзСуммыПоСтавке(НайденнаяСтрока.СуммаРознФакт,НайденнаяСтрока.СтавкаНДС),2) - НайденнаяСтрока.СуммаНДСРознУчет); 
			
			#Если Клиент Тогда
			Если НайденнаяСтрока.СуммаРознФакт < НайденнаяСтрока.СуммаЗакупФакт Тогда
				ТекстДок.ДобавитьСтроку("партия: " + ПартияСсылка + " розн = " + НайденнаяСтрока.СуммаРознФакт + " < закуп = " + НайденнаяСтрока.СуммаЗакупФакт);
			КонецЕсли;
			#КонецЕсли
			
			//Если количества и суммы равны , то разницы ндс по любому должны нулю равны быть
			Если (НайденнаяСтрока.КоличествоУчет = НайденнаяСтрока.КоличествоФакт)
				И (НайденнаяСтрока.СуммаРознУчет = НайденнаяСтрока.СуммаРознФакт)
				И (НайденнаяСтрока.СуммаЗакупУчет = НайденнаяСтрока.СуммаЗакупФакт) Тогда
				НайденнаяСтрока.СуммаНДСЗакупРазница = 0; 
				НайденнаяСтрока.СуммаНДСРознРазница = 0;
			КонецЕсли;
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	СуммаЗакупУчет	= Товар.Итог("СуммаЗакупУчет");
	СуммаРознУчет	= Товар.Итог("СуммаРознУчет");
	СуммаЗакупФакт	= Товар.Итог("СуммаЗакупФакт");
	СуммаРознФакт	= Товар.Итог("СуммаРознФакт");
	СуммаРазница	= Товар.Итог("СуммаРознРазница");
	
	
	Записать(РежимЗаписиДокумента.Запись);
	#Если Клиент Тогда
		Если ТекстДок.КоличествоСтрок() > 0 Тогда
			ТекстДок.Показать();
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры


Процедура МДЗагрузитьДанныеАптекиИЗPG() Экспорт
	
	Если Товар.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеХранилища = ДанныеАптеки.Получить();
	Если НЕ ДанныеХранилища = Неопределено Тогда
		
		ТЗДанныхАптеки = СформироватьТЗДанныхАптеки(ДанныеХранилища);
		СовместитьУчетИФакт(ТЗДанныхАптеки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуОстаткомАптеки() Экспорт
	
	ТХТ="ВЫБРАТЬ
	    |	ПартииЖНВЛСОстатки.Товар КАК Товар,
	    |	ПартииЖНВЛСОстатки.Партия КАК Партия,
	    |	ПартииЖНВЛСОстатки.КолвоОстаток КАК КоличествоУчет,
	    |	ПартииЖНВЛСОстатки.СуммаЗакупСНДСОстаток КАК СуммаЗакупУчет,
	    |	ПартииЖНВЛСОстатки.СуммаНДСЗакупОстаток КАК СуммаНДСЗакупУчет,
	    |	ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток КАК СуммаРознУчет,
	    |	ПартииЖНВЛСОстатки.СуммаРознНДСОстаток КАК СуммаНДСРознУчет,
	    |	ПартииЖНВЛСОстатки.СтавкаНДС КАК СтавкаНДС,
	    |	1 КАК Коэфф,
	    |	-ПартииЖНВЛСОстатки.КолвоОстаток КАК КоличествоРазница,
	    |	-ПартииЖНВЛСОстатки.СуммаЗакупСНДСОстаток КАК СуммаЗакупРазница,
	    |	-ПартииЖНВЛСОстатки.СуммаНДСЗакупОстаток КАК СуммаНДСЗакупРазница,
	    |	-ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток КАК СуммаРознРазница,
	    |	-ПартииЖНВЛСОстатки.СуммаРознНДСОстаток КАК СуммаНДСРознРазница
	    |ИЗ
	    |	РегистрНакопления.ПартииЖНВЛС.Остатки(&Дата, Склад = &Склад) КАК ПартииЖНВЛСОстатки
	    |
	    |УПОРЯДОЧИТЬ ПО
	    |	ПартииЖНВЛСОстатки.Товар.Наименование";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТХТ;
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("Склад",Склад);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	Товар.Загрузить(ТЗ);	
	
	     
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	
	    Для Каждого ТекСтр Из Товар Цикл
		
			Если (ТекСтр.КоличествоРазница  = 0) и (ТекСтр.СуммаНДСЗакупРазница  = 0) и (ТекСтр.СуммаНДСРознРазница  = 0) и (ТекСтр.СуммаРознРазница = 0) И (ТекСтр.СуммаЗакупРазница = 0) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекСтр.КоличествоРазница > 0 Тогда
				
				
				ДвижениеП = Движения.ПартииЖНВЛС.Добавить();
				ДвижениеП.ВидДвижения= ВидДвиженияНакопления.Приход;
				ДвижениеП.Период = Дата;
				ДвижениеП.Товар = ТекСтр.Товар;
				ДвижениеП.Склад = Склад;
				ДвижениеП.СтавкаНДС = ТекСтр.СтавкаНДС;
				ДвижениеП.Партия = ТекСтр.Партия ;
				ДвижениеП.Фирма= Фирма;
				ДвижениеП.ВидОперации=Перечисления.ВидыОпераций.Инвентаризация;
				
				
				ДвижениеП.Колво = ТекСтр.КоличествоРазница*ТекСтр.Коэфф;
				ДвижениеП.СуммаЗакупСНДС = ТекСтр.СуммаЗакупРазница;
				ДвижениеП.СуммаНДСЗакуп = ТекСтр.СуммаНДСЗакупРазница;
				ДвижениеП.СуммаРознСНДС = ТекСтр.СуммаРознРазница;
				ДвижениеП.СуммаРознНДС = ТекСтр.СуммаНДСРознРазница;
			ИначеЕсли ТекСтр.КоличествоРазница < 0 Тогда
				ДвижениеП = Движения.ПартииЖНВЛС.Добавить();
				ДвижениеП.ВидДвижения= ВидДвиженияНакопления.Расход;
				ДвижениеП.Период = Дата;
				ДвижениеП.Товар = ТекСтр.Товар;
				ДвижениеП.Склад = Склад;
				ДвижениеП.СтавкаНДС = ТекСтр.СтавкаНДС;
				ДвижениеП.Партия = ТекСтр.Партия ;
				ДвижениеП.Фирма= Фирма;
				ДвижениеП.ВидОперации=Перечисления.ВидыОпераций.Инвентаризация;
				
				
				ДвижениеП.Колво = -ТекСтр.КоличествоРазница*ТекСтр.Коэфф;
				ДвижениеП.СуммаЗакупСНДС = -ТекСтр.СуммаЗакупРазница;
				ДвижениеП.СуммаНДСЗакуп = -ТекСтр.СуммаНДСЗакупРазница;
				ДвижениеП.СуммаРознСНДС = -ТекСтр.СуммаРознРазница;
				ДвижениеП.СуммаРознНДС = -ТекСтр.СуммаНДСРознРазница;			
			Иначе
				
				ДвижениеП = Движения.ПартииЖНВЛС.Добавить();
				ДвижениеП.ВидДвижения= ВидДвиженияНакопления.Приход;
				ДвижениеП.Период = Дата;
				ДвижениеП.Товар = ТекСтр.Товар;
				ДвижениеП.Склад = Склад;
				ДвижениеП.СтавкаНДС = ТекСтр.СтавкаНДС;
				ДвижениеП.Партия = ТекСтр.Партия ;
				ДвижениеП.Фирма= Фирма;
				ДвижениеП.ВидОперации=Перечисления.ВидыОпераций.Инвентаризация;
				ДвижениеП.СуммаЗакупСНДС = ТекСтр.СуммаЗакупРазница;
				ДвижениеП.СуммаРознСНДС = ТекСтр.СуммаРознРазница;
				ДвижениеП.СуммаНДСЗакуп = ТекСтр.СуммаНДСЗакупРазница;
				ДвижениеП.СуммаРознНДС = ТекСтр.СуммаНДСРознРазница;
				
			КонецЕсли;	
				
		
	КонецЦикла;
	// записываем движения регистров
	//Движения.ПартииЖНВЛС.Записать();

	
	//----------------------------< По регитсру учета по ставке НДС >--------------------------------GtG---
	ТЗПодСвертку = ТОвар.Выгрузить();
	
	ТЗПодСвертку.Свернуть("СтавкаНДС","СуммаЗакупРазница,СуммаНДСЗакупРазница,СуммаРознРазница,СуммаНДСРознРазница");
	
	Для Каждого ТекСтр Из ТЗПодСвертку Цикл

		Движение = Движения.ОстаткиПоСтНДСПоСкладам.Добавить();
		Движение.ВидДвижения =ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.СтавкаНДС = ТекСтр.СтавкаНДС;
		Движение.Фирма= Фирма;
		Движение.ВидОперации=Перечисления.ВидыОпераций.Инвентаризация;

		Движение.СуммаЗакупСНДС = ТекСтр.СуммаЗакупРазница;
		Движение.СуммаНДСЗакуп = ТекСтр.СуммаНДСЗакупРазница;
		Движение.СуммаРознСНДС = ТекСтр.СуммаРознРазница;
		Движение.СуммаРознНДС = ТекСтр.СуммаНДСРознРазница;
	КонецЦикла;
	// записываем движения регистров
	//Движения.ОстаткиПоСтНДСПоСкладам.Записать();

	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ЗаписатьДействияВИсториюДокумента(Изменения,РежимЗаписи,ПометкаУдаления);	
	
КонецПроцедуры
