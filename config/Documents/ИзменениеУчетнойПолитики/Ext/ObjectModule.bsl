
Процедура ПодготовитьТаблицыДвижений(ТаблицыДвижений)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("СкладКод",Склад.Код);
	Запрос.УстановитьПараметр("ФирмаКод",Фирма.Код);
	Запрос.УстановитьПараметр("Склад",Склад);
	
	Запрос.УстановитьПараметр("КачествоТовараПорядок",Перечисления.УЗ_КачествоТовара.Индекс(Перечисления.УЗ_КачествоТовара.ХорошийТовар));
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	&СкладКод КАК СкладКод,
	               |	&ФирмаКод КАК ФирмаКод,
	               |	ТЧТовар.ВидПоступления КАК ВидПоступленияПорядок,
	               |	&КачествоТовараПорядок КАК КачествоТовараПорядок,
	               |	ТЧТовар.КодПартии КАК ПартияКод,
	               |	ТЧТовар.КодТовара КАК ТоварКод,
	               |	ТЧТовар.КоличествоОсновное КАК Количество,
	               |	ТЧТовар.Коэфф,
	               |	ТЧТовар.СтавкаНДСЗакуп,
	               |	ТЧТовар.СуммаЗакупБезНДСРасчет / ТЧТовар.Количество * ТЧТовар.КоличествоОсновное КАК СуммаЗакупБезНДС,
	               |	ТЧТовар.СуммаОкругления,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	&Дата КАК Период
	               |ИЗ
	               |	Документ.ИзменениеУчетнойПолитики.Товар КАК ТЧТовар
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |	И ТЧТовар.КоличествоОсновное <> 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&СкладКод КАК СкладКод,
	               |	&ФирмаКод КАК ФирмаКод,
	               |	ТЧТовар.ВидПоступления КАК ВидПоступленияПорядок,
	               |	&КачествоТовараПорядок КАК КачествоТовараПорядок,
	               |	ТЧТовар.КодПартии КАК ПартияКод,
	               |	ТЧТовар.КодТовара КАК ТоварКод,
	               |	ТЧТовар.КоличествоАрбитраж КАК Количество,
	               |	ТЧТовар.Коэфф,
	               |	ТЧТовар.СтавкаНДСЗакуп,
	               |	ТЧТовар.СуммаЗакупБезНДСРасчет / ТЧТовар.Количество * ТЧТовар.КоличествоАрбитраж КАК СуммаЗакупБезНДС,
	               |	ТЧТовар.СуммаОкругления,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	&Дата КАК Период
	               |ИЗ
	               |	Документ.ИзменениеУчетнойПолитики.Товар КАК ТЧТовар
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |	И ТЧТовар.КоличествоАрбитраж <> 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&СкладКод КАК СкладКод,
	               |	&ФирмаКод КАК ФирмаКод,
	               |	ТЧТовар.ВидПоступления КАК ВидПоступленияПорядок,
	               |	&КачествоТовараПорядок КАК КачествоТовараПорядок,
	               |	ТЧТовар.СтавкаНДСЗакуп КАК СтавкаНДС,
	               |	СУММА(ТЧТовар.СуммаЗакупБезНДСРасчет) КАК СуммаЗакупБезНДС,
	               |	СУММА(ТЧТовар.СуммаОкругления) КАК СуммаОкругления,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	&Дата КАК Период
	               |ИЗ
	               |	Документ.ИзменениеУчетнойПолитики.Товар КАК ТЧТовар
	               |ГДЕ
	               |	ТЧТовар.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТЧТовар.ВидПоступления,
	               |	ТЧТовар.СтавкаНДСЗакуп";
				   
			Результат = Запрос.ВыполнитьПакет();	   
			ТаблицыДвижений.Вставить("УЗ_Партии",				                        Результат[0].Выгрузить());
			ТаблицыДвижений.Вставить("УЗ_ПартииАрбитраж",		                        Результат[1].Выгрузить());
			ТаблицыДвижений.Вставить("УЗ_ТоварныйОтчет",			                    Результат[2].Выгрузить());
				   
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	 
	 
	
	ТаблицыДвижений = Новый Структура();
	ПодготовитьТаблицыДвижений(ТаблицыДвижений);
		
	
	Движения.УЗ_Партии.Записывать = Истина;
	Движения.УЗ_Партии.Загрузить(ТаблицыДвижений.УЗ_Партии);	
	
	Движения.УЗ_ПартииАрбитраж.Записывать = Истина;
	Движения.УЗ_ПартииАрбитраж.Загрузить(ТаблицыДвижений.УЗ_ПартииАрбитраж);	
	
	Движения.УЗ_ТоварныйОтчет.Записывать = Истина;
	Движения.УЗ_ТоварныйОтчет.Загрузить(ТаблицыДвижений.УЗ_ТоварныйОтчет);
	
	
	
КонецПроцедуры

