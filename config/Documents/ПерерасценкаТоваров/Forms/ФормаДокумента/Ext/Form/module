
Процедура РасценитьРасценить(Кнопка)
	ОМ6_КоманднаяПанельКнопкаРасценить(ЭтаФорма,ЭтотОбъект,Товар,Ложь);// не обновляя данные по партиям
	
	
	
	СтрИзм=Изменения.Добавить();
	СтрИзм.Дата=ТекущаяДата();
	СтрИзм.КомментарийИзменения="Расценен.";
	СтрИзм.Сотрудник=ПараметрыСеанса.ТекущийСотр;
	СтрИзм.ТипИзм=Перечисления.ДействияНадДокументами.Расценка;
	ПересчитатьПредварительныйРезультат();
КонецПроцедуры

Процедура КоманднаяПанель1ОтобратьТоварНаПереоценку(Кнопка)
	
	Если Склад.Пустая()=Истина Тогда
		Предупреждение("Не указан склад!");
		Возврат;
	КонецЕсли;	
	
	
	Если Товар.Количество()>0 Тогда
		Товар.Очистить();
		
		СтрИзм=Изменения.Добавить();
		СтрИзм.Дата=ТекущаяДата();
		СтрИзм.КомментарийИзменения="Очищена таблица товаров на перерасценку!";
		СтрИзм.Сотрудник=ПараметрыСеанса.ТекущийСотр;
		СтрИзм.ТипИзм=Перечисления.ДействияНадДокументами.ИзмТабЧасть;
	КонецЕсли;
	
	Построитель.Параметры.Очистить();
	Построитель.Параметры.Вставить("Дата",ТекущаяДата());
	Построитель.Параметры.Вставить("Склад",Склад);
	Построитель.Параметры.Вставить("СубъектРФ",Склад.СубъектРФ);

	
	Построитель.Выполнить();
	
	
	РезОтбора=Построитель.Результат.Выгрузить();
	
	Для Каждого стр Из РезОтбора Цикл
		
		СтрТовара=Товар.Добавить();
		СтрТовара.Товар=Стр.Товар;
		СтрТовара.Партия=Стр.Партия;
		СтрТовара.СтавкаНДС=Стр.СтавкаНДС;
		СтрТОвара.Баркод=Стр.БарКод;
		СтрТОвара.ЕИТ=Стр.ЕдиницаПоУмолчанию;
		СтрТовара.Коэфф=Стр.ЕдиницаПоУмолчаниюК;
		
		Колво= (Стр.КолвоОстаток/Стр.ЕдиницаПоУмолчаниюК);
		
		Если Цел(Колво)<>Колво тогда
			Колво=Цел(Колво)+1;
		КонецЕсли;	
		
		СтрТовара.КоличествоФакт=Колво;		
		
		СтрТовара.ЦенаГосРегистрации=Стр.ПартияЦенаГосРегистрации;
		СтрТовара.Серия=стр.Серия;
		
		Если Стр.ЕИТЗакупкиК=стр.ЕдиницаПоУмолчаниюК и Стр.ЦенаЗакупПартии>0 Тогда
			СтрТовара.ЦенаЗакупБезНДС=Стр.ЦенаЗакупПартии - ОМ3_НДСИзСуммыПоСтавке(Стр.ЦенаЗакупПартии,Стр.СтавкаНДС);
			СтрТовара.ЦенаЗакуп=Стр.ЦенаЗакупПартии;
		Иначе
			ЦенаЗакупСНДС=Стр.СуммаЗакупСНДСОстаток/стр.КолвоОстаток* стр.ЕдиницаПоУмолчаниюК;
			СтрТовара.ЦенаЗакупБезНДС=ЦенаЗакупСНДС - ОМ3_НДСИзСуммыПоСтавке(ЦенаЗакупСНДС,Стр.СтавкаНДС);
			СтрТовара.ЦенаЗакуп=ЦенаЗакупСНДС;
		КонецЕсли;	
		
		СтрТовара.ЦенаПроизводителя=Стр.ЦенаПроизводителяБезНДС;
		СтрТовара.Сертификат=Стр.Сертификат;
		СтрТовара.НомерГТД=Стр.НомерГТД;
		СтрТовара.СтараяЦенаРозн=Стр.СуммаРознСНДСОстаток/Стр.КолвоОстаток*Стр.ЕдиницаПоУмолчаниюК;
		СтрТовара.ФиксЦена = стр.ФиксЦена;
		
	КонецЦикла;	
	
	СтрИзм=Изменения.Добавить();
	СтрИзм.Дата=ТекущаяДата();
	СтрИзм.КомментарийИзменения="Отобран товар на перерасценку "+Товар.Количество()+" строк";
	СтрИзм.Сотрудник=ПараметрыСеанса.ТекущийСотр;
	СтрИзм.ТипИзм=Перечисления.ДействияНадДокументами.ИзмТабЧасть;
	
	
КонецПроцедуры

Процедура РасценитьСоздатьРаспоряженияНаПереоценку(Кнопка)
	
	
	Х=0;
	
	Док=Документы.РаспоряжениеНаПереоценку.СоздатьДокумент();
	Док.Дата=ТекущаяДата();
	Док.Комментарий=Комментарий;
	Док.Склад=Склад;
	
	
	
	
	Для Каждого Стр Из ТОвар Цикл
		
		СТ=Док.Товар.Добавить();
		
		СТ.Товар=Стр.Товар;
		СТ.ЕИТ=Стр.ЕИТ;
		СТ.К=Стр.Коэфф;
		СТ.Количество=Стр.КоличествоФакт;
		СТ.НоваяЦена=Стр.ЦенаРозн;
		СТ.СтараяЦена=Стр.СтараяЦенаРозн;
		СТ.Серия=Стр.Серия;
		СТ.Партия=Стр.Партия;

	    Х=Х+1;
		
		Если Х%200=0 Тогда
			
			Док.Записать(РежимЗаписиДокумента.Запись);
			ДОк.НомерДокументаАптеки="ПРР"+Формат(Док.Номер,"ЧЦ=7; ЧВН=; ЧГ=");
			
		    Док.Записать(РежимЗаписиДокумента.Проведение);
			
			РаспоряженияНаПереоценку.Добавить().РаспоряжениеНаПереоценку=Док.Ссылка;
			
			
			Док=Документы.РаспоряжениеНаПереоценку.СоздатьДокумент();
			Док.Дата=ТекущаяДата();
			Док.Комментарий=Комментарий;
			Док.Склад=Склад;

			
		КонецЕсли;
	КонецЦикла;
	
	Если Док.ЭтоНовый()=Истина Тогда
		  Док.Записать(РежимЗаписиДокумента.Запись);
		  ДОк.НомерДокументаАптеки="ПРР"+Формат(Док.Номер,"ЧЦ=7; ЧВН=; ЧГ=");
		  Док.Записать(РежимЗаписиДокумента.Проведение);
		  РаспоряженияНаПереоценку.Добавить().РаспоряжениеНаПереоценку=Док.Ссылка;
	  КонецЕсли;
	  
	  
	  СтрИзм=Изменения.Добавить();
	  СтрИзм.Дата=ТекущаяДата();
	  СтрИзм.КомментарийИзменения="Созданы распоряжения на переоценку "+РаспоряженияНаПереоценку.Количество()+" штук";
	  СтрИзм.Сотрудник=ПараметрыСеанса.ТекущийСотр;
	  СтрИзм.ТипИзм=Перечисления.ДействияНадДокументами.ИзмТабЧасть;
	  
	Записать(РежимЗаписиДокумента.Проведение);
	ПриИзмененииДанных();

	ЭлементыФормы.Расценить_ПанельДействий.Кнопки.СоздатьРаспоряженияНаПереоценку.Доступность=Ложь;
	Для каждого Кнопко из ЭлементыФормы.Расценить_ПанельДействий.Кнопки.ОперацииНадСпискомТоваров.Кнопки цикл
		Кнопко.Доступность=Ложь;
	КонецЦикла;
	ЭлементыФормы.Расценить_ПанельДействий.Кнопки.Расценить.Доступность=Ложь;
	ЭлементыФормы.Отбор_КомПанель.Кнопки.ОтобратьТоварНаПереоценку.Доступность=Ложь;
	ЭлементыФормы.Товар.ТолькоПросмотр=Истина;

	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Если ЭтоНовый()=Истина Тогда
		СтрИзм=Изменения.Добавить();
		СтрИзм.Дата=ТекущаяДата();
		СтрИзм.КомментарийИзменения="Начало создания документа";
		СтрИзм.Сотрудник=ПараметрыСеанса.ТекущийСотр;
		СтрИзм.ТипИзм=Перечисления.ДействияНадДокументами.ВводНового;
	ИначеЕсли РаспоряженияНаПереоценку.Количество()>0 или Проведен=Истина тогда
		ЭлементыФормы.Расценить_ПанельДействий.Кнопки.СоздатьРаспоряженияНаПереоценку.Доступность=Ложь;
		Для каждого Кнопко из ЭлементыФормы.Расценить_ПанельДействий.Кнопки.ОперацииНадСпискомТоваров.Кнопки цикл
			Кнопко.Доступность=Ложь;
		КонецЦикла;
		ЭлементыФормы.Расценить_ПанельДействий.Кнопки.Расценить.Доступность=Ложь;
		ЭлементыФормы.Отбор_КомПанель.Кнопки.ОтобратьТоварНаПереоценку.Доступность=Ложь;
		ЭтаФорма.ТолькоПросмотр=Истина;
	КонецЕсли;	
КонецПроцедуры

Процедура ПриИзмененииДанных()
	
	Если ЭтоНовый()=Ложь Тогда
		Если РаспоряженияНаПереоценку.Количество()>0 Тогда
			Запрос=Новый Запрос("ВЫБРАТЬ
			                    |	ПерерасценкаТоваровРаспоряженияНаПереоценку.НомерСтроки КАК Н,
			                    |	ПерерасценкаТоваровРаспоряженияНаПереоценку.РаспоряжениеНаПереоценку КАК Распоряжение_на_переоценку,
			                    |	ПерерасценкаТоваровРаспоряженияНаПереоценку.РаспоряжениеНаПереоценку.Проведен КАК Проведен,
			                    |	ПерерасценкаТоваровРаспоряженияНаПереоценку.РаспоряжениеНаПереоценку.ДатаВыгрузкиВАптеку КАК Дата_выгрузки_в_аптеку
			                    |ИЗ
			                    |	Документ.ПерерасценкаТоваров.РаспоряженияНаПереоценку КАК ПерерасценкаТоваровРаспоряженияНаПереоценку
			                    |ГДЕ
			                    |	ПерерасценкаТоваровРаспоряженияНаПереоценку.Ссылка = &ЭтотДокумент");
			Запрос.УстановитьПараметр("ЭтотДокумент",ЭтотОбъект.Ссылка);
			
			Рез=Запрос.Выполнить().Выгрузить();
			
			ЭлементыФормы.ХРаспоряженияНаПереоценку.Значение=Рез;
			ЭлементыФормы.ХРаспоряженияНаПереоценку.СоздатьКолонки();
			
			
			
		КонецЕсли;
	КонецЕсли;	
	
	ПересчитатьПредварительныйРезультат();
	
КонецПроцедуры

Процедура ХРаспоряженияНаПереоценкуПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
		ОформлениеСтроки.Ячейки.Проведен.ЗначениеФлажка=ОформлениеСтроки.Ячейки.Проведен.Значение;
КонецПроцедуры

Процедура Расценить_ПанельДействийУдалитьНеРасценившиеся(Кнопка)
	// Вставить содержимое обработчика.
	
	УдаляемыеСтроки=Товар.НайтиСтроки(Новый Структура("ЦенаРозн",0));
	
	Для Каждого Стр Из УдаляемыеСтроки Цикл
		Товар.Удалить(Стр);
	КонецЦикла;	
	
	УдаляемыеСтроки.Очистить();
	
	Для каждого стр из Товар Цикл
		Если Стр.СтараяЦенаРозн=Стр.ЦенаРозн ТОгда
			УдаляемыеСтроки.Добавить(Стр);
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого Стр Из УдаляемыеСтроки Цикл
		Товар.Удалить(Стр);
	КонецЦикла;	
	
	Записать(РежимЗаписиДокумента.Запись);
	
КонецПроцедуры


Процедура Расценить_ПанельДействийУдалитьГдеНОваяЦенаБольшеСтарой(Кнопка)
	 	
		
	УдаляемыеСтроки = новый массив;	
	Для каждого стр из Товар Цикл
		Если Стр.ЦенаРозн >= Стр.СтараяЦенаРозн ТОгда
			УдаляемыеСтроки.Добавить(Стр);
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого Стр Из УдаляемыеСтроки Цикл
		Товар.Удалить(Стр);
	КонецЦикла;	
	
	Записать(РежимЗаписиДокумента.Запись);

КонецПроцедуры

Процедура Расценить_ПанельДействийУдалитьГдеНоваяЦенаМеньшеСтарой(Кнопка)
		
	УдаляемыеСтроки = новый массив;	

	
	Для каждого стр из Товар Цикл
		Если Стр.ЦенаРозн <= Стр.СтараяЦенаРозн ТОгда
			УдаляемыеСтроки.Добавить(Стр);
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого Стр Из УдаляемыеСтроки Цикл
		Товар.Удалить(Стр);
	КонецЦикла;	
	
	Записать(РежимЗаписиДокумента.Запись);

КонецПроцедуры

Процедура Расценить_ПанельДействийСложнаяОбработка(Кнопка)
	// Вставить содержимое обработчика.
	Если ЭтаФорма.Модифицированность=Истина и Товар.Количество()>0 тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;	
	
	
	СОбр=ЭтотОбъект.ПолучитьФорму("СложнаяОбработка",ЭтаФорма,1);
	
	
	ТЗ=Товар.Выгрузить();
	БульТип=Новый ОписаниеТипов("Булево");
	ТЗ.Колонки.Вставить(1,"Удалить",БульТип,"Удалить",10);
	ТЗ.ЗаполнитьЗначения(Ложь,"Удалить");
	
	Собр.Открыть();
	Собр.ЭлементыФормы.ТОвар.Значение=ТЗ;
	Собр.ЭлементыФормы.ТОвар.СоздатьКолонки();
	
	СОБР.ЗаполнитьСписокДоступныхПараметров ();
	ПересчитатьПредварительныйРезультат();
КонецПроцедуры

Процедура Расценить_ПанельДействийОкруглитьДоРублей(Кнопка)
	Для Каждого Стр Из Товар Цикл
		Стр.ЦенаРозн=Цел(Стр.ЦенаРозн);
	КонецЦикла;	
КонецПроцедуры

Процедура ПересчитатьПредварительныйРезультат()
	
	//Сообщить("ПересчитатьПредварительныйРезультат");
		
	ТТ=Товар.Выгрузить();
	ТТ.Колонки.Добавить("СуммаПоСтарымЦенам");
	ТТ.Колонки.Добавить("СуммаПоНовымЦенам");

	
	
	Для Каждого Стр Из ТТ Цикл
		Стр.СуммаПоСтарымЦенам	=Стр.КоличествоФакт*Стр.СтараяЦенаРозн;
		Стр.СуммаПоНовымЦенам	=Стр.КоличествоФакт*Стр.ЦенаРозн;
		Стр.СуммаЗакуп=Стр.КоличествоФакт*Стр.ЦенаЗакуп;
	КонецЦиклА;
	
	Если ТТ.Итог("СуммаЗакуп")=0 тогда 
		возврат;
	КонецЕсли;	

		
	СуммаПоСтарымЦенам	=ТТ.Итог("СуммаПоСтарымЦенам");	
	СуммаПоНовымЦенам	=ТТ.Итог("СуммаПоНовымЦенам");		
	РезультатСумма	    =ТТ.Итог("СуммаПоНовымЦенам")-ТТ.Итог("СуммаПоСтарымЦенам");
	НаценкаПоСтарымЦенам=Окр((ТТ.Итог("СуммаПоСтарымЦенам")/ТТ.Итог("СуммаЗакуп")-1)*100,2);		
	НаценкаПоНовымЦенам	=Окр((ТТ.Итог("СуммаПоНовымЦенам")/ТТ.Итог("СуммаЗакуп")-1)*100,2);			
	РезультатПроцент	=НаценкаПоНовымЦенам-НаценкаПоСтарымЦенам;
	
	ЭФТППР=ЭлементыФормы.ТППР;
	
	ЭФТППР.Очистить();
	ЭФТППР.ОтображатьСетку=Ложь;
	ЭФТППР.ОтображатьЗаголовки=Ложь;
	ЭФТППР.ТолькоПросмотр=Истина;
	
	Макет=ЭтотОбъект.ПолучитьМакет("ПредварительныеРезультаты");
	Облать=Макет.ПолучитьОбласть("ПР");
	
	Облать.Параметры.	СуммаПоСтарымЦенам	=		СуммаПоСтарымЦенам	;
	Облать.Параметры.	НаценкаПоСтарымЦенам	=		НаценкаПоСтарымЦенам	;
	Облать.Параметры.	СуммаПоНовымЦенам	=		СуммаПоНовымЦенам	;
	Облать.Параметры.	НаценкаПоНовымЦенам	=		НаценкаПоНовымЦенам	;
	Облать.Параметры.	РезультатСумма	=		РезультатСумма	;
	Облать.Параметры.	РезультатПроцент	=		РезультатПроцент	;
	
	ЭФТППР.Вывести(Облать);
	
	
	
	
	
	
КонецПроцедуры	



Процедура КоманднаяПанель1ПересчитатьПредварительныеРезультаты(Кнопка)
	ПересчитатьПредварительныйРезультат();
КонецПроцедуры

Процедура ТоварПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПересчитатьПредварительныйРезультат();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ПересчитатьПредварительныйРезультат();
КонецПроцедуры

Процедура ПриОткрытии()
	
		
КонецПроцедуры

Процедура Расценить_ПанельДействийкнПодтянутьФиксЦены(Кнопка)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ФиксированныеЦены.Товар,
	               |	ФиксированныеЦены.Цена
	               |ИЗ
	               |	РегистрСведений.ФиксированныеЦены.СрезПоследних КАК ФиксированныеЦены
	               |ГДЕ
	               |	ФиксированныеЦены.Товар В(&Товары)
	               |	"; //
				   
	Запрос.УстановитьПараметр("СубъектРФ",Склад.СубъектРФ);
	Запрос.УстановитьПараметр("Товары",Товар.ВыгрузитьКолонку("Товар"));
	Рез = Запрос.Выполнить();
	Выборка = Рез.Выбрать();
	Пока Выборка.Следующий() Цикл
		Отбор = Новый Структура();
		Отбор.Вставить("Товар",Выборка.Товар);
		НайденныеСтроки = Товар.НайтиСтроки(Отбор);	
		Для каждого стрТЧ из НайденныеСтроки Цикл
			стрТЧ.ФиксЦена = Выборка.Цена;	
		КонецЦикла;
	КонецЦикла;
	
	
	
КонецПроцедуры






Построитель.Текст="ВЫБРАТЬ
                  |	ПартииЖНВЛСОстатки.Товар КАК Товар,
                  |	ПартииЖНВЛСОстатки.Партия КАК Партия,
                  |	ПартииЖНВЛСОстатки.СтавкаНДС КАК СтавкаНДС,
                  |	ПартииЖНВЛСОстатки.КолвоОстаток КАК КолвоОстаток,
                  |	ПартииЖНВЛСОстатки.СуммаЗакупСНДСОстаток КАК СуммаЗакупСНДСОстаток,
                  |	ПартииЖНВЛСОстатки.СуммаНДСЗакупОстаток КАК СуммаНДСЗакупОстаток,
                  |	ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток КАК СуммаРознСНДСОстаток,
                  |	ПартииЖНВЛСОстатки.СуммаРознНДСОстаток КАК СуммаРознНДСОстаток,
                  |	ПартииЖНВЛСОстатки.Товар.ЕдиницаПоУмолчанию КАК ЕдиницаПоУмолчанию,
                  |	ПартииЖНВЛСОстатки.Товар.ЕдиницаПоУмолчанию.К КАК ЕдиницаПоУмолчаниюК,
                  |	ПартииЖНВЛСОстатки.Товар.ЕдиницаМин КАК ЕдиницаМин,
                  |	ПартииЖНВЛСОстатки.Товар.ЕдиницаМин.К КАК ЕдиницаМинК,
                  |	ПартииЖНВЛСОстатки.Партия.ЦенаГосРегистрации КАК ПартияЦенаГосРегистрации,
                  |	ПартииЖНВЛСОстатки.Партия.ЦенаПроизводителяБезНДС КАК ЦенаПроизводителяБезНДС,
                  |	ПартииЖНВЛСОстатки.Партия.Серия КАК Серия,
                  |	ПартииЖНВЛСОстатки.Партия.Серия.Сертификат КАК Сертификат,
                  |	ПартииЖНВЛСОстатки.Партия.БарКод КАК БарКод,
                  |	ПартииЖНВЛСОстатки.Партия.НомерГТД КАК НомерГТД,
                  |	ПартииЖНВЛСОстатки.Партия.ЦенаЗакуп КАК ЦенаЗакупПартии,
                  |	ПартииЖНВЛСОстатки.Партия.ЕИТЗакупки КАК ЕИТЗакупки,
                  |	ПартииЖНВЛСОстатки.Партия.К КАК ЕИТЗакупкиК,
                  |	ВЫРАЗИТЬ(ВЫБОР
                  |			КОГДА ПартииЖНВЛСОстатки.КолвоОстаток > 0
                  |				ТОГДА (ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток / ПартииЖНВЛСОстатки.КолвоОстаток / (ПартииЖНВЛСОстатки.СуммаЗакупСНДСОстаток / ПартииЖНВЛСОстатки.КолвоОстаток) - 1) * 100
                  |			ИНАЧЕ 0
                  |		КОНЕЦ КАК ЧИСЛО(10, 2)) КАК Процент_наценки,
                  |	ЕСТЬNULL(ФиксЦены.Цена, 0) КАК ФиксЦена
                  |ИЗ
                  |	РегистрНакопления.ПартииЖНВЛС.Остатки(&Дата, Склад = &Склад {(Партия).*, (Товар).*}) КАК ПартииЖНВЛСОстатки
                  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФиксированныеЦены.СрезПоследних КАК ФиксЦены
                  |		ПО (ФиксЦены.Товар = ПартииЖНВЛСОстатки.Товар)
                  |			
                  |ГДЕ
                  |	ПартииЖНВЛСОстатки.КолвоОстаток > 0
                  |	И ПартииЖНВЛСОстатки.СуммаЗакупСНДСОстаток > 0
                  |	И ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток > 0
                  |{ГДЕ
                  |	ПартииЖНВЛСОстатки.Товар.*,
                  |	ПартииЖНВЛСОстатки.Партия.*,
                  |	(ВЫРАЗИТЬ(ВЫБОР
                  |				КОГДА ПартииЖНВЛСОстатки.КолвоОстаток > 0
                  |					ТОГДА (ПартииЖНВЛСОстатки.СуммаРознСНДСОстаток / ПартииЖНВЛСОстатки.КолвоОстаток / (ПартииЖНВЛСОстатки.СуммаЗакупСНДСОстаток / ПартииЖНВЛСОстатки.КолвоОстаток) - 1) * 100
                  |				ИНАЧЕ 0
                  |			КОНЕЦ КАК ЧИСЛО(10, 2))) КАК Процент_наценки,
                  |	ПартииЖНВЛСОстатки.КолвоОстаток КАК Остаток,
                  |	(ЕСТЬNULL(ФиксЦены.Цена, 0)) КАК ФиксЦена}";

