
Процедура ГруппыТоваровПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = ЭлементыФормы.ГруппыТоваров.ТекущаяСтрока;
	
	Отбор1 = ЭлементыФормы.СоставГруппТоваров.ОтборСтрок.КодГруппыТоваров;
	Отбор2 = ЭлементыФормы.Показатели.ОтборСтрок.КодГруппыТоваров;
	
	Если ТекСтрока = Неопределено Тогда
		Отбор1.Значение = 0;
		Отбор2.Значение = 0;
		
	Иначе
		КодГруппыТоваровОтбор = ТекСтрока.Код;
		
		Отбор1.Значение = КодГруппыТоваровОтбор;
		Отбор2.Значение = КодГруппыТоваровОтбор;
		
		ЭлементыФормы.СоставПоказатели.Доступность = Истина;
		
	КонецЕсли;
	
	Отбор1.Использование = Истина;
	Отбор2.Использование = Истина;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	МаксКодГруппыТоваров = 0;
	Для Каждого ТекСтр Из ГруппыТоваров Цикл
		Если МаксКодГруппыТоваров < ТекСтр.Код Тогда
			МаксКодГруппыТоваров = ТекСтр.Код;
		КонецЕсли;		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ГруппыТоваровПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		МаксКодГруппыТоваров = МаксКодГруппыТоваров + 1;
		Элемент.ТекущаяСтрока.Код = МаксКодГруппыТоваров;
	КонецЕсли;
	
КонецПроцедуры

Процедура ГруппыТоваровПередУдалением(Элемент, Отказ)
	
	Отбор = Новый Структура;
	Отбор.Вставить("КодГруппыТоваров", КодГруппыТоваровОтбор);
	
	НайденныеСтроки = СоставГруппТоваров.НайтиСтроки(Отбор); 	                	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		СоставГруппТоваров.Удалить(НайденнаяСтрока);		
	КонецЦикла;
	
	НайденныеСтроки = Показатели.НайтиСтроки(Отбор); 	                	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		Показатели.Удалить(НайденнаяСтрока);		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодобратьТовар(Кнопка)
	
	Отбор = Новый Структура;
	Отбор.Вставить("КодГруппыТоваров", КодГруппыТоваровОтбор);	
	НайденныеСтроки = СоставГруппТоваров.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда		
		Режим = РежимДиалогаВопрос.ДаНет;
		Ответ = Вопрос("Очистить существующие строки?", Режим, 0);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				СоставГруппТоваров.Удалить(НайденнаяСтрока);		
			КонецЦикла;			
		КонецЕсли;		
	КонецЕсли;
	           		
	Обработка = Обработки.ПодборТоваровВТабличнуюЧасть.Создать();
	ФормаОбработки = Обработка.ПолучитьФорму("Форма", , Новый УникальныйИдентификатор);
	МассивПодобранныхТоваров = ФормаОбработки.ОткрытьМодально();
	
	Если МассивПодобранныхТоваров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекТовар Из МассивПодобранныхТоваров Цикл		
		НовСтрока = СоставГруппТоваров.Добавить();
		НовСтрока.КодГруппыТоваров = КодГруппыТоваровОтбор;
		НовСтрока.Товар = ТекТовар;
	КонецЦикла;
	
	
	УдалитьДублиПоТоварам();
	
КонецПроцедуры

Процедура КоманднаяПанель3ЗагрузитьИзФайла(Кнопка)
	
	Отбор = Новый Структура;
	Отбор.Вставить("КодГруппыТоваров", КодГруппыТоваровОтбор);	
	НайденныеСтроки = Показатели.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда		
		Режим = РежимДиалогаВопрос.ДаНет;
		Ответ = Вопрос("Очистить существующие строки?", Режим, 0);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				Показатели.Удалить(НайденнаяСтрока);		
			КонецЦикла;			
		КонецЕсли;		
	КонецЕсли;
	
	Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗагрузкиПоказателей");
	ФайлСпискаТоваров = Форма.ОткрытьМодально();
	
	Если Не ЗначениеЗаполнено(ФайлСпискаТоваров) Тогда
		Сообщить("Не указан путь к файлу!");
		Возврат;
	КонецЕсли;
	
	ВыбФайл = Новый Файл(ФайлСпискаТоваров); 	
	Если Не ВыбФайл.Существует() Тогда
		Сообщить("Не найден файл " + ФайлСпискаТоваров);
		Возврат;
	КонецЕсли;
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");	
	Исключение
		Сообщить ("Не удалось запустить Excel");
		Возврат;
	КонецПопытки;
  
	Книга = Excel.Workbooks.Open(ФайлСпискаТоваров);	
	Лист = Книга.WorkSheets(1);	
	ВсегоСтрок = Лист.Cells(1,1).SpecialCells(11).Row;
	
	Для Строка = 2 По ВсегоСтрок Цикл
		
		ОбработкаПрерыванияПользователя();
		
		Поле1 = Лист.Cells(Строка,1).Value;
		Поле2 = Лист.Cells(Строка,2).Value;
		 		
		КодАптеки = ОМ_ТСО.УбратьНечисловыеСимволы(Поле1);		
		Аптека = Справочники.МестаХранения.НайтиПоКоду(КодАптеки);		
		Если Не ЗначениеЗаполнено(Аптека) Тогда			
			Сообщить("Не найдена аптека по коду: " + КодАптеки);
			Продолжить; 			
		КонецЕсли;
		
		КолвоПродаж = ОМ_ТСО.ПолучитьЧислоИзСтроки(Поле2);
		             		
		НовСтр = Показатели.Добавить();
		НовСтр.КодГруппыТоваров = КодГруппыТоваровОтбор;
		НовСтр.Аптека = Аптека;
		НовСтр.КолвоПродаж = КолвоПродаж;
		
	КонецЦикла;
	
	Excel.Application.Quit();
	
	
	УдалитьДублиПоАптекам();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыкнВыгрузить(Кнопка)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Вопрос("Вы действительно хотите выгрузить документ в аптеки?", Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьВАптеку();
	
	
КонецПроцедуры

Процедура СоставГруппТоваровПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Если Копирование Тогда
		Предупреждение("Копирование строк запрещено", 10);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказателиПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Если Копирование Тогда
		Предупреждение("Копирование строк запрещено", 10);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоставГруппТоваровТоварПриИзменении(Элемент)
	
	ТекСтрока = ЭлементыФормы.СоставГруппТоваров.ТекущаяСтрока;
	
	Отбор = Новый Структура;
	Отбор.Вставить("КодГруппыТоваров", ТекСтрока.КодГруппыТоваров);
	Отбор.Вставить("Товар", ТекСтрока.Товар);
	
	НайденныеСтроки = СоставГруппТоваров.НайтиСтроки(Отбор);	
	Если НайденныеСтроки.Количество() > 1 Тогда
		НаимТовара = ТекСтрока.Товар.Наименование;
		ТекСтрока.Товар = Справочники.АССОРТИМЕНТНЫЙ_ПЛАН.ПустаяСсылка();
		Предупреждение("Дублирование строки по товару " + НаимТовара + ". Изменения не сохранены!");		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказателиАптекаПриИзменении(Элемент)
	
	ТекСтрока = ЭлементыФормы.Показатели.ТекущаяСтрока;
	
	Отбор = Новый Структура;
	Отбор.Вставить("КодГруппыТоваров", ТекСтрока.КодГруппыТоваров);
	Отбор.Вставить("Аптека", ТекСтрока.Аптека);
	
	НайденныеСтроки = Показатели.НайтиСтроки(Отбор);	
	Если НайденныеСтроки.Количество() > 1 Тогда
		НаимАптеки = ТекСтрока.Аптека.Наименование;
		ТекСтрока.Аптека = Справочники.МестаХранения.ПустаяСсылка();
		Предупреждение("Дублирование строки по аптеке " + НаимАптеки + ". Изменения не сохранены!");
	КонецЕсли;
	
КонецПроцедуры

Процедура СоставГруппТоваровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущаяСтрока.КодГруппыТоваров = КодГруппыТоваровОтбор;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказателиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущаяСтрока.КодГруппыТоваров = КодГруппыТоваровОтбор;
	КонецЕсли;
	
КонецПроцедуры
