
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЭтотОбъект.Статус <> Перечисления.СтатусыРаспоряженийНаПеремещение.Создан Тогда
		Сообщить("Документ был ранее выгружен в ""Личный кабинет"". Изменения не сохранены.", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПоказатьИсторию()
	
	ИсторияИзменений.Очистить();
	
	ТЗИстории = ОбщегоНазначения.ПолучитьИсториюИзмененийДокумента(Ссылка);
	Если НЕ ТЗИстории = Неопределено Тогда
		ЭлементыФормы.ИсторияИзменений.Значение = ТЗИстории;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПанельШапкиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элемент.ТекущаяСтраница = Элемент.Страницы.Изменения Тогда
		ПоказатьИсторию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыУтвердитьВЛичномКабинете(Кнопка)
	
	Если Не ЗначениеЗаполнено(ВидЭкспедиции) Тогда
		Сообщить("Поле ""Вид экспедиции"" не заполнено", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Успешно = ИзменитьСтатусВЛичномКабинете(Истина);
	
	Если Успешно Тогда		
		Статус = Перечисления.СтатусыРаспоряженийНаПеремещение.Подтвержден;	
		Записать(РежимЗаписиДокумента.Запись);
		
		УстановитьДоступностьЭлементовФормы();
		
	Иначе
		Сообщить("Ошибка записи. Статус заявки в ""Личном кабинете"" не изменен!");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыАннулироватьВЛичномКабинете(Кнопка)
	
	Разрешить = Ложь;
	Если РольДоступна("АннулированиеЗаявокНаПеремещение") Тогда		
		Разрешить = Истина;
		
	Иначе		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИсторияИзмененийДокументов.Пользователь
		|ИЗ
		|	Справочник.ИсторияИзмененийДокументов КАК ИсторияИзмененийДокументов
		|ГДЕ
		|	ИсторияИзмененийДокументов.Объект = &Объект
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсторияИзмененийДокументов.ДатаИзменения";
		
		Запрос.УстановитьПараметр("Объект", ЭтотОбъект.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Если Выборка.Пользователь = ПараметрыСеанса.ТекущийСотр Тогда
				Разрешить = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;  		
	
	Если Не Разрешить Тогда
		Сообщить("Нет прав на аннулирование документа", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Успешно = ИзменитьСтатусВЛичномКабинете(Ложь);
	
	Если Успешно Тогда		
		Статус = Перечисления.СтатусыРаспоряженийНаПеремещение.Отклонен;
		Проведен = Ложь;
		ПометкаУдаления = Истина;
		Записать(РежимЗаписиДокумента.Запись);
		ЭтаФорма.Закрыть();
	Иначе
		Сообщить("Ошибка записи. Статус заявки в ""Личном кабинете"" не изменен!");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЭтотОбъект.Статус <> Перечисления.СтатусыРаспоряженийНаПеремещение.Создан Тогда
		Предупреждение("Распоряжение выгружено в ""Личный кабинет""! Не редактируется.", 5);
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы();
		
КонецПроцедуры

Процедура УстановитьДоступностьЭлементовФормы()

	ДоступностьРегистрирования = ЭтотОбъект.Статус = Перечисления.СтатусыРаспоряженийНаПеремещение.Создан;
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ЗарегистрироватьВЛичномКабинете.Доступность = ДоступностьРегистрирования;
	
	ДоступностьУтверждения = ЭтотОбъект.Статус = Перечисления.СтатусыРаспоряженийНаПеремещение.Собран;
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.УтвердитьВЛичномКабинете.Доступность = ДоступностьУтверждения;
	
	ДоступностьАннулирования = ЭтотОбъект.Статус = Перечисления.СтатусыРаспоряженийНаПеремещение.Зарегистрирован;
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.АннулироватьВЛичномКабинете.Доступность = ДоступностьАннулирования;
	
	ЭлементыФормы.Товар.ТолькоПросмотр = Не ДоступностьРегистрирования;

КонецПроцедуры

Процедура ОсновныеДействияФормыЗарегистрироватьВЛичномКабинете(Кнопка)
	
	Если СозданВручную Тогда
		Сообщить("Действие ""Зарегистрировать в ЛК"" не доступно для заявок, изначально созданных в Личном кабинете", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если Не Проведен Тогда
		Сообщить("Необходимо предварительно провести документ", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Вопрос("Документ будет выгружен в ""Личный кабинет"" и отправлен письмом аптеке. Продолжить?", Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗарегистрироватьРаспоряжение();
	
	Статус = Перечисления.СтатусыРаспоряженийНаПеремещение.Зарегистрирован;	
	Записать(РежимЗаписиДокумента.Запись);
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры
