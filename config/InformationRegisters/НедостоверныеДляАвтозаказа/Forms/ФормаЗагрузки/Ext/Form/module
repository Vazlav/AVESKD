
Процедура ПолеВвода1НачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ВыборФайла(Элемент);
	
КонецПроцедуры

Процедура ВыборФайла(Элемент)

	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	ДиалогВыбораФайла.Фильтр                  = "Файл Microsoft Excel (*.xls*)|*.xls*";
	ДиалогВыбораФайла.Заголовок               = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;		
	ДиалогВыбораФайла.ИндексФильтра           = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла          = Элемент.Значение;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;

	Если ДиалогВыбораФайла.Выбрать() Тогда
		Элемент.Значение = ДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьЗагрузку()
	
	Если Не ЗначениеЗаполнено(ПутьКФайлу) Тогда
		Сообщить("Не указан путь к файлу списка товаров!");
		Возврат;
	КонецЕсли;
	
	ВыбФайл = Новый Файл(ПутьКФайлу); 	
	Если Не ВыбФайл.Существует() Тогда
		Сообщить("Не найден файл " + ПутьКФайлу);
		Возврат;
	КонецЕсли;
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");	
	Исключение
		Сообщить ("Не удалось запустить Excel");
		Возврат;
	КонецПопытки;
  
	Книга = Excel.Workbooks.Open(ПутьКФайлу);	
	Лист = Книга.WorkSheets(1);	
	ВсегоСтрок = Лист.Cells(1,1).SpecialCells(11).Row;
	
	НаборЗаписей = РегистрыСведений.НедостоверныеДляАвтозаказа.СоздатьНаборЗаписей();
	ДатаБлокировки = ТекущаяДата();
	
	Для Строка = 2 По ВсегоСтрок Цикл 
		
		КодТовара = УбратьЛишниеСимволы(СокрЛП(Лист.Cells(Строка,1).Value));
		КодАптеки = УбратьЛишниеСимволы(СокрЛП(Лист.Cells(Строка,2).Value));
		Количество = ПолучитьЧисло(Лист.Cells(Строка,3).Value);
		
		Товар = Справочники.АССОРТИМЕНТНЫЙ_ПЛАН.НайтиПоКоду(КодТовара);
		Аптека = Справочники.МестаХранения.НайтиПоКоду(КодАптеки);
		
		Если ЗначениеЗаполнено(Товар) Тогда
			
			Если ЗначениеЗаполнено(Аптека) Тогда
				
				НоваяЗапись = НаборЗаписей.Добавить();
				
				НоваяЗапись.Аптека			= Аптека;
				НоваяЗапись.Товар			= Товар;
				НоваяЗапись.ДатаБлокировки	= ДатаБлокировки;
				
			Иначе
				Сообщить("Не найдена аптека по коду: " + КодАптеки);			
			КонецЕсли;
			
		Иначе
			Сообщить("Не найден товар по коду: " + КодТовара);			
		КонецЕсли;
		
	КонецЦикла;
	
	Excel.Application.Quit();
	
	НаборЗаписей.Записать(Ложь);
	
	Закрыть(1);
	
КонецПроцедуры

Функция ПолучитьЧисло(ИсходнаяСтрока)
	
	Если ТипЗнч(ИсходнаяСтрока) = Тип("Число") Тогда
		Возврат ИсходнаяСтрока;
	ИначеЕсли ТипЗнч(ИсходнаяСтрока) = Тип("Строка") Тогда
		ИсходнаяСтрока = СокрЛП(ИсходнаяСтрока);
		Если ЗначениеЗаполнено(ИсходнаяСтрока) Тогда
			Возврат Число(ИсходнаяСтрока);
		Иначе
			Возврат 0;
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции
   
Функция УбратьЛишниеСимволы(ИсходнаяСтрока) Экспорт
	
	НовСтрока = "";
	ПравильныеСимволы = "0123456789";
	
	Для Сч = 1 по СтрДлина(ИсходнаяСтрока) Цикл
		ТекСимв = Сред(ИсходнаяСтрока, Сч, 1);
		Если Найти(ПравильныеСимволы, ТекСимв) > 0 Тогда
			НовСтрока = НовСтрока + ТекСимв;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НовСтрока;
	
КонецФункции 

Процедура КоманднаяПанель1Выполнить(Кнопка)
	
	ВыполнитьЗагрузку();
	
КонецПроцедуры


