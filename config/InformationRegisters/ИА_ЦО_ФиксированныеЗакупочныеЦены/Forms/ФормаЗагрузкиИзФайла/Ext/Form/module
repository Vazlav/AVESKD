Процедура КнопкаВыборФайлаНажатие(Элемент)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	Если Диалог.Выбрать() Тогда
    	ПутьКФайлу = Диалог.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если ПустаяСтрока(ПутьКФайлу) Тогда
    
    	Сообщить("Не указан путь к файлу!");
        Возврат
    
    КонецЕсли; 
	
	СпрРегионыИА = Справочники.ИА_ЦО_Регионы;
	СпрРегионыИА_Москва = СпрРегионыИА.НайтиПоКоду("000000001");
	Если СпрРегионыИА_Москва = Неопределено Тогда
		Сообщить("В справочнике ""Регионы(ЦО ИА)"" не обнаружен элемент ""Москва"" (код 1). Проверьте правильность заполнения справочника и повторите загрузку");
		Возврат
	КонецЕсли; 
	СпрРегионыИА_СПт = СпрРегионыИА.НайтиПоКоду("000000002");
	Если СпрРегионыИА_СПт = Неопределено Тогда
		Сообщить("В справочнике ""Регионы(ЦО ИА)"" не обнаружен элемент ""Санкт-Петербург"" (код 2). Проверьте правильность заполнения справочника и повторите загрузку");
		Возврат
	КонецЕсли; 
	
	Попытка
		
		Excel = Новый COMОбъект("Excel.Application");
		Excel.WorkBooks.Open(ПутьКФайлу);
		Состояние("Обработка файла Microsoft Excel...");
		
	Исключение
		Сообщить("Ошибка при открытии файла с помощью Excel! Загрузка не будет произведена!");
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Попытка
		//Открываем необходимый лист
		Excel.Sheets(1).Select(); // лист 1, по умолчанию
	Исключение
		//Закрываем Excel
		Excel.ActiveWorkbook.Close();
		Excel = 0;
		Сообщить("Файл "+Строка(ПутьКФайлу)+" не соответствует необходимому формату! Первый лист не найден!");
		ОтменитьТранзакцию();
		Возврат;
	КонецПопытки;
	
	//Получим количество строк и колонок.
	//В разных версиях Excel получаются по-разному, поэтому сначала определим версию Excel
	Версия = Лев(Excel.Version,Найти(Excel.Version,".")-1);
	Если Версия = "8" тогда
		ФайлСтрок = Excel.Cells.CurrentRegion.Rows.Count;
		ФайлКолонок = Макс(Excel.Cells.CurrentRegion.Columns.Count, 13);
	Иначе
		ФайлСтрок = Excel.Cells(1,1).SpecialCells(11).Row;
		ФайлКолонок = Excel.Cells(1,1).SpecialCells(11).Column;
    Конецесли;
	
	СпрАП	= Справочники.АССОРТИМЕНТНЫЙ_ПЛАН;
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Товар");
	ТЗ.Колонки.Добавить("Регион");
	ТЗ.Колонки.Добавить("ФиксированнаяЦена");
	ТЗ.Колонки.Добавить("ДатаНачалаДействияЦены");
	ТЗ.Колонки.Добавить("ДатаОкончанияДействияЦены");
	ТЗ.Колонки.Добавить("Период");
	ТЗ.Колонки.Добавить("Автор");
	ТЗ.Колонки.Добавить("Комментарий");
	
	ДатаЗагрузки = ТекущаяДата();
	Автор = ПараметрыСеанса.ТекущийСотр;
	
	Для Сч = НачальнаяСтрока  По ФайлСтрок Цикл
    
		Состояние("Обработка: "+ Сч + " строк из " + ФайлСтрок);
		ОбработкаПрерыванияПользователя(); 
		
		Код1С					= Число(СтрЗаменить(Excel.Cells(Сч, 1).Text," ",""));
		НайденнаяСсылкаАП = СпрАП.НайтиПоКоду(Код1С);
		Если НайденнаяСсылкаАП = СпрАП.ПустаяСсылка() Тогда
			
			Сообщить("Не найден код 1с: "+ Код1С+", строка не загружена");
			Продолжить;
		
		КонецЕсли;
		ФиксированнаяЦена			= Число(Excel.Cells(Сч, 2).Text);
		Excel.Cells(Сч , 3).NumberFormat="ДД.ММ.ГГГГ";
		ДатаНачалаДействияЦены		= Excel.Cells(Сч, 3).Value;
		Excel.Cells(Сч , 4).NumberFormat="ДД.ММ.ГГГГ";
		ДатаОкончанияДействияЦены	= Excel.Cells(Сч, 4).Value;
		Регион						= Число(Excel.Cells(Сч, 5).Text);
		Комментарий					= Строка(Excel.Cells(Сч, 6).Text);
		
		
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Товар						= НайденнаяСсылкаАП;
		Если Регион = 1 Тогда
			НоваяСтрока.Регион					= СпрРегионыИА_Москва;
		ИначеЕсли Регион = 2 Тогда
			НоваяСтрока.Регион					= СпрРегионыИА_СПт;
		Иначе
			НоваяСтрока.Регион					= СпрРегионыИА.ПустаяСсылка();
		КонецЕсли;
		НоваяСтрока.ФиксированнаяЦена			= ФиксированнаяЦена;
		НоваяСтрока.ДатаНачалаДействияЦены		= ДатаНачалаДействияЦены;
		НоваяСтрока.ДатаОкончанияДействияЦены	= ДатаОкончанияДействияЦены;
		НоваяСтрока.Период						= ДатаЗагрузки;
 		НоваяСтрока.Автор						= Автор;
 		НоваяСтрока.Комментарий					= Комментарий;
   
    КонецЦикла; 
    
	Excel.DisplayAlerts = 0; 
	Excel.Quit();
	Excel.DisplayAlerts = 1;
	
	НаборЗаписей = РегистрыСведений.ИА_ЦО_ФиксированныеЗакупочныеЦены.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДатаЗагрузки);
	НаборЗаписей.Загрузить(ТЗ);
	Попытка
		НаборЗаписей.Записать();
	Исключение
	   Сообщить("" + ОписаниеОшибки());
   КонецПопытки;
   
	Закрыть();
    
КонецПроцедуры

Процедура ПриОткрытии()
	НачальнаяСтрока = 2;
КонецПроцедуры

Процедура ЗакрытьНажатие(Элемент)
	Закрыть()
КонецПроцедуры
