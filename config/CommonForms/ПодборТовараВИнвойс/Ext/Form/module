
Процедура ЗаполнитьТаблицуПоЗаказу()
	
		ТЗЗаказа.Очистить();
		
		Запрос = Новый Запрос;
		
		Если СпособОтображенияЗаказа = "ПолныйЗаказ" Тогда
			ТипСоединения = " ЛЕВОЕ ";
		Иначе
			ТипСоединения = " ВНУТРЕННЕЕ ";
		КонецЕсли;
		
		ТХТ = "ВЫБРАТЬ
		      |	ЗаказПроизводителюОстатки.КодТовара КАК КодТовара,
		      |	ЗаказПроизводителюОстатки.КоличествоОстаток КАК КоличествоОстаток
		      |ПОМЕСТИТЬ ТоварыЗаказа
		      |ИЗ
		      |	РегистрНакопления.ЗаказПроизводителю.Остатки(
		      |			&Дата,
		      |			КодПроизводителя = &КодПроизводителя
		      |				И КодТовара В (&КодыТовара)
		      |				И Заказ = &Основание) КАК ЗаказПроизводителюОстатки
		      |;
		      |
		      |////////////////////////////////////////////////////////////////////////////////
		      |ВЫБРАТЬ
		      |	ЗаказПроизводителюТовар.КодТовара КАК КодТовара,
		      |	ЗаказПроизводителюТовар.Товар КАК Товар,
		      |	СУММА(ЗаказПроизводителюТовар.Количество) КАК КоличествоЗаказа,
		      |	МАКСИМУМ(ЗаказПроизводителюТовар.Цена) КАК ЦенаЗаказа,
		      |	СУММА(ЗаказПроизводителюТовар.Сумма) КАК СуммаЗаказа,
		      |	МАКСИМУМ(ЕСТЬNULL(ТоварыЗаказа.КоличествоОстаток, 0)) КАК ВПути,
		      |	0 КАК КоличествоФакт,
		      |	МАКСИМУМ(ЗаказПроизводителюТовар.Цена) КАК ЦенаФакт,
		      |	ДАТАВРЕМЯ(1, 1, 1) КАК СрокГодности,
		      |	&ПустоеПодтверждение КАК ПодтверждениеЗаказа,
		      |	0 КАК ЦенаУчета,
	 		  // НДС20/18 
			  //|	ЗаказПроизводителюТовар.Товар.СтавкаНДС.Ставка КАК СтавкаНДС
			  |	ВЫБОР
			  |		КОГДА ЗаказПроизводителюТовар.Товар.СтавкаНДС.Код = 3
			  |				И &Дата < ДАТАВРЕМЯ(2019, 1, 1)
			  |			ТОГДА 18
			  |		ИНАЧЕ ЗаказПроизводителюТовар.Товар.СтавкаНДС.Ставка
			  |	КОНЕЦ КАК СтавкаНДС
		      |ИЗ
		      |	Документ.ЗаказПроизводителю.Товар КАК ЗаказПроизводителюТовар
		      |		" + ТипСоединения + " СОЕДИНЕНИЕ ТоварыЗаказа КАК ТоварыЗаказа
		      |		ПО (ТоварыЗаказа.КодТовара = ЗаказПроизводителюТовар.КодТовара)
		      |ГДЕ
		      |	ЗаказПроизводителюТовар.Ссылка = &Основание
		      |
		      |СГРУППИРОВАТЬ ПО
		      |	ЗаказПроизводителюТовар.КодТовара,
		      |	ЗаказПроизводителюТовар.Товар,
	 		// НДС20/18 
		      //|	ЗаказПроизводителюТовар.Товар.СтавкаНДС.Ставка
			  |	ВЫБОР
			  |		КОГДА ЗаказПроизводителюТовар.Товар.СтавкаНДС.Код = 3
			  |				И &Дата < ДАТАВРЕМЯ(2019, 1, 1)
			  |			ТОГДА 18
			  |		ИНАЧЕ ЗаказПроизводителюТовар.Товар.СтавкаНДС.Ставка
			  |	КОНЕЦ 	
			  |;
		      |
		      |////////////////////////////////////////////////////////////////////////////////
		      |УНИЧТОЖИТЬ ТоварыЗаказа";
		
		Запрос.Текст = ТХТ;
		Запрос.УстановитьПараметр("КодПроизводителя",Основание.Производитель.Код);
		Запрос.УстановитьПараметр("КодыТовара",Основание.Товар.ВыгрузитьКолонку("КодТовара"));
		Запрос.УстановитьПараметр("Дата",ДатаДокумента);
		Запрос.УстановитьПараметр("Основание",Основание);
		Запрос.УстановитьПараметр("ПустоеПодтверждение",Документы.ПодтверждениеЗаказа.ПустаяСсылка());

		ТЗЗаказа = Запрос.Выполнить().Выгрузить();
	
	
КонецПроцедуры


Процедура ЗаполнитьТаблицуПоПодтверждению()
	
		ТЗЗаказа.Очистить();
		
		Запрос = Новый Запрос;
		
		Если СпособОтображенияЗаказа = "ПолныйЗаказ" Тогда
			ТипСоединения = " ЛЕВОЕ ";
		Иначе
			ТипСоединения = " ВНУТРЕННЕЕ ";
		КонецЕсли;
		
		ТХТ = "ВЫБРАТЬ
		      |	ЗаказПроизводителюОстатки.КодТовара,
		      |	ЗаказПроизводителюОстатки.КоличествоОстаток,
		      |	ЗаказПроизводителюОстатки.ПодтверждениеЗаказа,
		      |	ЗаказПроизводителюОстатки.СуммаЗаказаОстаток КАК СуммаЗаказа,
		      |	ЗаказПроизводителюОстатки.СуммаЗаказаОстаток / ЗаказПроизводителюОстатки.КоличествоОстаток КАК ЦенаУчета
		      |ПОМЕСТИТЬ ТоварыВПути
		      |ИЗ
		      |	РегистрНакопления.ПодтверждениеЗаказа.Остатки(
		      |			&Дата,
		      |			КодТовара В (&КодыТовара)
		      |				И Заказ = &Основание) КАК ЗаказПроизводителюОстатки
		      |;
		      |
		      |////////////////////////////////////////////////////////////////////////////////
		      |ВЫБРАТЬ
		      |	Док.КодТовара,
		      |	Док.Товар,
		      |	Док.Количество КАК КоличествоЗаказа,
		      |	Док.ЦенаСоСкидкойСНДС КАК ЦенаЗаказа,
		      |	Док.СуммаСНДС КАК СуммаЗаказа,
	 		// НДС20/18 
			 // |	Док.СтавкаНДСПроизводителя КАК СтавкаНДС,
			  |	ВЫБОР
			  |		КОГДА Док.СтавкаНДСПроизводителя = 20
			  |				И &Дата < ДАТАВРЕМЯ(2019, 1, 1)
			  |			ТОГДА 18
			  |		ИНАЧЕ Док.СтавкаНДСПроизводителя
			  |	КОНЕЦ КАК СтавкаНДС,
		      |	0 КАК ВПути,
		      |	0 КАК КоличествоФакт,
		      |	Док.ЦенаСоСкидкойСНДС КАК ЦенаФакт,
		      |	Док.СрокГодности,
		      |	Док.Ссылка КАК ПодтверждениеЗаказа, Док.ЦенаУчета
		      |ИЗ
		      |	Документ.ПодтверждениеЗаказа.Товар КАК Док
		      |		" + ТипСоединения + " СОЕДИНЕНИЕ ТоварыВПути КАК ТоварыВПути
		      |		ПО (ТоварыВПути.ПодтверждениеЗаказа = Док.Ссылка)
		      |			И (ТоварыВПути.КодТовара = Док.КодТовара)
		      |ГДЕ
		      |	Док.Ссылка В
		      |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		      |				Выборка.ПодтверждениеЗаказа
		      |			ИЗ
		      |				ТоварыВПути КАК Выборка)
		      |;
		      |
		      |////////////////////////////////////////////////////////////////////////////////
		      |ВЫБРАТЬ
		      |	ТоварыВПути.КодТовара,
		      |	ТоварыВПути.КоличествоОстаток как ВПути,
		      |	ТоварыВПути.ПодтверждениеЗаказа,
		      |	ТоварыВПути.СуммаЗаказа
		      |ИЗ
		      |	ТоварыВПути КАК ТоварыВПути
		      |;
		      |
		      |////////////////////////////////////////////////////////////////////////////////
		      |УНИЧТОЖИТЬ ТоварыВПути";
		
		Запрос.Текст = ТХТ;
		Запрос.УстановитьПараметр("КодПроизводителя",Основание.Производитель.Код);
		Запрос.УстановитьПараметр("КодыТовара",Основание.Товар.ВыгрузитьКолонку("КодТовара"));
		Запрос.УстановитьПараметр("Дата",ДатаДокумента);
		Запрос.УстановитьПараметр("Основание",Основание);

		Рез = Запрос.ВыполнитьПакет();
		
		пер_ТЗЗаказа = Рез.Получить(1).Выгрузить();
		ТЗПути = Рез.Получить(2).Выгрузить();
		
		
		Для каждого стр из ВладелецФормы.ЭлементыФормы.Товар.Значение Цикл
				нс = ТЗЗаказа.Добавить();
			    нс.КодТовара = стр.КодТовара;
				нс.Товар = стр.Товар;
				//нс.СтавкаНДС = стр.СтавкаНДС.Ставка;
				нс.СтавкаНДС = ПереопределениеЗначенияСтавки20_18(стр.СтавкаНДС,ДатаДокумента);
				нс.КоличествоЗаказа = 0;
				нс.ВПути = 0;
				нс.ЦенаЗаказа = стр.ЦенаЗаказа;
				нс.СуммаЗаказа = 0;
				нс.КоличествоФакт = стр.Количество;
				нс.ЦенаФакт = стр.ЦенаОтпускнаяСНДС;
				нс.СрокГодности = стр.СрокГодности;
				нс.ЦенаУчета = стр.ЦенаУчета;
				нс.ПодтверждениеЗаказа = стр.ПодтверждениеЗаказа;
		КонецЦикла;
		
		
		Для каждого стр из пер_ТЗЗаказа Цикл
			Отбор = Новый Структура;
			Отбор.Вставить("КодТовара",стр.КодТовара);
			Отбор.Вставить("ЦенаЗаказа",стр.ЦенаЗаказа);
			Отбор.Вставить("ПодтверждениеЗаказа",стр.ПодтверждениеЗаказа);
			НайденныеСтроки = ТЗЗаказа.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда
				нс = ТЗЗаказа.Добавить();
			    нс.КодТовара = стр.КодТовара;
				нс.Товар = стр.Товар;
				нс.СтавкаНДС = стр.СтавкаНДС;
				нс.КоличествоЗаказа = стр.КоличествоЗаказа;
				нс.ВПути = 0;
				нс.ЦенаЗаказа = стр.ЦенаЗаказа;
				нс.СуммаЗаказа = стр.СуммаЗаказа;
				нс.КоличествоФакт = 0;
				нс.ЦенаФакт = нс.ЦенаЗаказа;
				нс.СрокГодности = стр.СрокГодности;				
				нс.ЦенаУчета = стр.ЦенаУчета;
				нс.ПодтверждениеЗаказа = стр.ПодтверждениеЗаказа;
			Иначе
				ПерваяСтрока = НайденныеСтроки[0];
				ПерваяСтрока.КоличествоЗаказа = стр.КоличествоЗаказа;
				ПерваяСтрока.ЦенаЗаказа = стр.ЦенаЗаказа;
				ПерваяСтрока.СуммаЗаказа = ПерваяСтрока.КоличествоЗаказа*ПерваяСтрока.ЦенаЗаказа;
			КонецЕсли;
				
		КонецЦикла;	
		
		
		
	
		
		

		Для каждого стр из ТЗПути Цикл
			
			ОстатокВПути = стр.ВПути;
			
			Отбор = Новый Структура;
			Отбор.Вставить("КодТовара",стр.КодТовара);
			Отбор.Вставить("ПодтверждениеЗаказа",стр.ПодтверждениеЗаказа);
			
			НайденныеСтроки = ТЗЗаказа.НайтиСтроки(Отбор);
			Для каждого с из НайденныеСтроки Цикл
				
				Если  с.КоличествоЗаказа >= ОстатокВПути Тогда
					с.ВПути = ОстатокВПути;
					Прервать;
				КонецЕсли;
				
				с.ВПути = с.КоличествоЗаказа;	
				ОстатокВПути = ОстатокВПути - с.КоличествоЗаказа;
				
				
			КонецЦикла;
			
			
		КонецЦикла;
	
	
КонецПроцедуры



Процедура ТЗЗаказаПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.КоличествоФакт > 0 Тогда
		ОформлениеСтроки.ЦветФона=Новый Цвет(152,251,152);
	КонецЕсли;
	
	Если ДанныеСтроки.ЦенаЗаказа <> ДанныеСтроки.ЦенаФакт Тогда
		ОформлениеСтроки.Ячейки.ЦенаФакт.ЦветФона = Новый Цвет(255, 0, 0);
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуЗаказа()
	
	Если Параметры.ВариантЗаполнения = "ПоЗаказу" Тогда
		ЗаполнитьТаблицуПоЗаказу();
	ИначеЕсли Параметры.ВариантЗаполнения = "ПоПодтверждению" Тогда
		ЗаполнитьТаблицуПоПодтверждению();
	Иначе
		Предупреждение("Заполнение таблицы по документам типа " + ТипЗнч(Основание) + " невозможен");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если Основание <> Неопределено И НЕ Основание.Пустая() Тогда

		ЗаполнитьТаблицуЗаказа();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыкнПеренестиВИнвойс(Кнопка)
	
	ОповеститьОВыборе(ТЗЗаказа);
	
КонецПроцедуры

Процедура ОснованиеПриИзменении(Элемент)
	
	ЗаполнитьТаблицуЗаказа();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыкнЗаполнитьДаннымиЗаказа(Кнопка)
	
	Для каждого стр из ТЗЗаказа Цикл
		Если стр.ВПути > 0 Тогда
			стр.КоличествоФакт = стр.ВПути;
			стр.ЦенаФакт = стр.ЦенаЗаказа;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СпособОтображенияЗаказаПриИзменении(Элемент)
	
	ЗаполнитьТаблицуЗаказа();
	
КонецПроцедуры

СпособОтображенияЗаказа = "ПолныйЗаказ";
